////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

//Рекурсивная процедура поиска текста
Функция НайтиЗапросыПоСтруктуреПоискаРекурсивно(СтруктураДляПоиска, Дерево, Результат = Неопределено)
	
	Если Результат = Неопределено тогда
		
		Результат = РезультатПоиска.СкопироватьКолонки();
		
		Если СтруктураДляПоиска.ИскатьВАлгоритмах Тогда
			
			Для каждого СтрокаСКодом Из ВладелецФормы.ИсполняемыйКод Цикл
				Если Найти(НРег(СтрокаСКодом.Текст), НРег(СтруктураДляПоиска.СтрокаПоиска)) > 0 тогда
					НоваяСтрока               = Результат.Добавить();
					НоваяСтрока.Тип           = 1;
					НоваяСтрока.СтрокаКода    = СтрокаСКодом.ИдентификаторСтроки;
					НоваяСтрока.ИмяКода       = СтрокаСКодом.Имя;
					
					лНайденнаяСтрокаЗапроса   = ВладелецФормы.ДеревоЗапросов.Строки.Найти(СтрокаСКодом.ИдентификаторЗапроса, "Идентификатор", Истина);
					НоваяСтрока.СтрокаЗапроса = лНайденнаяСтрокаЗапроса.Идентификатор;
					НоваяСтрока.ИмяЗапроса    = лНайденнаяСтрокаЗапроса.Имя;
				КонецЕсли;
			КонецЦикла; 
		КонецЕсли;
	КонецЕсли;

	Для каждого ТекСтрока Из Дерево.Строки Цикл
		
		ТекСтрокаПодходит = Ложь;
		
		Если СтруктураДляПоиска.ИскатьВАлгоритмах тогда
			Если Найти(НРег(ТекСтрока.ТекстЗапроса), НРег(СтруктураДляПоиска.СтрокаПоиска)) > 0 тогда
				НоваяСтрока               = Результат.Добавить();
				НоваяСтрока.Тип           = 0;
				НоваяСтрока.СтрокаЗапроса = ТекСтрока.Идентификатор;
				НоваяСтрока.ИмяЗапроса    = ТекСтрока.Имя;
				ТекСтрокаПодходит         = Истина;
			КонецЕсли;
		КонецЕсли;
		
		Если Не ТекСтрокаПодходит И СтруктураДляПоиска.ИскатьВНазваниях тогда
			Если Найти(НРег(ТекСтрока.Имя), НРег(СтруктураДляПоиска.СтрокаПоиска)) > 0 тогда
				НоваяСтрока               = Результат.Добавить();
				НоваяСтрока.Тип           = 2;
				НоваяСтрока.СтрокаЗапроса = ТекСтрока.Идентификатор;
				НоваяСтрока.ИмяЗапроса    = ТекСтрока.Имя;
			КонецЕсли;
		КонецЕсли;
		НайтиЗапросыПоСтруктуреПоискаРекурсивно(СтруктураДляПоиска, ТекСтрока, РезультатПоиска)
	КонецЦикла; 
	
	Возврат Результат;
	
КонецФункции // НайтиЗапросыПоСтруктуреПоискаРекурсивно()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПриОткрытии()
	
	Если НЕ ИскатьВАлгоритмах И НЕ ИскатьВКоде И НЕ ИскатьВНазваниях тогда 
		
		ИскатьВАлгоритмах	= Истина;
		ИскатьВКоде			= Истина;
		ИскатьВНазваниях	= Истина;
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

Процедура РезультатПоискаВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	
	Если ВыбраннаяСтрока.Тип = 0 ИЛИ ВыбраннаяСтрока.Тип = 2 тогда
		//позиционируем дерево с запросами на данной строке
		ВладелецФормы.ЭлементыФормы.ДеревоЗапросов.ТекущаяСтрока = ВладелецФормы.ДеревоЗапросов.Строки.Найти(ВыбраннаяСтрока.СтрокаЗапроса, "Идентификатор", Истина);
		ВладелецФормы.Активизировать();
	ИначеЕсли ВыбраннаяСтрока.Тип = 1 тогда
		
		//позиционируем дерево с запросами на данной строке и открываем код
		ФормаВариантовКода = ВладелецФормы.мФормаИсполняемыйКод;
		Если ФормаВариантовКода.Открыта() Тогда 
			ФормаВариантовКода.Закрыть();
		КонецЕсли;
		ВладелецФормы.ЭлементыФормы.ДеревоЗапросов.ТекущаяСтрока = ВладелецФормы.ДеревоЗапросов.Строки.Найти(ВыбраннаяСтрока.СтрокаЗапроса, "Идентификатор", Истина);
		ФормаВариантовКода.Открыть();
		ФормаВариантовКода.ЭлементыФормы.ИсполняемыйКодСписок.ТекущаяСтрока = ФормаВариантовКода.ИсполняемыйКодСписок.Найти(ВыбраннаяСтрока.СтрокаКода, "ИдентификаторСтроки");
	КонецЕсли;
	
КонецПроцедуры

Процедура НайтиНажатие(Элемент)
	ОбработкаДействияНайти();
КонецПроцедуры

Процедура СтрокаПоискаПриИзменении(Элемент)
	ОбработкаДействияНайти()
КонецПроцедуры

Процедура ОбработкаДействияНайти()
    РезультатПоиска = НайтиЗапросыПоСтруктуреПоискаРекурсивно(
		Новый Структура("ИскатьВАлгоритмах, ИскатьВКоде, СтрокаПоиска, ИскатьВНазваниях",ИскатьВАлгоритмах, ИскатьВКоде, СтрокаПоиска, ИскатьВНазваниях),
		ВладелецФормы.ДеревоЗапросов);
		
	Если РезультатПоиска.Количество() > 0 тогда
		ТекущийЭлемент = ЭлементыФормы.РезультатПоиска;
	Иначе
		Предупреждение("Поиск завершен.", 10);
	КонецЕсли;		
КонецПроцедуры // ОбработкаДействияНайти()
