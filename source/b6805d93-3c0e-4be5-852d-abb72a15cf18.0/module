//Консоль запросов (с)Евгений Лавелин
//e-mail:work@lavelin.ru
//http: www.lavelin.ru

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
 
Перем мФормаПараметров Экспорт;         // форма параметров
Перем мФормаИсполняемыйКод Экспорт;		// форма вариантов кода
Перем мФормаПоиска Экспорт;				// форма поиска
Перем мФормаПараметрыИБ Экспорт;		// форма параметров ИБ
Перем мФормаПрогрессора Экспорт;
Перем мТаблицаЗагружена;                // признак того, что рез - т запроса загружен в табличное поле
Перем мСводнаяТаблицаЗагружена;         // признак того, что рез - т запроса загружен в сводную таблицу

Перем мМассивУдаленныхСтрок;

Перем мОтменаРедактирования, мЭтотОбъектФорма;

Перем мДатаНачалаВыполнения Экспорт;
Перем мДатаОкончанияЗапроса Экспорт;

Перем РежимВыбораВеткиДерева Экспорт;

Перем АгрегатнаяФункция;

Перем ЗапросДляОтладки Экспорт; 		//Сюда перед открытием формы необходимо передать значение типа Запрос

Перем мПутьККартинкам Экспорт;

Перем мЗаголовокФормы;                  // заголовок формы
Перем мТекущаяСтрока;                   // текущая(прошлая) строка дерева запросов.

Перем мИмяРеквизитаТаблицы;

Перем мНастройкиПрокси Экспорт;

// "облачные" переменные
Перем мИмяФайла;                        // имя файла запросов 
Перем мИмяПользователяВОблаке;          // имя пользователя в облаке


/////////////////////////////////////////
// КонтекстнаяПодсказка

//ПРОЦЕДУРА ДУБЛЬ(!!!)
Процедура ОткрытьМенюIntelliSense(ПолеТекстаЗапроса)

	Если ПолеТекстаЗапроса.ВыделенныйТекст <> "" тогда
		Возврат
	Конецесли;
	
	Если Не гИнициализацияVBScript() тогда
		Возврат;	
	КонецЕсли;
	
	//Запоминаем текущие координаты курсора 
	лСтруктураКоординат = гПолучитьГраницыВыделенияПоляФормы(ПолеТекстаЗапроса);
	
	//Если позиция курсора больше длины текущей строки, то перемещаем курсор в конец строки
	лТекущаяСтрока = ПолеТекстаЗапроса.ПолучитьСтроку(лСтруктураКоординат.СтрокаНач);
	Если СтрДлина(лТекущаяСтрока) < лСтруктураКоординат.КолонкаКон тогда
		лСтруктураКоординат.КолонкаНач = СтрДлина(лТекущаяСтрока)+1;
		лСтруктураКоординат.КолонкаКон = лСтруктураКоординат.КолонкаНач;
	КонецЕсли;
	
	лПозицияКурсораВТексте = ПолучитьПозициюКурсораВТексте(ПолеТекстаЗапроса, лСтруктураКоординат);
	лДанныеДляПодбораКП    = гПолучитьДанныеДляПодбораКП(ПолеТекстаЗапроса.ПолучитьТекст(), лПозицияКурсораВТексте);
	мПутьККартинкам        = гСеансовыеДанные.ПутьККартинкам;
	
	лРезультатКП = Неопределено;
	
	Если лДанныеДляПодбораКП <> Неопределено Тогда 
		
		// выбираем значения контекстной подсказки
		Если лДанныеДляПодбораКП.ЗначенияДляВыбора.Количество() = 0 Тогда 
			Возврат;
		Иначе
			Если лДанныеДляПодбораКП.ЗначенияДляВыбора.Количество() = 1 Тогда 
				лВыбранноеЗначение = лДанныеДляПодбораКП.ЗначенияДляВыбора[0];
			Иначе
				Если лДанныеДляПодбораКП.МножественныйВыбор Тогда 
					Если Не лДанныеДляПодбораКП.ЗначенияДляВыбора.ОтметитьЭлементы() Тогда 
						Возврат 
					КонецЕсли;
					
					лВыбранноеЗначение = Новый СписокЗначений;
					Для каждого лВыбранныйЭлемент Из лДанныеДляПодбораКП.ЗначенияДляВыбора Цикл 
						Если Не лВыбранныйЭлемент.Пометка Тогда 
							Продолжить;
						КонецЕсли;
						лВыбранноеЗначение.Добавить(лВыбранныйЭлемент.Значение);
					КонецЦикла; 
				Иначе
					лВыбранноеЗначение = лДанныеДляПодбораКП.ЗначенияДляВыбора.ВыбратьЭлемент();
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если лВыбранноеЗначение <> Неопределено Тогда 
		Если ТипЗнч(лВыбранноеЗначение) = Тип("СписокЗначений") Тогда 
			лРезультатПодстановкиКП = "";
			Для каждого ЭлементСписка Из лВыбранноеЗначение Цикл
				лРезультатПодстановкиКП = лРезультатПодстановкиКП + ?(лРезультатПодстановкиКП = "", "", ", ") + ЭлементСписка.Значение;
			КонецЦикла; 
		Иначе
			лРезультатПодстановкиКП = лВыбранноеЗначение.Значение
		КонецЕсли;
		
		Если ПустаяСтрока(лРезультатПодстановкиКП) Тогда 
			лРезультатКП = Новый Структура("ТекстЗапроса, Режим, дельта_x, дельта_y, дельта_x1, дельта_y1", 
				лДанныеДляПодбораКП.ТекстЗапросаДоКурсора + лДанныеДляПодбораКП.ТекстЗапросаПослеКурсора, 
				лДанныеДляПодбораКП.РежимКП.Режим,
				0, 
				0, 
				0, 
				0
				);
		Иначе
			лВременныйТД = Новый ТекстовыйДокумент;
			лВременныйТД.УстановитьТекст(лРезультатПодстановкиКП);
			
			лТекстЗапроса = Лев(лДанныеДляПодбораКП.ТекстЗапросаДоКурсора, СтрДлина(лДанныеДляПодбораКП.ТекстЗапросаДоКурсора) - СтрДлина(лДанныеДляПодбораКП.СловоДоКурсора)) + 
				лРезультатПодстановкиКП + Сред(лДанныеДляПодбораКП.ТекстЗапросаПослеКурсора, СтрДлина(лДанныеДляПодбораКП.СловоПослеКурсора) + 1);
				
			лРезультатКП = Новый Структура("ТекстЗапроса, Режим, дельта_x, дельта_y, дельта_x1, дельта_y1", 
				лТекстЗапроса, 
				лДанныеДляПодбораКП.РежимКП.Режим,
				СтрДлина(лДанныеДляПодбораКП.СловоДоКурсора), 
				0, 
				СтрДлина(лВременныйТД.ПолучитьСтроку(лВременныйТД.КоличествоСтрок())) - ?(лВременныйТД.КоличествоСтрок() > 1, 0, СтрДлина(лДанныеДляПодбораКП.СловоДоКурсора)), 
				лВременныйТД.КоличествоСтрок() - 1
				);
		КонецЕсли;
	КонецЕсли;
	
	Если лРезультатКП <> Неопределено Тогда 
		
		ПолеТекстаЗапроса.УстановитьТекст(лРезультатКП.ТекстЗапроса);
		
		Если гПродолжитьФормироватьКП(лРезультатКП.Режим, лРезультатПодстановкиКП) Тогда
			ПолеТекстаЗапроса.УстановитьГраницыВыделения(лСтруктураКоординат.СтрокаНач + лРезультатКП.дельта_y1, лСтруктураКоординат.КолонкаНач + лРезультатКП.дельта_x1,
				лСтруктураКоординат.СтрокаНач + лРезультатКП.дельта_y1, лСтруктураКоординат.КолонкаНач + лРезультатКП.дельта_x1);			
			ОткрытьМенюIntelliSense(ПолеТекстаЗапроса)
		Иначе
			ПолеТекстаЗапроса.УстановитьГраницыВыделения(лСтруктураКоординат.СтрокаНач - лРезультатКП.дельта_y, лСтруктураКоординат.КолонкаНач - лРезультатКП.дельта_x, 
				лСтруктураКоординат.СтрокаНач + лРезультатКП.дельта_y1, лСтруктураКоординат.КолонкаНач + лРезультатКП.дельта_x1);			
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры // ОткрытьМенюIntelliSense()

//ПРОЦЕДУРА ДУБЛЬ(!!!)
Функция ПолучитьПозициюКурсораВТексте(ПолеФормыСТекстом, СтруктураКоординат)
	
	// выделяем текст от начала, до текущего
	ПолеФормыСТекстом.УстановитьГраницыВыделения(1, 1, СтруктураКоординат.СтрокаКон, СтруктураКоординат.КолонкаКон);
	
	Результат = СтрДлина(ПолеФормыСТекстом.ВыделенныйТекст);

	// возвращаем исходное выделение
	ПолеФормыСТекстом.УстановитьГраницыВыделения(СтруктураКоординат.СтрокаНач, СтруктураКоординат.КолонкаНач, СтруктураКоординат.СтрокаКон, СтруктураКоординат.КолонкаКон);
	
	Возврат Результат
	
КонецФункции // ПолучитьПозициюКурсораВТексте()

///////////////////////////////////////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиенте
Процедура ПерерисоватьСпискиВременныхТаблиц() Экспорт
	
	// Пересоздаем список временных таблиц к показу
	КнопкаПодменю = гПодменюВременныеТаблицыКнопки.ПоказатьВременнуюТаблицу;
	
	КнопкаПодменю.Кнопки.Очистить();
	Для Каждого ВремТаблица Из ВременныеТаблицы Цикл
		КнопкаПодменю.Кнопки.Добавить(ВремТаблица.Имя, 
		ТипКнопкиКоманднойПанели.Действие, 
		ВремТаблица.Имя + " (" + гСоответствиеВременныеТаблицы[ВремТаблица.Имя].Количество() + ")", // добавим в текст кнопки меню количество строк во временной таблице
		Новый Действие("ПодменюВременныеТаблицыПриВыбореПоказатьВременнуюТаблицу"));
	КонецЦикла;
	
	КнопкаПодменю = гПодменюВременныеТаблицыКнопки.УдалитьВременнуюТаблицу;
	
	КнопкаПодменю.Кнопки.Очистить();
	КнопкаПодменю.Кнопки.Добавить("УдалитьВсе", ТипКнопкиКоманднойПанели.Действие, "Удалить все таблицы", Новый Действие("ПодменюВременныеТаблицыПриВыбореУдалитьВременнуюТаблицу"));
	КнопкаПодменю.Кнопки.Добавить();
	Для Каждого ВремТаблица Из ВременныеТаблицы Цикл
		КнопкаПодменю.Кнопки.Добавить(ВремТаблица.Имя, 
		ТипКнопкиКоманднойПанели.Действие, 
		ВремТаблица.Имя + " (" + гСоответствиеВременныеТаблицы[ВремТаблица.Имя].Количество() + ")", // добавим в текст кнопки меню количество строк во временной таблице
		Новый Действие("ПодменюВременныеТаблицыПриВыбореУдалитьВременнуюТаблицу"));
	КонецЦикла;
	
КонецПроцедуры // ПерерисоватьСпискиВременныхТаблиц()

Процедура ВыгрузитьТекущийЗапросДействие(Локально)
	
	лТекущийЗапрос = ЭлементыФормы.ДеревоЗапросов.ТекущаяСтрока;
	Если лТекущийЗапрос = Неопределено Тогда
		ПоказатьПредупреждение(, "Не выбран запрос для выгрузки.", 10);
		Возврат;
	КонецЕсли;

	Если Локально Тогда 
		СохранитьЗапросыЛокальноДействие(гОперацииСЗапросами().СохранитьКАК, лТекущийЗапрос);
	Иначе
		СохранитьЗапросыВОблакоДействие(гОперацииСЗапросами().СохранитьКАК, лТекущийЗапрос);
	КонецЕсли;
КонецПроцедуры


Процедура СравнитьЗапросыДействие()
	СохранитьЗапросТекущейСтроки();
	
	ФормаВыбораСтрокиДереваЗапросов = ПолучитьФорму("ФормаВыбораСтрокиДереваЗапросов", ЭтаФорма);
	ФормаВыбораСтрокиДереваЗапросов.Элементыформы.КнопкаНаВерхнийУровень.Заголовок = "Сравнить с текущим запросом...";
	ФормаВыбораСтрокиДереваЗапросов.ЗакрыватьПриВыборе = Истина;

	ФормаВыбораСтрокиДереваЗапросов.ДеревоЗапросов = ДеревоЗапросов;
	ФормаВыбораСтрокиДереваЗапросов.ТекущаяСтрокаВладельца = ЭлементыФормы.ДеревоЗапросов.ТекущаяСтрока;
	ФормаВыбораСтрокиДереваЗапросов.ЭлементыФормы.ДеревоЗапросов.ТекущаяСтрока = ЭлементыФормы.ДеревоЗапросов.ТекущаяСтрока;

	РежимВыбораВеткиДерева = "СравнитьЗапросы";
	ФормаВыбораСтрокиДереваЗапросов.ОткрытьМодально();
КонецПроцедуры

Процедура ЗагрузитьВеткуЛокальноДействие()
	Родитель = ЭлементыФормы.ДеревоЗапросов.ТекущаяСтрока.Родитель;
	ЗагрузитьВеткуИзФайла(?(Родитель = Неопределено, ДеревоЗапросов, Родитель), ДеревоЗапросов.Колонки);
КонецПроцедуры

Процедура ДобавитьВетку(Данные, Родитель)
	
	лЗаменяемыеИД = Новый Соответствие;
	// читаем запросы
	лНоваяВетка = ПрочитатьЗапросыИзФайлаJSONРекурсивно(Данные.querys, Родитель, лЗаменяемыеИД);
	
	// читаем параметры
	Для каждого лПараметр Из Данные.parameters Цикл
		лНоваяСтрока = ПараметрыЗапросов.Добавить();
		ЗаполнитьЗначенияСвойств(лНоваяСтрока, лПараметр);
		лНовыйИд = лЗаменяемыеИД.Получить(лНоваяСтрока.ИдентификаторЗапроса);
		Если лНовыйИд <> Неопределено Тогда 
			лНоваяСтрока.ИдентификаторЗапроса = лНовыйИд;
		КонецЕсли;
		Если ПустаяСтрока(лНоваяСтрока.ИдентификаторСтроки) Тогда 
			лНоваяСтрока.ИдентификаторСтроки = Новый УникальныйИдентификатор //#рефакторинг необходимо понять откуда приходит пустой Ид
		КонецЕсли;			
	КонецЦикла; 
	
	// читаем исполняемый код
	Для каждого лСтрокаСкодом Из Данные.codes Цикл
		лНоваяСтрока = ИсполняемыйКод.Добавить();
		ЗаполнитьЗначенияСвойств(лНоваяСтрока, лСтрокаСкодом);
		лНовыйИд = лЗаменяемыеИД.Получить(лНоваяСтрока.ИдентификаторЗапроса);
		Если лНовыйИд <> Неопределено Тогда 
			лНоваяСтрока.ИдентификаторЗапроса = лНовыйИд;
		КонецЕсли;
		Если ПустаяСтрока(лНоваяСтрока.ИдентификаторСтроки) Тогда 
			лНоваяСтрока.ИдентификаторСтроки = Новый УникальныйИдентификатор //#рефакторинг необходимо понять откуда приходит пустой Ид
		КонецЕсли;			
	КонецЦикла; 
	
	Если лНоваяВетка <> Неопределено Тогда 
		ЭлементыФормы.ДеревоЗапросов.ТекущаяСтрока = ДеревоЗапросов.Строки.Найти(?(ТипЗнч(Родитель) = Тип("СтрокаДереваЗначений"), Родитель.Идентификатор, лНоваяВетка.Идентификатор), "Идентификатор", Истина);			
		Активизировать();
		Модифицированность = Истина;
	КонецЕсли;
		
КонецПроцедуры // ДобавитьВетку()

//Процедура ПолучитьСсылкуИзОблака(ВыгружатьПодчиненные)
//	
//	лСоединение = гВосстановитьИдентификаторСессииConsqueryCloud();
//	лРезультат = гПолучитьСсылкуНаЗапросВОблаке(лСоединение, мИдФайлаВОблаке, ЭлементыФормы.ДеревоЗапросов.ТекущиеДанные.Идентификатор, ВыгружатьПодчиненные);
//	Если лРезультат.Статус = "OK" Тогда 
//		ПоказатьВводСтроки(Новый ОписаниеОповещения("ПослеВводаСтроки", ЭтаФорма, Новый Структура), лРезультат.Ссылка, "Ссылка на запрос в облаке",, Истина);
//	Иначе
//		Сообщить("Ошибка получения ссылки в облаке: " + лРезультат.ТекстОшибки);
//	КонецЕсли;
//	
//КонецПроцедуры // ПолучитьСсылкуИзОблака()

//Процедура ВставитьСсылкуИзОблака(Ссылка)
//	лСоединение = гВосстановитьИдентификаторСессииConsqueryCloud();
//	лРезультат = гПолучитьЗапросИзОблака(лСоединение, мИдФайлаВОблаке, Ссылка);
//	Если лРезультат.Статус = "OK" Тогда 
//		ДобавитьВетку(лРезультат.Данные);
//	Иначе
//		Сообщить("Ошибка загрузки запроса по ссылке: " + лРезультат.ТекстОшибки);
//	КонецЕсли;
//КонецПроцедуры // ВставитьСсылкуИзОблака()

Процедура ПодключенияКОблакуДействие()
	лФормаДиалога = ПолучитьФорму("ВнешняяОбработка.КонсольЗапросов.Форма.ФормаПодключениеКОблаку",, мЭтотОбъектФорма);
	лФормаДиалога.email = мИмяПользователяВОблаке;
	лФормаДиалога.Открыть();
КонецПроцедуры // ПодключенияКОблакуДействие()

&НаКлиенте
Функция НастройкиПроксиДействие()
	лФормаНастройкиПрокси = ПолучитьФорму("ВнешняяОбработка.КонсольЗапросов.Форма.ФормаНастройкиПрокси");
	Если лФормаНастройкиПрокси.Открыта() Тогда 
		лФормаНастройкиПрокси.Активизировать();
	Иначе
		Если мНастройкиПрокси <> Неопределено Тогда 
			ЗаполнитьЗначенияСвойств(лФормаНастройкиПрокси, мНастройкиПрокси);
		КонецЕсли;
		лФормаНастройкиПрокси.Открыть();
	КонецЕсли;
КонецФункции // НастройкиПроксиДействие()

//Процедура ПолучитьСсылкуДействие()
//	
//	лЗаголовокДиалогов = "Получение ссылки в облаке";
//	
//	Если ЭлементыФормы.ДеревоЗапросов.ТекущиеДанные = Неопределено Тогда 
//		ПоказатьПредупреждение(,"Выберите запрос для получения ссылки.",, лЗаголовокДиалогов);
//		Возврат;
//	КонецЕсли;
//	
//	Если Не ЗначениеЗаполнено(мИдФайлаВОблаке) Тогда 
//		Сообщить("Для получения ссылки сохраните запрос в облаке, а после еще раз получите ссылку.", СтатусСообщения.Информация);
//		СохранитьЗапросыДействие(гОперацииСЗапросами().СохранитьКАК, Ложь);
//		Возврат;
//	КонецЕсли;
//	
//	ПоказатьВопрос(
//		Новый ОписаниеОповещения("ПослеЗакрытияВопросаВыгружатьПодчиненныеЗапросы", ЭтаФорма, Новый Структура),
//		"Выгружать подчиненные запросы?",
//		РежимДиалогаВопрос.ДаНетОтмена,,
//		КодВозвратаДиалога.Да,
//		лЗаголовокДиалогов);
//		
//КонецПроцедуры // ПолучитьСсылкуДействие()

//Процедура ВставитьСсылкуДействие()
//	
//	лСодержимоеБуфера = гПолучитьСодержимоеБуфера();
//	лПутьКСкрипту = "http://consquery.ru/getQuery.php?link=";
//	
//	Если Найти(лСодержимоеБуфера, лПутьКСкрипту) = 0 Тогда // #СтарыйКод Найти->СтрНайти 8.3.6
//		ПоказатьВводСтроки(Новый ОписаниеОповещения("ПослеВводаСтроки", ЭтаФорма, Новый Структура("ВставитьСсылку", Истина)), лПутьКСкрипту, "Ссылка на запрос в облаке",, Истина);
//	Иначе
//		ПоказатьВводСтроки(Новый ОписаниеОповещения("ПослеВводаСтроки", ЭтаФорма, Новый Структура("ВставитьСсылку", Истина)), лСодержимоеБуфера, "Ссылка на запрос в облаке",, Истина);
//	КонецЕсли;	
//	
//КонецПроцедуры // ПолучитьСсылкуДействие()

Процедура УстановитьИмяПользователяВОблаке()
	
	лИмяПользователяВОблаке = мИмяПользователяВОблаке;
	лСоединение = гВосстановитьИдентификаторСессииConsqueryCloud();
	Если ЗначениеЗаполнено(лСоединение) Тогда 
		лРезультат = гПолучитьИмяПодключенногоПользователяВОблаке(лСоединение);
		Если лРезультат.Статус = "OK" Тогда 
			мИмяПользователяВОблаке = лРезультат.ИмяПользователя;
		Иначе
			мИмяПользователяВОблаке = Неопределено;
		КонецЕсли;
	Иначе
		мИмяПользователяВОблаке = Неопределено;
	КонецЕсли;
	
	Если лИмяПользователяВОблаке <> мИмяПользователяВОблаке Тогда 
		ОбновитьЗаголовок();
	КонецЕсли;
	
КонецПроцедуры // УстановитьИмяПользователяВОблаке()

Процедура ИнициализацияДанных()
	
	лСистемнаяИнформация = Новый СистемнаяИнформация();
	лВерсияПлатформы     = лСистемнаяИнформация.ВерсияПриложения;
	лПлатформаАктуальна  = (гСравнитьВерсии(лВерсияПлатформы, "8.3.6.2421") >= 0);
	
	Если лПлатформаАктуальна Тогда 
		мЭтотОбъектФорма = "ЭтотОбъект"
	Иначе
		мЭтотОбъектФорма = "ЭтаФорма"
	КонецЕсли;
	
	мНастройкиПрокси = гВосстановитьНастройкиПрокси();
	
	//+++ восстановление имени файла
	мИмяФайла       = ВосстановитьЗначение("КонсольЗапросов_ИмяФайла");
	Если мИмяФайла = Неопределено Тогда
		мИмяФайла = "";
	КонецЕсли;
	//--- восстановление имени файла
	
	ПараметрыПодключенияFTP = Новый Структура();
	ПараметрыПодключенияFTP.Вставить("Адрес");
	ПараметрыПодключенияFTP.Вставить("Пользователь");
	ПараметрыПодключенияFTP.Вставить("Пароль");
	ПараметрыПодключенияFTP.Вставить("Порт");
	
	УстановитьИмяПользователяВОблаке(); // необходимо вызывать после гВосстановитьНастройкиПрокси
	
	// Создадим структуру дерева запросов
	ДеревоЗапросов.Колонки.Добавить("ТекстЗапроса");
	ДеревоЗапросов.Колонки.Добавить("СпособВыгрузки");
	ДеревоЗапросов.Колонки.Добавить("ПараметрыИБ");
	ДеревоЗапросов.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка"));

	ПараметрыЗапросов.Колонки.Добавить("ИдентификаторЗапроса", Новый ОписаниеТипов("Строка"));
	ПараметрыЗапросов.Колонки.Добавить("ИдентификаторСтроки", Новый ОписаниеТипов("Строка"));
	ПараметрыЗапросов.Колонки.Добавить("Значение");
	ПараметрыЗапросов.Колонки.Добавить("Имя");
	ПараметрыЗапросов.Колонки.Добавить("Тип");
	
	ИсполняемыйКод.Колонки.Добавить("ИдентификаторЗапроса", Новый ОписаниеТипов("Строка"));
	ИсполняемыйКод.Колонки.Добавить("ИдентификаторСтроки", Новый ОписаниеТипов("Строка"));
	ИсполняемыйКод.Колонки.Добавить("Текст");
	ИсполняемыйКод.Колонки.Добавить("Имя");
	
	ПараметрыИБ.Колонки.Добавить("ИдентификаторЗапроса", Новый ОписаниеТипов("Строка"));
	ПараметрыИБ.Колонки.Добавить("Использовать");
	ПараметрыИБ.Колонки.Добавить("Установить");
	ПараметрыИБ.Колонки.Добавить("Версия");
	ПараметрыИБ.Колонки.Добавить("КаталогБазы");
	ПараметрыИБ.Колонки.Добавить("SQL_Сервер");
	ПараметрыИБ.Колонки.Добавить("SQL_БазаДанных");
	ПараметрыИБ.Колонки.Добавить("ТипБазы");
	ПараметрыИБ.Колонки.Добавить("ИмяПользователя");
	ПараметрыИБ.Колонки.Добавить("Пароль");
	ПараметрыИБ.Колонки.Добавить("ВыводитьСообщение");
	
	гМассивИзмененныхСтрок = Новый Массив;
	
КонецПроцедуры // ИнициализацияДанных()
 
Функция ПолучитьТаблицуНайденныхСсылок(КолонкаССылками)

	лТаблицаРезультата = Вычислить(мИмяРеквизитаТаблицы);
	
	Если лТаблицаРезультата.Количество() = 0 Тогда
		ПоказатьПредупреждение(, "Таблица результатов запроса пуста.", 10);
		Возврат Новый ТаблицаЗначений
	КонецЕсли;
	
	КолонкаССылками = кзВыбратьКолонку(ЭлементыФормы);
	Если КолонкаССылками = Неопределено Тогда
		Возврат Новый ТаблицаЗначений
	КонецЕсли;
	
	Сообщить("Поиск ссылок на объекты (колонка """ + КолонкаССылками + """) начато в " + ТекущаяДата());
	
	лПараметры = Новый Структура;
	
	Для Каждого РегистрБухгалтерии ИЗ Метаданные.РегистрыБухгалтерии Цикл
		лПараметры.Вставить(РегистрБухгалтерии.Имя+"Субконто", РегистрБухгалтерии.ПланСчетов.МаксКоличествоСубконто);
		лПараметры.Вставить(РегистрБухгалтерии.Имя+"Корреспонденция", РегистрБухгалтерии.Корреспонденция);		
	КонецЦикла;
	
	лТаблицаНайденныхСсылок = НайтиПоСсылкам(лТаблицаРезультата.ВыгрузитьКолонку(КолонкаССылками));
	//Ссылка	Данные	Метаданные
	лТаблицаНайденныхСсылок.Колонки.Добавить("ПутьКРасположениюСсылкиВОбъекте",, "Путь к расположению ссылки в объекте");
	
	Для каждого СтрокаТаблицы Из лТаблицаНайденныхСсылок Цикл
		
		Ссылка = СтрокаТаблицы.Ссылка;
		Объект = СтрокаТаблицы.Данные;
		СписокПутей = Новый СписокЗначений;
		
		Если Метаданные.Документы.Содержит(СтрокаТаблицы.Метаданные)
			 ИЛИ Метаданные.Справочники.Содержит(СтрокаТаблицы.Метаданные)
			 ИЛИ Метаданные.ПланыВидовХарактеристик.Содержит(СтрокаТаблицы.Метаданные)
			 ИЛИ Метаданные.ПланыСчетов.Содержит(СтрокаТаблицы.Метаданные)
			 ИЛИ Метаданные.ПланыВидовРасчета.Содержит(СтрокаТаблицы.Метаданные)
			 ИЛИ Метаданные.Задачи.Содержит(СтрокаТаблицы.Метаданные)
			 ИЛИ Метаданные.БизнесПроцессы.Содержит(СтрокаТаблицы.Метаданные) Тогда
			 
			//Справочники
			Если Метаданные.Справочники.Содержит(СтрокаТаблицы.Метаданные) Тогда
				Если СтрокаТаблицы.Метаданные.Владельцы.Содержит(Ссылка.Метаданные()) И Объект.Владелец = Ссылка Тогда
					СписокПутей.Добавить("Владелец");
				КонецЕсли;
				
				Если СтрокаТаблицы.Метаданные.Иерархический И Объект.Родитель = Ссылка Тогда
					СписокПутей.Добавить("Родитель");
				КонецЕсли;
				
				
			//Документы
			ИначеЕсли Метаданные.Документы.Содержит(СтрокаТаблицы.Метаданные)  Тогда
				
				Для Каждого Движение ИЗ СтрокаТаблицы.Метаданные.Движения Цикл
					
					ЭтоДвижениеРегистраБухгалтерии = Метаданные.РегистрыБухгалтерии.Содержит(Движение);
					ЕстьКорреспонденция = ЭтоДвижениеРегистраБухгалтерии и лПараметры[Движение.Имя + "Корреспонденция"];
					
					НаборЗаписей  = Объект.ПолучитьОбъект().Движения[Движение.Имя];
					НаборЗаписей.Прочитать();
					ТаблицаНабора = НаборЗаписей.Выгрузить();
					
					Если ТаблицаНабора.Количество() = 0 Тогда
						Продолжить;
					КонецЕсли;
					
					масИменКолонок = Новый Массив;
					
					// Получим имена измерений, которые могут содержать ссылку
					Для Каждого Измерение ИЗ Движение.Измерения Цикл
						Если Измерение.Тип.СодержитТип(ТипЗнч(Ссылка)) Тогда
							Если (Измерение.Имя = "Валюта") И ЕстьКорреспонденция Тогда
								масИменКолонок.Добавить("ВалютаДт");
								масИменКолонок.Добавить("ВалютаКт");
							Иначе
								масИменКолонок.Добавить(Измерение.Имя);
							КонецЕсли;
						КонецЕсли;
					КонецЦикла;
							
					// Получим имена ресурсов, которые могут содержать ссылку
					Если Метаданные.РегистрыСведений.Содержит(Движение) Тогда
						Для Каждого Ресурс ИЗ Движение.Ресурсы Цикл
							Если Ресурс.Тип.СодержитТип(ТипЗнч(Ссылка)) Тогда
								масИменКолонок.Добавить(Ресурс.Имя);
							КонецЕсли;
						КонецЦикла;
					КонецЕсли;
							
					// Получим имена ресурсов, которые могут содержать ссылку
					Для Каждого Реквизит ИЗ Движение.Реквизиты Цикл
						Если Реквизит.Тип.СодержитТип(ТипЗнч(Ссылка)) Тогда
							масИменКолонок.Добавить(Реквизит.Имя);
						КонецЕсли;
					КонецЦикла;
					
					// Произведем замены в таблице
					Для Каждого ИмяКолонки Из масИменКолонок Цикл
						СтрокаТабЧасти = ТаблицаНабора.Найти(Ссылка, ИмяКолонки);
						Пока СтрокаТабЧасти <> Неопределено Цикл

							СтрокаТабЧасти[ИмяКолонки] = Неопределено;//затираем значение в строке, чтобы строка не попалась на следующей итерации поиска
							СписокПутей.Добавить("Движение_" + Движение.Имя + "_" + ИмяКолонки + "[" + СтрокаТабЧасти.НомерСтроки + "]");
							СтрокаТабЧасти = ТаблицаНабора.Найти(Ссылка, ИмяКолонки);
							
						КонецЦикла;
					КонецЦикла;
					
					Если Метаданные.РегистрыБухгалтерии.Содержит(Движение) Тогда
						
						Для ИндексСубконто = 1 по лПараметры[Движение.Имя + "Субконто"] Цикл
							Если ЕстьКорреспонденция Тогда
								СтрокаТабЧасти = ТаблицаНабора.Найти(Ссылка, "СубконтоДт"+ИндексСубконто);
								Пока СтрокаТабЧасти <> Неопределено Цикл
									СтрокаТабЧасти["СубконтоДт"+ИндексСубконто] = Неопределено;//затираем значение в строке, чтобы строка не попалась на следующей итерации поиска
									СписокПутей.Добавить("Движение_" + Движение.Имя + "_СубконтоДт"+ИндексСубконто + "[" + СтрокаТабЧасти.НомерСтроки + "]");
									СтрокаТабЧасти = ТаблицаНабора.Найти(Ссылка, "СубконтоДт"+ИндексСубконто);
								КонецЦикла;
								СтрокаТабЧасти = ТаблицаНабора.Найти(Ссылка, "СубконтоКт"+ИндексСубконто);
								Пока СтрокаТабЧасти <> Неопределено Цикл
									СтрокаТабЧасти["СубконтоКт"+ИндексСубконто] = Неопределено;
									СписокПутей.Добавить("Движение_" + Движение.Имя + "_СубконтоКт"+ИндексСубконто + "[" + СтрокаТабЧасти.НомерСтроки + "]");
									СтрокаТабЧасти = ТаблицаНабора.Найти(Ссылка, "СубконтоКт"+ИндексСубконто);
								КонецЦикла;								
							Иначе							
								СтрокаТабЧасти = ТаблицаНабора.Найти(Ссылка, "Субконто"+ИндексСубконто);
								Пока СтрокаТабЧасти <> Неопределено Цикл
									СтрокаТабЧасти["Субконто"+ИндексСубконто] = Неопределено;
									СписокПутей.Добавить("Движение_" + Движение.Имя + "_Субконто"+ИндексСубконто + "[" + СтрокаТабЧасти.НомерСтроки + "]");
									СтрокаТабЧасти = ТаблицаНабора.Найти(Ссылка, "Субконто"+ИндексСубконто);
								КонецЦикла;							
							КонецЕсли;						
						КонецЦикла;
						
						Если Ссылка.Метаданные() = Движение.ПланСчетов Тогда
							Для Каждого СтрокаТабЧасти Из ТаблицаНабора Цикл
								Если ЕстьКорреспонденция Тогда
									Если СтрокаТабЧасти.СчетДт = Ссылка Тогда
										СписокПутей.Добавить("Движение_" + Движение.Имя + "_СчетДт" + "[" + СтрокаТабЧасти.НомерСтроки + "]");
									КонецЕсли;
									Если СтрокаТабЧасти.СчетКт = Ссылка Тогда
										СписокПутей.Добавить("Движение_" + Движение.Имя + "_СчетКт" + "[" + СтрокаТабЧасти.НомерСтроки + "]");
									КонецЕсли;
								Иначе
									Если СтрокаТабЧасти.Счет = Ссылка Тогда
										СписокПутей.Добавить("Движение_" + Движение.Имя + "_Счет" + "[" + СтрокаТабЧасти.НомерСтроки + "]");
									КонецЕсли;
								КонецЕсли;
							КонецЦикла;
						КонецЕсли;
					КонецЕсли;
					
					Если Метаданные.РегистрыРасчета.Содержит(Движение) Тогда
						СтрокаТабЧасти = ТаблицаНабора.Найти(Ссылка, "ВидРасчета");
						Пока СтрокаТабЧасти <> Неопределено Цикл
							СтрокаТабЧасти["ВидРасчета"] = Неопределено;
							СписокПутей.Добавить("Движение_" + Движение.Имя + "_ВидРасчета" + "[" + СтрокаТабЧасти.НомерСтроки + "]");
							СтрокаТабЧасти = ТаблицаНабора.Найти(Ссылка, "ВидРасчета");
						КонецЦикла;
					КонецЕсли;
					
				КонецЦикла;
				
				
				Для Каждого Последовательность ИЗ Метаданные.Последовательности Цикл
					Если Последовательность.Документы.Содержит(СтрокаТаблицы.Метаданные) Тогда
						НадоЗаписывать = Ложь;
						НаборЗаписи = Последовательности[Последовательность.Имя].СоздатьНаборЗаписей();
						НаборЗаписи.Отбор.Регистратор.Установить(СтрокаТаблицы.Данные);
						НаборЗаписи.Прочитать();
						ТаблицаНабора = НаборЗаписей.Выгрузить();
						
						Если ТаблицаНабора.Количество() > 0 Тогда
							Для Каждого Измерение ИЗ Последовательность.Измерения Цикл
								Если Измерение.Тип.СодержитТип(ТипЗнч(Ссылка)) И ТаблицаНабора[0][Измерение.Имя]=Ссылка Тогда
									ТаблицаНабора[0][Измерение.Имя] = Неопределено;
									СписокПутей.Добавить("Последовательность_" + Последовательность.Имя + "_Измерение_" + Измерение.Имя);
								КонецЕсли;
							КонецЦикла;					
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
				
			КонецЕсли;
			
			Для Каждого Реквизит Из СтрокаТаблицы.Метаданные.Реквизиты Цикл
				Если Реквизит.Тип.СодержитТип(ТипЗнч(Ссылка)) И Объект[Реквизит.Имя] = Ссылка Тогда
					СписокПутей.Добавить("Реквизит_" + Реквизит.Имя);
				КонецЕсли;
			КонецЦикла;
			
			Для Каждого ТЧ ИЗ СтрокаТаблицы.Метаданные.ТабличныеЧасти Цикл
				Для Каждого Реквизит Из ТЧ.Реквизиты Цикл
					Если Реквизит.Тип.СодержитТип(ТипЗнч(Ссылка)) Тогда
						ОбъектТЧ = Объект[ТЧ.Имя];
						СтрокаТабЧасти = ОбъектТЧ.Найти(Ссылка, Реквизит.Имя);
						Пока СтрокаТабЧасти <> Неопределено Цикл
							СтрокаТабЧасти[Реквизит.Имя] = Неопределено;//затираем значение в строке, чтобы строка не попалась на следующей итерации поиска
							СписокПутей.Добавить("ТЧ_" + ТЧ.Имя + "_Реквизит_" + Реквизит.Имя + "[" + СтрокаТабЧасти.НомерСтроки + "]");
							СтрокаТабЧасти = ОбъектТЧ.Найти(Ссылка, Реквизит.Имя);
						КонецЦикла;
					КонецЕсли;
				КонецЦикла;
			КонецЦикла;
			
		ИначеЕсли Метаданные.РегистрыСведений.Содержит(СтрокаТаблицы.Метаданные) Тогда	
			
			СтруктураИзмерений = Новый Структура;
			НаборЗаписей = РегистрыСведений[СтрокаТаблицы.Метаданные.Имя].СоздатьНаборЗаписей();
			Для Каждого Измерение ИЗ СтрокаТаблицы.Метаданные.Измерения Цикл
				НаборЗаписей.Отбор[Измерение.Имя].Установить(Объект[Измерение.Имя]);
				СтруктураИзмерений.Вставить(Измерение.Имя);
			КонецЦикла;
			Если СтрокаТаблицы.Метаданные.ПериодичностьРегистраСведений <> Метаданные.СвойстваОбъектов.ПериодичностьРегистраСведений.Непериодический Тогда
				НаборЗаписей.Отбор["Период"].Установить(Объект.Период);
			КонецЕсли;
			НаборЗаписей.Прочитать();
			
			Если НаборЗаписей.Количество() = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			ТаблицаНабора = НаборЗаписей.Выгрузить();
			
			Попытка
				Для Каждого Колонка ИЗ ТаблицаНабора.Колонки Цикл
					Если ТаблицаНабора[0][Колонка.Имя] = Ссылка Тогда
						СписокПутей.Добавить(?(СтруктураИзмерений.Свойство(Колонка.Имя), "Измерение_", "") + Колонка.Имя);
					КонецЕсли;
				КонецЦикла;
			Исключение
			КонецПопытки;
			
		КонецЕсли;

		СтрокаТаблицы.ПутьКРасположениюСсылкиВОбъекте = СписокПутей;
		
	КонецЦикла; 
	
	Возврат лТаблицаНайденныхСсылок

КонецФункции // ПолучитьТаблицуНайденныхСсылок()
 
Процедура кзУстановитьШиринуКолонок(ЭлементыФормы,СтруктураСРазмерами)
	
	ТекКолонки = Вычислить("ЭлементыФормы." + мИмяРеквизитаТаблицы + ".Колонки");
	Для каждого Колонка Из ТекКолонки Цикл
		Если СтруктураСРазмерами.Свойство(Колонка.Имя) Тогда
			Колонка.Ширина = СтруктураСРазмерами[Колонка.Имя]
		КонецЕсли;
	КонецЦикла; 
	
КонецПроцедуры

// Обработчик нажатия кнопки командной панели "Выполнить"
//
Процедура ВыполнитьЗапросИзФормы(ОбъектЗапрос = Неопределено, ЭлементТаблицаРезультата = Неопределено, ВыполнитьПакет = Ложь)

	Если ФлажокСохранитьПередВыполнением И Модифицированность Тогда
		Если Не СохранитьЗапросыЛокальноДействие(гОперацииСЗапросами().Сохранить) Тогда
			Если Вопрос("Запрос не сохранился, продолжить выполнение?" + Символы.ПС + 
				"Чтобы не выводился данный вопрос снимите галочку ""Сохранить запрос перед выполнением"".", 
					РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Нет Тогда
				Возврат;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если Модифицированность Тогда 
		СохранитьЗапросТекущейСтроки();
	КонецЕсли;
		
	//если переменная объектзапрос неопределена (т.е. это не вызов обработки извне), то определяем ее
	Если ОбъектЗапрос = Неопределено Тогда 
		ОбъектЗапрос = Новый Запрос;
		ОбъектЗапрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	КонецЕсли;

	ОбъектЗапрос.Текст = СтрЗаменить(ПолучитьТекстЗапроса(Истина), "|", "");

	Если ПустаяСтрока(ОбъектЗапрос.Текст) Тогда
		ПоказатьПредупреждение(,"Не заполнен текст запроса!", 10);
		Возврат;
	КонецЕсли;
	
	СписокПараметров = ОбъектЗапрос.НайтиПараметры();
	
	Для каждого СтрокаПараметров Из мФормаПараметров.ПараметрыСписок Цикл
		
		Если СписокПараметров.Найти(СтрокаПараметров.Имя) = Неопределено Тогда 
			Продолжить;
		КонецЕсли;
		
		Если СтрокаПараметров.Тип = гТипыЗначенийПараметров().ТаблицаЗначений Тогда 
			лИдентификаторПараметра = гПреобразоватьВПравильноеНазвание(СтрокаПараметров.ИдентификаторСтроки);
			Если гСтруктураТЗДляВременныхТаблиц.Свойство(лИдентификаторПараметра) Тогда 
				ОбъектЗапрос.УстановитьПараметр(СтрокаПараметров.Имя, гСтруктураТЗДляВременныхТаблиц[лИдентификаторПараметра]);
			Иначе
				Сообщить("Параметр " + СтрокаПараметров.Имя + " не простого типа. 
					|Значение данного параметра берется из структуры гСтруктураТЗДляВременныхТаблиц с ключем """ + лИдентификаторПараметра + """.
					|Код для вставки значения параметра через форму выполнения произвольного кода:                              
					|	гСтруктураТЗДляВременныхТаблиц.Вставить(""" + лИдентификаторПараметра + """, %ПеременнаяСоЗначениемПараметра%)", СтатусСообщения.Важное);
			КонецЕсли;
		Иначе
			ОбъектЗапрос.УстановитьПараметр(СтрокаПараметров.Имя, СтрокаПараметров.Значение);
		КонецЕсли;
	КонецЦикла;

	мДатаНачалаВыполнения = ТекущаяДата();
	
	Если ВыполнитьПакет Тогда 
		мРезЗапроса = ОбъектЗапрос.ВыполнитьПакет();
	Иначе
		мРезЗапроса = ОбъектЗапрос.Выполнить();
	КонецЕсли;

	мДатаОкончанияЗапроса = ТекущаяДата();

	мТаблицаЗагружена = Ложь;
	мСводнаяТаблицаЗагружена = Ложь;

	
	//сформируем список временных таблиц
	Если ТипЗнч(ОбъектЗапрос) <> Тип("COMОбъект") Тогда 
		
		Если гПодменюВременныеТаблицыКнопки.ИспользоватьМенеджерВременныхТаблиц.Пометка Тогда 
			
			// Проанализируем использование временных таблиц
			Если гИнициализацияVBScript() тогда
				
				// паттерн, позволяющий получить строку с названием виртуальной таблицы
				RegExp.Pattern	= "^(//){0,}.*ПОМЕСТИТЬ(\s){0,}(//){0,}(.){0,}[^\s; ]{1,}";//Ищем имена создаваемых таблиц
				Matches			= RegExp.Execute(ОбъектЗапрос.Текст);
				ЧислоВхождений	= Matches.Count();
				
				НекорректныеИменаТаблиц = Новый Массив;
				
				Если ЧислоВхождений>0 Тогда 
					
					Для Счетчик = 0 По ЧислоВхождений-1 Цикл
						
						Match = Matches.Item(Счетчик);
						Если Найти(Match.Value, "//") = 0 Тогда
							
							RegExp.Pattern = "ПОМЕСТИТЬ(\s){0,}[^\s; ]{1,}";
							SubMatches = RegExp.Execute(Match.Value);
							Если SubMatches.Count > 0 Тогда //В одной строке может быть вписано сразу несколько вхождений
								
								Для СубСчетчик = 0 По SubMatches.Count - 1 Цикл
									
									SubMatch = SubMatches.Item(СубСчетчик);
									ИмяТаблицы = СокрЛП(Сред(СокрЛП(SubMatch.Value), 10));
									
									Если ВременныеТаблицы.Найти(ИмяТаблицы,"Имя") = Неопределено Тогда
										
										ВременныеТаблицы.Добавить().Имя = ИмяТаблицы;
										
									КонецЕсли;
									
								КонецЦикла;
							КонецЕсли;						
						КонецЕсли;					
					КонецЦикла;
				КонецЕсли;
				
				RegExp.Pattern	= "^(//){0,}.*УНИЧТОЖИТЬ(\s){0,}(//){0,}(.){0,}[^\s; ]{1,}";
				Matches			= RegExp.Execute(ОбъектЗапрос.Текст);
				ЧислоВхождений	= Matches.Count();
				
				НекорректныеИменаТаблиц.Очистить();
				
				Если ЧислоВхождений > 0 Тогда 
					Для Счетчик = 0 По ЧислоВхождений-1 Цикл
						
						Match = Matches.Item(Счетчик);
						Если Найти(Match.Value, "//") = 0 Тогда
							
							RegExp.Pattern = "УНИЧТОЖИТЬ(\s){0,}[^\s; ]{1,}";
							SubMatches = RegExp.Execute(Match.Value);
							Если SubMatches.Count > 0 Тогда //В одной строке может быть вписано сразу несколько вхождений
								Для СубСчетчик = 0 По SubMatches.Count-1 Цикл
									
									SubMatch	= SubMatches.Item(СубСчетчик);
									ИмяТаблицы	= СокрЛП(Сред(СокрЛП(SubMatch.Value), 11));
									
									стрВременнойТаблицы = ВременныеТаблицы.Найти(ИмяТаблицы,"Имя");
									Если стрВременнойТаблицы <> Неопределено Тогда
										ВременныеТаблицы.Удалить(стрВременнойТаблицы);
									Иначе
										НекорректныеИменаТаблиц.Добавить(ИмяТаблицы);
									КонецЕсли;
								КонецЦикла;
							КонецЕсли;
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
				
				Счетчик = 0;
				Пока Счетчик < ВременныеТаблицы.Количество() Цикл
					ИмяТаблицы = ВременныеТаблицы[Счетчик].Имя;
					ВременнаяТаблица = кзДобавитьВременнуюТаблицу(ИмяТаблицы, ОбъектЗапрос.МенеджерВременныхТаблиц);
					Если ВременнаяТаблица = Неопределено Тогда 
						ВременныеТаблицы.Удалить(Счетчик);
						Счетчик = Счетчик - 1;
					Иначе 
						гСоответствиеВременныеТаблицы.Вставить(ИмяТаблицы, ВременнаяТаблица);
					КонецЕсли;
					Счетчик = Счетчик + 1;
				КонецЦикла;
				
				Для каждого ВременнаяТаблица Из ВременныеТаблицы Цикл
					ИмяТаблицы = ВременнаяТаблица.Имя;
					гСоответствиеВременныеТаблицы.Вставить(ИмяТаблицы, кзДобавитьВременнуюТаблицу(ИмяТаблицы, ОбъектЗапрос.МенеджерВременныхТаблиц));
				КонецЦикла; 
				
				Если НекорректныеИменаТаблиц.Количество() > 0 Тогда
					ИменаТаблиц = "";
					Для Счетчик = 0 По Мин(НекорректныеИменаТаблиц.Количество() - 1, 9) Цикл
						ИменаТаблиц = ИменаТаблиц + НекорректныеИменаТаблиц[Счетчик] + Символы.ПС;
					КонецЦикла;
					Если НекорректныеИменаТаблиц.Количество() > 10 Тогда
						ИменаТаблиц = ИменаТаблиц + "..."+Символы.ПС;
					КонецЕсли;
					
					Если Вопрос("Некоторые временные таблицы не были найдены в менеджере:"+Символы.ПС+ИменаТаблиц+"Выполнение запроса возможно приведет к ошибке. Продолжить выполнение?", РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Нет Тогда
						Возврат;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли; // гИнициализацияVBScript()
		Иначе
			ВременныеТаблицы.Очистить();
		КонецЕсли; 
		
	КонецЕсли; //ТипЗнч(ОбъектЗапрос) <> Тип("COMОбъект")

	ЗагрузитьРезультат(ЭлементТаблицаРезультата);

	Если гПодменюВременныеТаблицыКнопки.ИспользоватьМенеджерВременныхТаблиц.Пометка Тогда 
		ПерерисоватьСпискиВременныхТаблиц();
	КонецЕсли;
	
КонецПроцедуры // ВыполнитьЗапросИзФормы()

//СравнитьФайлы() - Сравнивает два файла и выдает рещультат
//Параметры:
//ИмяПервогоФайла,ИмяВторогоФайла - Имена сравниваемых файлов
//СпособСравнения - Способ сравнения текст/табдок
// Получает текст запроса Из текстового поля
//
// Параметры:
//  СВыделением - признак получения только выделенного текста.
//
// Возвращаемое значение:
//	Текст запроса в виде строки.
//
Функция ПолучитьТекстЗапроса(СВыделением) Экспорт

	Если Не СВыделением Тогда
		Возврат ЭлементыФормы.ТекстЗапроса.ПолучитьТекст();
	КонецЕсли;

    ТекстЗап = ЭлементыФормы.ТекстЗапроса.ПолучитьВыделенныйТекст();
	Если СтрДлина(ТекстЗап) <> 0 Тогда
		// удалим последнее помещение во временную таблицу
		
		//лСистемнаяИнформация = Новый СистемнаяИнформация();
		//лВерсияПлатформы     = лСистемнаяИнформация.ВерсияПриложения;
		//Если гСравнитьВерсии(лВерсияПлатформы, "8.3.5.1068") >= 0 Тогда // #рефакторинг проанализировать на минимальный возможный релиз и убрать это условие и
			СхемаЗапроса = Вычислить("Новый СхемаЗапроса");               // код Вычислить(), если минимальный релиз старше
			СхемаЗапроса.УстановитьТекстЗапроса(ТекстЗап);
			Если СхемаЗапроса.ПакетЗапросов.Количество() > 0 И СхемаЗапроса.ПакетЗапросов[СхемаЗапроса.ПакетЗапросов.Количество()-1].ТаблицаДляПомещения <> "" Тогда 
				СхемаЗапроса.ПакетЗапросов[СхемаЗапроса.ПакетЗапросов.Количество()-1].ТаблицаДляПомещения = "";
				ТекстЗап = СхемаЗапроса.ПолучитьТекстЗапроса();
			КонецЕсли;
		//КонецЕсли;
		
		// код не работает для случая, когда последняя таблица в выделенном фрагменте не временная
		//Если гИнициализацияVBScript() тогда
		//	// паттерн, позволяющий получить строку с названием виртуальной таблицы
		//	RegExp.Pattern	= "ПОМЕСТИТЬ\s+\S+"; // Ищем подстроки, определяющую временнуютаблицу (ПОМЕСТИТЬ имяВременнойТаблицы)
		//	Matches			= RegExp.Execute(ТекстЗап);
		//	Если Matches.Count() > 0 Тогда 
		//		лПодстрокаВремТабл = Matches.Item(Matches.Count() - 1).Value;
		//		лПодстрокаВремТаблДлина = СтрДлина(лПодстрокаВремТабл);
		//		
		//		// лПозицияПодстроки = СтрНайти(ТекстЗап, лПодстрокаВремТабл, НаправлениеПоиска.СКонца); // для совместмости нельзя использовать (пока)
		//		// просто заменить нельзя, потомучто временная таблица в пакете может встречаться несколько раз, и каждый раз уничтожаться
		//		ДлинаОбрезанногоБлока = 0;
		//		ТекстЗапКопия = ТекстЗап;
		//		Пока Истина Цикл
		//			лПозицияПодстрокиТекущая = Найти(ТекстЗапКопия, лПодстрокаВремТабл);
		//			Если лПозицияПодстрокиТекущая = 0 Тогда 
		//				Прервать;
		//			Иначе
		//				ДлинаОбрезанногоБлока = ДлинаОбрезанногоБлока + лПозицияПодстрокиТекущая + лПодстрокаВремТаблДлина;
		//				ТекстЗапКопия = Сред(ТекстЗапКопия, лПозицияПодстрокиТекущая + лПодстрокаВремТаблДлина + 1);
		//			КонецЕсли;
		//		КонецЦикла;
		//		ТекстЗап = Лев(ТекстЗап, ДлинаОбрезанногоБлока - лПодстрокаВремТаблДлина - 1) + Сред(ТекстЗап, ДлинаОбрезанногоБлока);
		//	КонецЕсли;
		//КонецЕсли;
		
		Возврат ТекстЗап;
	Иначе
		Возврат ЭлементыФормы.ТекстЗапроса.ПолучитьТекст();
	КонецЕсли;

КонецФункции // ПолучитьТекстЗапроса()

// Устанавливает текст запроса в текстовом поле
//
// Параметры:
//  Текст - устанавливаемый текст запроса.
//
Процедура ЗадатьТекстЗапроса(Текст)

	ЭлементыФормы.ТекстЗапроса.УстановитьТекст(Текст);

КонецПроцедуры // ЗадатьТекстЗапроса()

// Устанавливает заголовок формы по имени файла запросов
//
// Параметры:
//  Нет.
//
Процедура ОбновитьЗаголовок()
	Заголовок = мЗаголовокФормы;
	Если ЗначениеЗаполнено(мИмяПользователяВОблаке) Тогда
	    Заголовок = Заголовок +  " | ~" + мИмяПользователяВОблаке + "~";
	КонецЕсли; 
	
    Заголовок = Заголовок +  " | " + ?(мИмяФайла = "", "Новый", мИмяФайла);
	
КонецПроцедуры // ОбновитьЗаголовок()

Процедура СохранитьЗапросТекущейСтроки()

	Если мОтменаРедактирования = Истина Тогда 
		мОтменаРедактирования = Ложь;
		Возврат;
	КонецЕсли;
	
	Если Не Модифицированность Тогда 
		Возврат;
	КонецЕсли;
	
	Если ДеревоЗапросов.Строки.Количество() <> 0 И мТекущаяСтрока <> Неопределено Тогда

		Если гМассивИзмененныхСтрок.Найти(мТекущаяСтрока.Идентификатор) = Неопределено Тогда гМассивИзмененныхСтрок.Добавить(мТекущаяСтрока.Идентификатор) КонецЕсли;
		
		Если мТекущаяСтрока.ТекстЗапроса <> ПолучитьТекстЗапроса(Ложь) Тогда
			Модифицированность = Истина;
		КонецЕсли;
		     
		мТекущаяСтрока.СпособВыгрузки = СпособВыгрузки;
		мТекущаяСтрока.ТекстЗапроса   = ПолучитьТекстЗапроса(Ложь);
		
		//#добавлениепараметразапроса 1
		лПараметрыЗапроса = ПараметрыЗапросов.НайтиСтроки(Новый Структура("ИдентификаторЗапроса", ""));		
		Для каждого лПараметрЗапроса Из лПараметрыЗапроса Цикл
			ПараметрыЗапросов.Удалить(лПараметрЗапроса);
		КонецЦикла; 
		
		лПараметрыЗапроса = ПараметрыЗапросов.НайтиСтроки(Новый Структура("ИдентификаторЗапроса", мТекущаяСтрока.Идентификатор));
		Для каждого лПараметрЗапроса Из лПараметрыЗапроса Цикл
			ПараметрыЗапросов.Удалить(лПараметрЗапроса);
		КонецЦикла; 
		
		Для каждого ПараметрЗапроса Из мФормаПараметров.ПараметрыСписок Цикл
			лНоваяСтрока = ПараметрыЗапросов.Добавить();
			ЗаполнитьЗначенияСвойств(лНоваяСтрока, ПараметрЗапроса);
			лНоваяСтрока.Значение = ЗначениеВСтрокуВнутр(ПараметрЗапроса.Значение);
			Если ПараметрЗапроса.ГлобальныйПараметр Тогда 
				лНоваяСтрока.ИдентификаторЗапроса = "";
			Иначе
				лНоваяСтрока.ИдентификаторЗапроса = мТекущаяСтрока.Идентификатор;
			КонецЕсли;
		КонецЦикла; 
		
		// сохраняем исполняемый код
		лИсполняемыйКодМассив = ИсполняемыйКод.НайтиСтроки(Новый Структура("ИдентификаторЗапроса", мТекущаяСтрока.Идентификатор));
		Для каждого лИсполняемыйКод Из лИсполняемыйКодМассив Цикл
			ИсполняемыйКод.Удалить(лИсполняемыйКод);
		КонецЦикла; 
		
		Для каждого лИсполняемыйКод Из мФормаИсполняемыйКод.ИсполняемыйКодСписок Цикл
			лНоваяСтрока = ИсполняемыйКод.Добавить();
			ЗаполнитьЗначенияСвойств(лНоваяСтрока, лИсполняемыйКод);
			лНоваяСтрока.ИдентификаторЗапроса = мТекущаяСтрока.Идентификатор;
		КонецЦикла;
		
		мТекущаяСтрока.ПараметрыИБ = мФормаПараметрыИБ.ТаблицаПараметров.Скопировать();
		
	КонецЕсли;	

	кзСохранитьШаблоны(ЭтаФорма);

КонецПроцедуры // СохранитьЗапросТекущейСтроки()

// Очищает дерево запросов, текстовое поле, список параметров
//
// Параметры:
//  Нет.
//
Процедура ОчиститьДанныеПоЗапросам()

	ДеревоЗапросов.Строки.Очистить();
	
	ЗадатьТекстЗапроса("");
	
	мТекущаяСтрока = Неопределено;
	мФормаПараметров.ПараметрыСписок.Очистить();
	мФормаИсполняемыйКод.ИсполняемыйКодСписок.Очистить();
	мФормаПараметрыИБ.ТаблицаПараметров.Очистить();
	
	ПараметрыЗапросов.Очистить();
	ИсполняемыйКод.Очистить();
	
КонецПроцедуры // ОчиститьДанныеПоЗапросам()

Процедура СкопироватьСтрокуЗапроса(СтрокаДерева, НоваяСтрока, Параметры)

	ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаДерева);
	
	лЭтоСтарыйРелиз = (Параметры.Свойство("ЭтоСтарыйРелиз") И Параметры.ЭтоСтарыйРелиз);
	
	Если лЭтоСтарыйРелиз Тогда 
		НоваяСтрока.Имя = СтрокаДерева.Запрос;
	Иначе
		НоваяСтрока.Имя = СтрокаДерева.Имя;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(СтрокаДерева.Идентификатор) ИЛИ (Параметры.Свойство("ОбновлятьИД") И Параметры.ОбновлятьИД) Тогда 
		НоваяСтрока.Идентификатор = Новый УникальныйИдентификатор;
	КонецЕсли;
	
	НоваяСтрока.ТекстЗапроса  = СтрокаДерева.ТекстЗапроса;
	
	Если лЭтоСтарыйРелиз Тогда 
		//#добавлениепараметразапроса 2
		Если Параметры.ЕстьПараметрыЗапроса И ЗначениеЗаполнено(СтрокаДерева.ПараметрыЗапроса) Тогда 
			Для каждого Строка Из СтрокаДерева.ПараметрыЗапроса Цикл
				лНовыйПараметр = ПараметрыЗапросов.Добавить();
				лНовыйПараметр.ИдентификаторЗапроса = НоваяСтрока.Идентификатор;
				лНовыйПараметр.ИдентификаторСтроки  = Новый УникальныйИдентификатор;
				лНовыйПараметр.Значение             = ЗначениеВСтрокуВнутр(Строка.Значение);
				лНовыйПараметр.Имя                  = Строка.ИмяПараметра;
			КонецЦикла; 
		КонецЕсли;
		
		Если Параметры.ЕстьВариантыКода И ЗначениеЗаполнено(СтрокаДерева.ВариантыКода) Тогда 
			Для каждого Строка Из СтрокаДерева.ВариантыКода Цикл
				лНовыйКод = ИсполняемыйКод.Добавить();
				лНовыйКод.ИдентификаторЗапроса = НоваяСтрока.Идентификатор;
				лНовыйКод.ИдентификаторСтроки  = Новый УникальныйИдентификатор;
				лНовыйКод.Имя                  = Строка.ИмяВарианта;
				лНовыйКод.Текст                = Строка.ТекстКода;
			КонецЦикла; 
		КонецЕсли;
	Иначе
		
		лОбновлятьИДСтрокДанныхЗапроса = Параметры.Свойство("ОбновлятьИДСтрокДанныхЗапроса") И Параметры.ОбновлятьИДСтрокДанныхЗапроса;
		
		лПараметрыЗапроса = ПараметрыЗапросов.НайтиСтроки(Новый Структура("ИдентификаторЗапроса", СтрокаДерева.Идентификатор));
		Для каждого лПараметрЗапроса Из лПараметрыЗапроса Цикл
			лНовыйПараметр = ПараметрыЗапросов.Добавить();
			ЗаполнитьЗначенияСвойств(лНовыйПараметр, лПараметрЗапроса);
			лНовыйПараметр.ИдентификаторЗапроса = НоваяСтрока.Идентификатор;
			Если лОбновлятьИДСтрокДанныхЗапроса Тогда 
				лНовыйПараметр.ИдентификаторСтроки  = Новый УникальныйИдентификатор;
			КонецЕсли;
		КонецЦикла; 
		
		лИсполняемыйКодМассив = ИсполняемыйКод.НайтиСтроки(Новый Структура("ИдентификаторЗапроса", СтрокаДерева.Идентификатор));
		Для каждого лИсполняемыйКод Из лИсполняемыйКодМассив Цикл
			лНовыйКод = ИсполняемыйКод.Добавить();
			ЗаполнитьЗначенияСвойств(лНовыйКод, лИсполняемыйКод);
			лНовыйКод.ИдентификаторЗапроса = НоваяСтрока.Идентификатор;
			Если лОбновлятьИДСтрокДанныхЗапроса Тогда 
				лНовыйКод.ИдентификаторСтроки  = Новый УникальныйИдентификатор;
			КонецЕсли;
		КонецЦикла; 		
	КонецЕсли;

КонецПроцедуры // СкопироватьСтрокуЗапроса()
 
// Копирует дерево
//
// Параметры:
//  ИсходноеДерево
//	НовоеДерево.
//
Процедура СкопироватьДеревоЗапросовРекурсивно(ИсходноеДерево, НовоеДерево, ПараметрыРелиза)

	// #рефакторинг только для совместимости со старым форматом
	Если Не ПараметрыРелиза.Свойство("ЕстьПараметрыЗапроса") Тогда 
		
		Если ПараметрыРелиза.ЭтоСтарыйРелиз = Истина Тогда 
			ПараметрыРелиза.Вставить("ЕстьПараметрыЗапроса", Истина);
			ПараметрыРелиза.Вставить("ЕстьВариантыКода", Истина);
			
			НазваниеСпециальнойСтроки = ДополнительныеПараметры.Найти("СлужебнаяСтрокаДереваЗапросов", "Параметр");
			
			Если НазваниеСпециальнойСтроки <> Неопределено тогда
				Специальнаястрока = ИсходноеДерево.Строки.Найти(НазваниеСпециальнойСтроки.Значение, "");
				Если СпециальнаяСтрока <> Неопределено тогда
					ЭтаФорма.Шаблоны = СпециальнаяСтрока.ПараметрыЗапроса.Скопировать();
					ИсходноеДерево.Строки.Удалить(СпециальнаяСтрока);
					кзСохранитьШаблоны(ЭтаФорма);
				КонецЕсли;
			КонецЕсли;		
		КонецЕсли;
		
	КонецЕсли;
	
	Если ТипЗнч(НовоеДерево) = Тип("СтрокаДереваЗначений") Тогда 
		СкопироватьСтрокуЗапроса(ИсходноеДерево, НовоеДерево, ПараметрыРелиза);
		Если гМассивИзмененныхСтрок.Найти(ИсходноеДерево.Идентификатор) = Неопределено Тогда гМассивИзмененныхСтрок.Добавить(ИсходноеДерево.Идентификатор) КонецЕсли;
	КонецЕсли;
	
	Для каждого СтрокаДерева Из ИсходноеДерево.Строки Цикл
		СкопироватьДеревоЗапросовРекурсивно(СтрокаДерева, НовоеДерево.Строки.Добавить(), ПараметрыРелиза);
	КонецЦикла;

КонецПроцедуры // СкопироватьДеревоЗапросовРекурсивно()

// Подготовка к созданию нового файла запросов
//
// Параметры:
//  Нет.
//
Процедура СоздатьНовыйФайлЗапросов()

	мИмяФайла = "";
	ОбновитьЗаголовок();
	
	ОчиститьДанныеПоЗапросам();

	мТекущаяСтрока                = ДеревоЗапросов.Строки.Добавить();
	мТекущаяСтрока.Имя            = "Запросы";
	мТекущаяСтрока.Идентификатор  = Новый УникальныйИдентификатор;
	мТекущаяСтрока.СпособВыгрузки = 1; // СпособВыгрузки = 1;
	
	гМассивИзмененныхСтрок.Очистить();
	
	Модифицированность = Ложь;
	
КонецПроцедуры // СоздатьНовыйФайлЗапросов()

&НаКлиенте
Функция ПрочитатьJSONСОбработкойОшибки(ЧтениеJSON, ТекстОшибки) 
	Попытка
		Результат = ЧтениеJSON.Прочитать();
		Возврат Результат;		
	Исключение
		ТекстОшибки = ОписаниеОшибки();	
		Возврат Ложь;		
	КонецПопытки; 
КонецФункции // ПрочитатьJSONСОбработкойОшибки()

// ДУБЛЬ В УПРАВЛЯЕМОЙ ФОРМЕ 
&НаКлиенте
Функция ПолучитьТипыJSONБЕзЗначений() Экспорт
	
	//Null (Null)
	//Булево (Boolean)
	//ИмяСвойства (PropertyName)
	//Комментарий (Comment)
	//КонецМассива (ArrayEnd)
	//КонецОбъекта (ОbjectEnd)
	//НачалоМассива (ArrayStart)
	//НачалоОбъекта (ObjectStart)
	//Ничего (None)
	//Строка (String)
	//Число (Number)
	
	лМассивТиповJSONБезЗначений = Новый Массив;
	лМассивТиповJSONБезЗначений.Добавить(ТипЗначенияJSON.НачалоМассива);
	лМассивТиповJSONБезЗначений.Добавить(ТипЗначенияJSON.КонецМассива);
	лМассивТиповJSONБезЗначений.Добавить(ТипЗначенияJSON.НачалоОбъекта);
	лМассивТиповJSONБезЗначений.Добавить(ТипЗначенияJSON.КонецОбъекта);
	
	Возврат лМассивТиповJSONБезЗначений;
КонецФункции // ПолучитьТипыJSONБЕзЗначений()

Функция ПрочитатьЗапросыИзФайлаJSONРекурсивно(Запросы, Знач ВеткаДерева = Неопределено, ЗаменяемыеИД = Неопределено)
	
	Если ВеткаДерева = Неопределено Тогда 
		ВеткаДерева = ДеревоЗапросов;
	КонецЕсли;
	
	Для каждого лСтруктураЗапроса Из Запросы Цикл
		лНоваяСтрока = ВеткаДерева.Строки.Добавить();			
		ЗаполнитьЗначенияСвойств(лНоваяСтрока, лСтруктураЗапроса);
		Если ЗаменяемыеИД <> Неопределено Тогда 
			лНовыйИД = Новый УникальныйИдентификатор;
			ЗаменяемыеИД.Вставить(лНоваяСтрока.Идентификатор, лНовыйИД);
			лНоваяСтрока.Идентификатор = лНовыйИД;
		КонецЕсли;
		ПрочитатьЗапросыИзФайлаJSONРекурсивно(лСтруктураЗапроса.Строки, лНоваяСтрока, ЗаменяемыеИД);
	КонецЦикла; 
	
	Возврат лНоваяСтрока
	
КонецФункции // ПрочитатьЗапросыИзФайлаJSONРекурсивно()

&НаКлиенте
//#дубль функции из управляемой формы
Функция ПрочитатьФайлСЗапросомJSON(локально, идФайла, веткаПриемника, текстОшибки, добавить)
	
	Перем лВерсияФайла, лДата, лНоваяСтрока;
	
	Если Не добавить Тогда 
		ОчиститьДанныеПоЗапросам();
	КонецЕсли;	
	
	ЧтениеJSON = Новый ЧтениеJSON;
	Ошибка     = Ложь;
	
	Если Локально Тогда 
		Попытка
			ЧтениеJSON.ОткрытьФайл(идФайла);
		Исключение
			ТекстОшибки = "Ошибка открытия файла: " + ОписаниеОшибки();
			Возврат - 1;
		КонецПопытки; 
		
		Попытка
			лДанные = ПрочитатьJSON(ЧтениеJSON);
		Исключение 
			ТекстОшибки = "Ошибка формата файла: " + ОписаниеОшибки();
			Возврат 0;
		КонецПопытки;		
	Иначе		
		лСоединение = гВосстановитьИдентификаторСессииConsqueryCloud();
		лРезультат = гПолучитьЗапросыИзОблака(лСоединение, идФайла, null, null, Истина, мНастройкиПрокси);
		
		Если Не лРезультат.Статус = "OK" Тогда 
			Если лРезультат.НеобходимоПереподключиться Тогда 
				Оповестить("НеобходимоПереподключиться", , ЭтаФорма);
			КонецЕсли;
			Ошибка = Истина;
			ТекстОшибки = "Ошибка получения запросов в облаке: " + лРезультат.ТекстОшибки;
			Возврат - 1;
		Иначе
			лДанные = лРезультат.ДанныеИзОблака;
		КонецЕсли;
	КонецЕсли;
	
	Если Не Ошибка Тогда 
		Попытка
			
			// читаем запросы
			ПрочитатьЗапросыИзФайлаJSONРекурсивно(лДанные.querys, веткаПриемника);
			
			// читаем параметры
			Для каждого лПараметр Из лДанные.parameters Цикл
				лНоваяСтрока = ПараметрыЗапросов.Добавить();
				ЗаполнитьЗначенияСвойств(лНоваяСтрока, лПараметр);
				Если ПустаяСтрока(лНоваяСтрока.ИдентификаторСтроки) Тогда 
					лНоваяСтрока.ИдентификаторСтроки = Новый УникальныйИдентификатор //#рефакторинг необходимо понять откуда приходит пустой Ид
				КонецЕсли;
			КонецЦикла; 
			
			// читаем исполняемый код
			Для каждого лСтрокаСкодом Из лДанные.codes Цикл
				лНоваяСтрока = ИсполняемыйКод.Добавить();
				ЗаполнитьЗначенияСвойств(лНоваяСтрока, лСтрокаСкодом);
				Если ПустаяСтрока(лНоваяСтрока.ИдентификаторСтроки) Тогда 
					лНоваяСтрока.ИдентификаторСтроки = Новый УникальныйИдентификатор //#рефакторинг необходимо понять откуда приходит пустой Ид
				КонецЕсли;
			КонецЦикла; 
			
			Возврат 1;
		Исключение
			ЧтениеJSON.Закрыть();
			Если Не Локально Тогда 
				ТекстОшибки = ОписаниеОшибки();
				Возврат - 1;
			КонецЕсли;
		КонецПопытки; 
	КонецЕсли;
	
	////#рефакторинг +++чтение промежуточного формата файла. В будущем необходимо удалить
	//ЧтениеJSON = Новый ЧтениеJSON;
	//Попытка
	//	ЧтениеJSON.ОткрытьФайл(мИмяФайла);
	//	
	//	лТипыСНачальнымБлоком = Новый Массив;
	//	лТипыСНачальнымБлоком.Добавить(ТипЗначенияJSON.НачалоМассива);
	//	лТипыСНачальнымБлоком.Добавить(ТипЗначенияJSON.НачалоОбъекта);
	//	
	//	лСтэкБлоков = Новый Массив;
	//	
	//	лМассивТиповJSONБезЗначений = ПолучитьТипыJSONБЕзЗначений();

	//	Пока ПрочитатьJSONСОбработкойОшибки(ЧтениеJSON, ТекстОшибки) Цикл
	//		
	//		ТипJSON = ЧтениеJSON.ТипТекущегоЗначения;
	//		
	//		лЭлементСтэка = Новый Структура("ТекущаяПозиция,ТекущаяСтрока", ЧтениеJSON.ТекущаяПозиция, ЧтениеJSON.ТекущаяСтрока);
	//		
	//		Если лМассивТиповJSONБезЗначений.Найти(ЧтениеJSON.ТипТекущегоЗначения) = Неопределено Тогда 
	//			лЭлементСтэка.Вставить("ТекущееЗначение"    , ЧтениеJSON.ТекущееЗначение);
	//		КонецЕсли;
	//		лЭлементСтэка.Вставить("ТипТекущегоЗначения", ТипJSON);
	//		
	//		лСтэкБлоков.Вставить(0, лЭлементСтэка);
	//		Если лСтэкБлоков.Количество() = 2 Тогда 
	//			Если лСтэкБлоков[1].ТипТекущегоЗначения = ТипЗначенияJSON.ИмяСвойства Тогда 
	//				лОчищатьСтэк = Истина;
	//				Если лСтэкБлоков[1].ТекущееЗначение = "Версия" Тогда 
	//					лВерсияФайла = лСтэкБлоков[0].ТекущееЗначение;
	//				ИначеЕсли лСтэкБлоков[1].ТекущееЗначение = "Дата" Тогда 
	//					лДата = СериализаторXDTO.XMLЗначение(Тип("Дата"), лСтэкБлоков[0].ТекущееЗначение);
	//				Иначе
	//					лОчищатьСтэк = Ложь;
	//				КонецЕсли;
	//				Если лОчищатьСтэк Тогда 
	//					лСтэкБлоков.Удалить(0); // удаляем текущее значение
	//					лСтэкБлоков.Удалить(0); // удаляем имя свойства
	//				КонецЕсли;
	//			КонецЕсли;
	//		ИначеЕсли лСтэкБлоков.Количество() = 3 Тогда 
	//			Если лСтэкБлоков[0].ТипТекущегоЗначения = ТипЗначенияJSON.НачалоОбъекта Тогда 
	//				Если лСтэкБлоков[2].ТекущееЗначение = "Параметры" Тогда 
	//					лНоваяСтрока = ПараметрыЗапросов.Добавить();//#добавлениепараметразапроса 3
	//				ИначеЕсли лСтэкБлоков[2].ТекущееЗначение = "ИсполняемыйКод" Тогда 
	//					лНоваяСтрока = ИсполняемыйКод.Добавить();			
	//				КонецЕсли;
	//			ИначеЕсли лСтэкБлоков[0].ТипТекущегоЗначения = ТипЗначенияJSON.КонецМассива Тогда 
	//				лСтэкБлоков.Удалить(0); // удаляем конец тэга
	//				лСтэкБлоков.Удалить(0); // удаляем начало тэга
	//				лСтэкБлоков.Удалить(0); // удаляем начало тэга табличного блока (в каждом блоке только один массив!)
	//			КонецЕсли;
	//		ИначеЕсли лСтэкБлоков.Количество() = 4 Тогда 
	//			Если лСтэкБлоков[0].ТипТекущегоЗначения = ТипЗначенияJSON.КонецОбъекта Тогда 
	//				лСтэкБлоков.Удалить(0); // удаляем конец тэга
	//				лСтэкБлоков.Удалить(0); // удаляем начало тэга
	//			КонецЕсли;
	//		ИначеЕсли лСтэкБлоков.Количество() = 5 Тогда 
	//			Если лСтэкБлоков[1].ТипТекущегоЗначения = ТипЗначенияJSON.ИмяСвойства Тогда 
	//				Если лСтэкБлоков[1].ТекущееЗначение = "ИдентификаторРодителя" Тогда 
	//					Если ЗначениеЗаполнено(лСтэкБлоков[0].ТекущееЗначение) Тогда 
	//						лНоваяСтрока = ДеревоЗапросов.Строки.НайтиСтроки(Новый Структура("Идентификатор", лСтэкБлоков[0].ТекущееЗначение), Истина)[0].Строки.Добавить();			
	//					Иначе
	//						мПрограммноеДобавлениеСтрокиЗапроса = Истина;
	//						лНоваяСтрока = ДеревоЗапросов.Строки.Добавить();			
	//					КонецЕсли;
	//				Иначе
	//					лНоваяСтрока[лСтэкБлоков[1].ТекущееЗначение] = лСтэкБлоков[0].ТекущееЗначение;
	//				КонецЕсли;
	//				
	//				лСтэкБлоков.Удалить(0); // удаляем текущее значение
	//				лСтэкБлоков.Удалить(0); // удаляем имя свойства
	//			КонецЕсли;
	//		КонецЕсли;
	//	КонецЦикла;
	//
	//Исключение
	//	ТекстОшибки = "Ошибка формата файла: " + ОписаниеОшибки();	
	//	Возврат -1
	//КонецПопытки; 
	////#рефакторинг ---чтение промежуточного формата файла. В будущем необходимо удалить
	
	Если ЗначениеЗаполнено(ТекстОшибки) Тогда 
		Возврат 0
	Иначе
		Возврат 1
	КонецЕсли;
	
КонецФункции // ПрочитатьФайлСЗапросомJSON()

// Загружает дерево запросов Из файла
//
// Параметры:
//  Нет.
//
Функция ЗагрузитьЗапросыИзФайлаСтараяВерсия(имяФайла, ТекстОшибки) Экспорт

	Ошибка = Ложь;
	
	ОчиститьДанныеПоЗапросам();
	
	ФайлЗначения = Новый Файл(имяФайла);
	Если ФайлЗначения.Существует() Тогда 
		Попытка
			ДанныеИзФайла = ЗначениеИзФайла(имяФайла);
			Если ТипЗнч(ДанныеИзФайла) = Тип("ДеревоЗначений") Тогда 
				Если ДанныеИзФайла.Колонки.Найти("Идентификатор") = Неопределено Тогда 
					ДанныеИзФайла.Колонки.Добавить("Идентификатор");
				КонецЕсли;
			Иначе
				Ошибка = Истина;
				ТекстОшибки = "Тип загружаемых данных(" + ТипЗнч(ДанныеИзФайла) + ") отличается от ДереваЗначений. Обратитесь к разработчику";	
			КонецЕсли;
		Исключение
			Ошибка = Истина;
			ТекстОшибки = ОписаниеОшибки();	
		КонецПопытки; 
	Иначе
		Ошибка = Истина;
		ТекстОшибки = "Не найден файл: " + имяФайла;
	КонецЕсли;
	
	Если Не Ошибка Тогда 
		Попытка
			СкопироватьДеревоЗапросовРекурсивно(ДанныеИзФайла, ДеревоЗапросов, Новый Структура("ОбновлятьИД, ЭтоСтарыйРелиз", Истина, Истина));
		Исключение
			Ошибка = Истина;
			ТекстОшибки = ОписаниеОшибки();	
		КонецПопытки; 
	КонецЕсли;
	
	мИмяФайла = "";
	Модифицированность = Ложь;
	
	Возврат Не Ошибка
	
КонецФункции // ЗагрузитьЗапросыИзФайлаСтараяВерсия()

/////////////////////////////////////////
// Печать

// Рефакторинг. Объединить с функционалом ФормированиеПредставленияЗначения
Функция ЗначениеКВыводу(Значение)
	
	Если ТипЗнч(Значение) = Тип("СписокЗначений") Тогда 
		
		Результат = "";
		Для каждого ТекЗначение Из Значение Цикл
			Результат = Результат + ?(Результат = "", "", Символы.ПС) + ТекЗначение;
		КонецЦикла; 
		
	Иначе
		Результат = Значение
	КонецЕсли;
	
	Возврат Результат
	
КонецФункции // ЗначениеКВыводу()

&НаКлиенте
Процедура ПечатьТЧ(ИсточникДанных, ТабДок, СтруктураПараметров) Экспорт
	
	Инд = 1;
	
	ИсточникДанныхЛок = ?(ТипЗнч(ИсточникДанных) = Тип("ДеревоЗначений") 
	ИЛИ ТипЗнч(ИсточникДанных) = Тип("СтрокаДереваЗначений"), ИсточникДанных.Строки, ИсточникДанных);
	
	Для каждого Выборка Из ИсточникДанныхЛок Цикл
		
		Инд = Инд + 1;
		
		ОбработкаПрерыванияПользователя();
		
		мФормаПрогрессора.ЗначениеИндикатора = Инд;
		мФормаПрогрессора.НадписьСостоянияИндикатораТекущая = "Выводится " + Инд + " - я строка";
		
		Если ТипЗнч(Выборка) = Тип("СтрокаДереваЗначений") И Выборка.Строки.Количество() > 0 Тогда 
			ИсходнаяСтрока = СтруктураПараметров.ОбластьИерархическихЗаписей;
		Иначе
			ИсходнаяСтрока = СтруктураПараметров.ДетальнаяСтрока;
		КонецЕсли;
		
		Для ТекущееПоле = 0 По СтруктураПараметров.КоличествоКолонок - 1 Цикл
			Область = ИсходнаяСтрока.Область(1, ТекущееПоле + 1);
			Область.Текст = ЗначениеКВыводу(Выборка[ТекущееПоле]);
			Область.Расшифровка = Выборка[ТекущееПоле];
		КонецЦикла;
		
		ТабДок.Вывести(ИсходнаяСтрока, ?(ТипЗнч(Выборка) = Тип("СтрокаДереваЗначений"),Выборка.Уровень(),0));
		
		Если ТипЗнч(Выборка) = Тип("СтрокаДереваЗначений") И Выборка.Строки.Количество() > 0 Тогда 
			ПечатьТЧ(Выборка, ТабДок, СтруктураПараметров);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры // ПечатьТЧ() 

&НаКлиенте
Функция ВывестиНаПечать(ДанныеДляПечати) Экспорт
	
	Перем ЗаголовокКолонки;
	
	Если ТипЗнч(ДанныеДляПечати) = Тип("РезультатЗапроса") Тогда
		
		ИсточникДанных = ДанныеДляПечати.Выгрузить();
		
	ИначеЕсли ТипЗнч(ДанныеДляПечати) = Тип("ТаблицаЗначений") ИЛИ 
		
		ТипЗнч(ДанныеДляПечати) = Тип("ДеревоЗначений") Тогда 
		
		ИсточникДанных = ДанныеДляПечати.Скопировать();
		
	Иначе
		Сообщить("Для типа " + ТипЗнч(ДанныеДляПечати) + " функция печати не доступна", СтатусСообщения.Информация);
		Возврат Неопределено
	КонецЕсли;
	
	КоличествоСтрок = ?(ТипЗнч(ИсточникДанных) = Тип("ДеревоЗначений"), ИсточникДанных.Строки.Количество(), ИсточникДанных.Количество());
	КоличествоКолонок = ИсточникДанных.Колонки.Количество();
	
	Если КоличествоСтрок > 0 Тогда
		
		ТабДок = Новый ТабличныйДокумент;
		
		ДетальнаяСтрока = ТабДок.ПолучитьОбласть(1, , 1, );
		ОбластьОбщихИтогов = ТабДок.ПолучитьОбласть(1, , 1, );
		ОбластьОбщихИтогов.Область().Шрифт = Новый Шрифт(ОбластьОбщихИтогов.Область().Шрифт, , , Истина, , ,);
		ОбластьИерархическихЗаписей = ТабДок.ПолучитьОбласть(1, , 1, );
		ОбластьИерархическихЗаписей.Область().Шрифт = Новый Шрифт(ОбластьИерархическихЗаписей.Область().Шрифт, , , Истина, , ,);
		ОбластьГрупповыхЗаписей = ТабДок.ПолучитьОбласть(1, , 1, );
		ОбластьГрупповыхЗаписей.Область().Шрифт = Новый Шрифт(ОбластьГрупповыхЗаписей.Область().Шрифт, , , Истина, , ,);
		ОбластьЗаголовка = ТабДок.ПолучитьОбласть(1, , 1, );
		
		Для ТекущееПоле = 0 По КоличествоКолонок - 1 Цикл
			Область = ОбластьЗаголовка.Область(1, ТекущееПоле + 1);
			текКолонка = ИсточникДанных.Колонки[ТекущееПоле];
			Область.Текст = ?(текКолонка.Заголовок = "", текКолонка.Имя, текКолонка.Заголовок);
			Область.ШиринаКолонки = ИсточникДанных.Колонки[ТекущееПоле].Ширина;
			Если Область.ШиринаКолонки = 0 Тогда 
				Область.ШиринаКолонки = 10
			КонецЕсли;
		КонецЦикла;
		ТабДок.Вывести(ОбластьЗаголовка);
		ОбластьЗаголовка = ТабДок.Область(1, 1, 1, КоличествоКолонок);
		
		ОбластьЗаголовка.Шрифт = Новый Шрифт(ОбластьЗаголовка.Шрифт, , , Истина, , ,);
		ОбластьЗаголовка.ЦветФона = Новый Цвет(255, 255, 0);
		ОбластьЗаголовка.ГраницаСнИзу = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная, 1);
		
		мФормаПрогрессора.Открыть();
		мФормаПрогрессора.ЭлементыФормы.Индикатор.МаксимальноеЗначение = КоличествоСтрок;
		мФормаПрогрессора.НадписьСостоянияИндикатораОбщая = "Формирование печатной формы:";
		Инд = 1;
		мФормаПрогрессора.ЗначениеИндикатора = 1;
		
		ТабДок.НачатьАвтогруппировкуСтрок();
		
		СтруктураПараметров = Новый структура;
		СтруктураПараметров.Вставить("ДетальнаяСтрока", ДетальнаяСтрока);
		СтруктураПараметров.Вставить("ОбластьОбщихИтогов", ОбластьОбщихИтогов);
		СтруктураПараметров.Вставить("ОбластьИерархическихЗаписей", ОбластьИерархическихЗаписей);
		СтруктураПараметров.Вставить("ОбластьГрупповыхЗаписей", ОбластьГрупповыхЗаписей);
		СтруктураПараметров.Вставить("ОбластьЗаголовка", ОбластьЗаголовка);
		СтруктураПараметров.Вставить("КоличествоКолонок", КоличествоКолонок);
		
		ПечатьТЧ(ИсточникДанных, ТабДок, СтруктураПараметров);
		
		ТабДок.ЗакончитьАвтогруппировкуСтрок();
		
		мФормаПрогрессора.Закрыть();
		
		ТабДок.ОтображатьСетку = True;
		ТабДок.Защита = Ложь;
		ТабДок.ТолькоПросмотр = Истина;
		ТабДок.Показать("Результат запроса");
	КонецЕсли;
	
КонецФункции // ВывестиНаПечать()


///////////////////////////////////////////////////////////////////////////////
//+++ ЗАПИСЬфАЙЛАСЗАПРОСОМ

&НаКлиенте
Функция ПолучитьСтруктуруЗапросовДляJSONРекурсивно(Знач Корень, МассивИД, ВыгружатьТолькоИзмененные, КолонкиКВыгрузке, ЗаменяемыеИД, ИмяЗапроса)
	
	Запросы       = Новый Массив;
	лСтрокиДерева = Корень.Строки;
	Для Каждого лСтрокаДерева Из лСтрокиДерева Цикл
		
		лСтруктураСтрокиЗапроса = Новый Структура("Идентификатор, Имя, ТекстЗапроса");
		Если КолонкиКВыгрузке = Неопределено Тогда 
			ЗаполнитьЗначенияСвойств(лСтруктураСтрокиЗапроса, лСтрокаДерева);
		Иначе
			ЗаполнитьЗначенияСвойств(лСтруктураСтрокиЗапроса, лСтрокаДерева, КолонкиКВыгрузке);
		КонецЕсли;
		
		Если ИмяЗапроса <> Неопределено Тогда 
			лСтруктураСтрокиЗапроса.Имя  = ИмяЗапроса; // добавляем имя корневого запроса, которое указал пользователь в диалоге сохранения запроса
		КонецЕсли;
		
		Если ЗаменяемыеИД <> Неопределено Тогда 
			лНовыйИД = Строка(Новый УникальныйИдентификатор);
			ЗаменяемыеИД.Вставить(лСтрокаДерева.Идентификатор, лНовыйИД);
			лСтруктураСтрокиЗапроса.Идентификатор = лНовыйИД;
		КонецЕсли;
		
		лСтруктураСтрокиЗапроса.Вставить("Строки", ПолучитьСтруктуруЗапросовДляJSONРекурсивно(лСтрокаДерева, МассивИД, ВыгружатьТолькоИзмененные, КолонкиКВыгрузке, ЗаменяемыеИД, Неопределено));
		
		Если лСтруктураСтрокиЗапроса.Строки.Количество() > 0 ИЛИ Не ВыгружатьТолькоИзмененные ИЛИ гМассивИзмененныхСтрок.Найти(лСтрокаДерева.Идентификатор) <> Неопределено  Тогда 
			МассивИД.Добавить(лСтрокаДерева.Идентификатор);
			Запросы.Добавить(лСтруктураСтрокиЗапроса);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Запросы;
	
КонецФункции // ПолучитьСтруктуруЗапросовДляJSONРекурсивно()

&НаКлиенте
Функция ПолучитьПараметрыЗапросовДляJSON(МассивИдентификаторовЗапросов, ЗаменяемыеИД)

	лМассивПараметров = Новый Массив;
	
	Если МассивИдентификаторовЗапросов.Найти("") = Неопределено Тогда 
		// Добавляем пустой идентификатор - признак глобального параметра
		МассивИдентификаторовЗапросов.Добавить("");
	КонецЕсли;
	
	// добавляем локальные параметры
	Для Каждого ИдентификаторЗапроса Из МассивИдентификаторовЗапросов Цикл
		лПараметрыТекущегоЗапроса = ПараметрыЗапросов.НайтиСтроки(Новый Структура("ИдентификаторЗапроса", ИдентификаторЗапроса));
		Для каждого ПараметрТекущегоЗапроса Из лПараметрыТекущегоЗапроса Цикл
			Если ЗаменяемыеИД <> Неопределено Тогда 
				лИдентификаторЗапроса = ЗаменяемыеИД.Получить(ИдентификаторЗапроса);
				Если лИдентификаторЗапроса = Неопределено Тогда 
					лИдентификаторЗапроса = ИдентификаторЗапроса;
				КонецЕсли;
			Иначе
				лИдентификаторЗапроса = ИдентификаторЗапроса;
			КонецЕсли;
			лМассивПараметров.Добавить(Новый Структура("ИдентификаторЗапроса, ИдентификаторСтроки, Имя, Значение", лИдентификаторЗапроса, ПараметрТекущегоЗапроса.ИдентификаторСтроки, ПараметрТекущегоЗапроса.Имя, ПараметрТекущегоЗапроса.Значение));
		КонецЦикла; 
	КонецЦикла; 
	
	Возврат лМассивПараметров
	
КонецФункции //ПолучитьПараметрыЗапросовДляJSON()

&НаКлиенте
Функция ПолучитьИсполняемыйКодДляJSON(МассивИдентификаторовЗапросов, ЗаменяемыеИД)

	лМассивСтрокКода = Новый Массив;
	
	Для каждого ИдентификаторЗапроса Из МассивИдентификаторовЗапросов Цикл
		лКодТекущегоЗапроса = ИсполняемыйКод.НайтиСтроки(Новый Структура("ИдентификаторЗапроса", ИдентификаторЗапроса));
		Для каждого СтрокаСКодом Из лКодТекущегоЗапроса Цикл
			Если ЗаменяемыеИД <> Неопределено Тогда 
				лИдентификаторЗапроса = ЗаменяемыеИД.Получить(ИдентификаторЗапроса);
				Если лИдентификаторЗапроса = Неопределено Тогда 
					лИдентификаторЗапроса = ИдентификаторЗапроса;
				КонецЕсли;
			Иначе
				лИдентификаторЗапроса = ИдентификаторЗапроса;
			КонецЕсли;
			лМассивСтрокКода.Добавить(Новый Структура("ИдентификаторЗапроса, ИдентификаторСтроки, Имя, Текст", лИдентификаторЗапроса, СтрокаСКодом.ИдентификаторСтроки, СтрокаСКодом.Имя, СтрокаСКодом.Текст));
		КонецЦикла; 
	КонецЦикла; 
	
	Возврат лМассивСтрокКода

КонецФункции // ПолучитьИсполняемыйКодДляJSON()

//--- ЗАПИСЬфАЙЛАСЗАПРОСОМ
///////////////////////////////////////////////////////////////////////////////

Функция ПолучитьЗапросыВJSON(ИмяЗапроса, ИдПакетаВОблаке, ВыгружатьТолькоИзмененные, Знач СтрокаСЗапросом)
	
	лЛокально = Не ЗначениеЗаполнено(ИдПакетаВОблаке);
	лПодменитьИмяКорневогоЗапроса = (СтрокаСЗапросом <> Неопределено) И НЕ лЛокально;
	
	лМассивИДЗапросов = Новый Массив;              
	Если ВыгружатьТолькоИзмененные Тогда 
		лЗаменяемыеИД = Неопределено;
	Иначе
		лЗаменяемыеИД = Новый Соответствие;
	КонецЕсли;
	
	Если СтрокаСЗапросом = Неопределено Тогда 
		Корень = ДеревоЗапросов;
	Иначе
		лСтрокиДерева = Новый Массив;
		лСтрокиДерева.Добавить(СтрокаСЗапросом);
		Корень = Новый Структура("Строки", лСтрокиДерева);
	КонецЕсли;
	
	Данные = Новый Структура();
	Данные.Вставить("header"    , Новый Структура("version, date", гНазваниеОбработки(), ТекущаяДата()));
	Данные.Вставить("querys"    , ПолучитьСтруктуруЗапросовДляJSONРекурсивно(Корень, лМассивИДЗапросов, ВыгружатьТолькоИзмененные, Неопределено, лЗаменяемыеИД, ?(лПодменитьИмяКорневогоЗапроса, ИмяЗапроса, Неопределено)));
	Данные.Вставить("parameters", ПолучитьПараметрыЗапросовДляJSON(лМассивИДЗапросов, лЗаменяемыеИД));
	Данные.Вставить("codes"     , ПолучитьИсполняемыйКодДляJSON(лМассивИДЗапросов, лЗаменяемыеИД));
	
	Если ВыгружатьТолькоИзмененные Тогда 
		Данные.Вставить("querysToDel", гМассивИзмененныхСтрок);
		Данные.Вставить("querysSort", ПолучитьСтруктуруЗапросовДляJSONРекурсивно(Корень, лМассивИДЗапросов, Ложь, "Идентификатор", Неопределено, Неопределено));
	КонецЕсли;
	
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.ПроверятьСтруктуру = Ложь;
	Если Не лЛокально Тогда 
		ЗаписьJSON.УстановитьСтроку(Новый ПараметрыЗаписиJSON(,,,,,,Истина)); // экранируем одинарные кавычки
	Иначе
		ЗаписьJSON.ОткрытьФайл(ИмяЗапроса);
	КонецЕсли;
	
	ЗаписатьJSON(ЗаписьJSON, Данные, Новый НастройкиСериализацииJSON);
	
	Возврат ЗаписьJSON;
	
КонецФункции // ПолучитьЗапросыВJSON()

Функция ПроверитьМодифицированность(СтрогаяПроверка = Ложь)
	
	лРезультат = Истина;
	
	Если Модифицированность Тогда
		
		Если СтрогаяПроверка Тогда 
			лРежимДиалогаВопрос	 = РежимДиалогаВопрос.ОКОтмена;
		Иначе
			лРежимДиалогаВопрос	 = РежимДиалогаВопрос.ДаНетОтмена;
		КонецЕсли;
		
		Ответ = Вопрос(НСтр("ru = 'Сохранить изменения?'"), лРежимДиалогаВопрос);
		Если Ответ = КодВозвратаДиалога.Да ИЛИ Ответ = КодВозвратаДиалога.ОК Тогда
			лРезультат = СохранитьЗапросыЛокальноДействие(гОперацииСЗапросами().Сохранить);
		ИначеЕсли Ответ = КодВозвратаДиалога.Отмена Тогда
			лРезультат = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Возврат лРезультат;
	
КонецФункции

Процедура СохранитьЗапросыАвтосохранение()
	СохранитьЗапросыЛокальноДействие(гОперацииСЗапросами().Сохранить);
КонецПроцедуры // СохранитьЗапросыАвтосохранение()
 
Функция СохранитьЗапросыВОблакоДействие(Режим, ТекущийЗапрос = Неопределено)
	
	лИмяЭлементаИзОблака = мИмяФайла;
	
	СохранитьЗапросТекущейСтроки();
	
	Если ТекущийЗапрос = Неопределено Тогда 
		Если ПустаяСтрока(лИмяЭлементаИзОблака) Тогда 
			лИмяЭлементаИзОблака = "Запросы от " + Формат(ТекущаяДата(), "ДФ='dd.MM.yyyy ЧЧ_ММ_сс'");
		Иначе
			лИмяЭлементаИзОблака = СтрЗаменить(лИмяЭлементаИзОблака, ".sel", "");
		КонецЕсли;
		лТипДиалога =  гТипыИсточниковДанных().Пакет;
	Иначе
		лИмяЭлементаИзОблака = ТекущийЗапрос.Имя;
		лТипДиалога          = гТипыИсточниковДанных().Запрос;
	КонецЕсли;
	
	лСоединение = гВосстановитьИдентификаторСессииConsqueryCloud();
	Если Не ЗначениеЗаполнено(лСоединение) Тогда 
		Оповестить("НеобходимоПереподключиться", , ЭтаФорма);
		Возврат Ложь;
	Иначе			
		лФормаДиалога = ПолучитьФорму("ФормаДиалогВыбораИсточникаДанныхИзОблака", ЭтаФорма);
		лФормаДиалога.Режим               = Режим;
		лФормаДиалога.Тип                 = лТипДиалога;
		лФормаДиалога.ИмяЭлементаИзОблака = лИмяЭлементаИзОблака;
		Длг = лФормаДиалога.ОткрытьМодально();
		
		Если Длг = Неопределено Или Не Длг.Выбран Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;

	ЗаписьJSON = ПолучитьЗапросыВJSON(Длг.Имя, Длг.ИдЗапроса, Ложь, ТекущийЗапрос);
	
	лСоединение = гВосстановитьИдентификаторСессииConsqueryCloud();
	лДанные     = ЗаписьJSON.Закрыть();
	
	Если ТекущийЗапрос = Неопределено Тогда 
		лРезультат  = гСохранитьФайлСЗапросомВОблаке(лСоединение, Длг.ИдПакета, Длг.Имя, истина, лДанные); //#рефакторинг добавить использование настройки режима обновления всех записей или только измененных
	Иначе
		лРезультат  = гСохранитьЗапросыВОблаке(лСоединение, Длг.ИдПакета, Длг.ИдРодительскогоЗапроса, Длг.ИдЗапроса, Длг.Имя, Длг.ВключатьПодчиненныеЗапросы, лДанные);
	КонецЕсли;
	
	Если Не лРезультат.Статус = "OK" Тогда 
		Ошибка = Истина;
		Сообщить("Ошибка сохранения в облаке: " + лРезультат.ТекстОшибки);
	Иначе
		Ошибка = Ложь;
		ПоказатьПредупреждение(, ?(ТекущийЗапрос = Неопределено, "Запросы выгружены в облако.", "Текущий запрос выгружен в облако."), 3);
	КонецЕсли;

	Возврат Не Ошибка;
	
КонецФункции // СохранитьЗапросыВОблакоДействие()

Функция СохранитьЗапросыЛокальноДействие(Режим, ТекущийЗапрос = Неопределено)

	лПутьКФайлу              = "";
	лПолноеИмяФайла          = мИмяФайла;
	лЭтоВыгрузкаВсехЗапросов = (ТекущийЗапрос = Неопределено);
	лОткрыватьДиалог         = (Режим = гОперацииСЗапросами().СохранитьКАК);
	
	СохранитьЗапросТекущейСтроки();
	
	Если лЭтоВыгрузкаВсехЗапросов Тогда 
		Если Не ПустаяСтрока(лПолноеИмяФайла) Тогда 
			лВремФайл = Новый Файл(лПолноеИмяФайла); 
			Если лВремФайл.Существует() Тогда 
				лПутьКФайлу     = лВремФайл.Путь; 
				лИмяФайла       = лВремФайл.Имя;	
			Иначе
				лИмяФайла = СтрЗаменить(лПолноеИмяФайла, ".sel", "");
				лОткрыватьДиалог = Истина;
			КонецЕсли;
		Иначе
			лИмяФайла = "Запросы от " + Формат(ТекущаяДата(), "ДФ='dd.MM.yyyy ЧЧ_ММ_сс'");
			лОткрыватьДиалог = Истина;
		КонецЕсли;
		
		лЗаголовок = "Укажите имя файла для сохранения запросов";
		лРасширение = "sel";
	Иначе
		лИмяФайла = ТекущийЗапрос.Имя;
		лЗаголовок = "Укажите файл для сохранения текущего запроса";
		лФильтр = "Файлы веток (*.sl)|*.sl";
		лРасширение = "sl";
	КонецЕсли;
	
	Если лОткрыватьДиалог Тогда		
		Длг = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
		Длг.ПолноеИмяФайла = лПутьКФайлу + лИмяФайла;
		Длг.Каталог        = лПутьКФайлу;
		Длг.Заголовок      = лЗаголовок;
		Длг.Фильтр         = лФильтр;
		Длг.Расширение     = лРасширение;
		
		Если Не Длг.Выбрать() Тогда
			Возврат Ложь;
		КонецЕсли;
		
		лПолноеИмяФайла = Длг.ПолноеИмяФайла;			
	КонецЕсли;

	ЗаписьJSON = ПолучитьЗапросыВJSON(лПолноеИмяФайла, Неопределено, Ложь, ТекущийЗапрос);
	
	Попытка
		ЗаписьJSON.Закрыть();
		Ошибка = Ложь;
	Исключение
		Ошибка = Истина;
		Сообщить(ОписаниеОшибки(), СтатусСообщения.Важное);		
	КонецПопытки; 
	
	Если Не Ошибка Тогда 
		////очищаем список измененных строк
		//Для каждого идСтрокиЗапроса Из лМассивИДЗапросов Цикл
		//	гМассивИзмененныхСтрок.Удалить(гМассивИзмененныхСтрок.Найти(идСтрокиЗапроса)); //#рефакторинг проверить существование в массиве лгМассивИзмененныхСтрок строк, отсутствующих в массиве МассивИДЗапросов 
		//КонецЦикла;			
		мИмяФайла = лПолноеИмяФайла;
		гМассивИзмененныхСтрок.Очистить();
		ОбновитьЗаголовок();
		Модифицированность = Ложь;
	КонецЕсли;
	
	Возврат Не Ошибка;
		
КонецФункции // СохранитьЗапросыЛокальноДействие()

Функция ОткрытьЗапросыДействие(Локально, Знач идЗагружаемогоОбъекта = Неопределено)

	Если Локально И НЕ ПроверитьМодифицированность() Тогда
		Возврат Ложь
	Иначе
		
		лВеткаПриемника         = Неопределено;
		лТипЗагружаемогоОбъекта = Неопределено;
		
		Если идЗагружаемогоОбъекта = Неопределено Тогда 
			Если Локально Тогда 
				Длг = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
				
				лВремФайл = Новый Файл(мИмяФайла); лПутьКФайлу = лВремФайл.Путь; 
				
				Длг.ПолноеИмяФайла = мИмяФайла;
				Длг.Каталог        = лПутьКФайлу;
				Длг.Заголовок      = "Выберите файл со списком запросов";
				Длг.Фильтр         = "Файлы запросов (*.sel)|*.sel|Все файлы (*.*)|*.*";
				Длг.Расширение     = "sel";
				
				Если Не Длг.Выбрать() Тогда
					Возврат Ложь;
				КонецЕсли;		
				
				мИмяФайла = Длг.ПолноеИмяФайла;
				идЗагружаемогоОбъекта = мИмяФайла;
				
				Ошибка =  НЕ ЗагрузитьЗапросы(Локально, идЗагружаемогоОбъекта, лВеткаПриемника, лТипЗагружаемогоОбъекта);

			Иначе
				
				лСоединение = гВосстановитьИдентификаторСессииConsqueryCloud();
				Если Не ЗначениеЗаполнено(лСоединение) Тогда 
					Оповестить("НеобходимоПереподключиться", , ЭтаФорма);
					Возврат Ложь;
				Иначе					
					лФормаДиалога = ПолучитьФорму("ФормаДиалогВыбораИсточникаДанныхИзОблака", ЭтаФорма);
					лФормаДиалога.Режим = гОперацииСЗапросами().ЗагрузитьИзОблака;
					Длг = лФормаДиалога.ОткрытьМодально();
					
					Если Длг = Неопределено Или Не Длг.Выбран Тогда
						Возврат Ложь;
					КонецЕсли;
					
					//необходимо хранить параметры текущего выбранного элемента (пакет, запрос. код)
					//доработать алгоритм открытия этих элементов
					//привести все названия элементов к единым (файл -> пакет )
					
					мИмяФайла = "";
					
					СохранитьЗапросТекущейСтроки();
					
					Если Длг.Тип = гТипыИсточниковДанных().Пакет Тогда 
						РодительТекущейСтроки = ?(мТекущаяСтрока.Родитель = Неопределено, ДеревоЗапросов, мТекущаяСтрока.Родитель);
						лВеткаПриемника = РодительТекущейСтроки.Строки.Добавить();
						лВеткаПриемника.Имя            = Длг.Имя;
						лВеткаПриемника.Идентификатор  = Новый УникальныйИдентификатор;
						лВеткаПриемника.СпособВыгрузки = 1;
						//ЭлементыФормы.ДеревоЗапросов.ТекущаяСтрока = лВеткаПриемника;		
					Иначе
						лВеткаПриемника = ЭлементыФормы.ДеревоЗапросов.ТекущаяСтрока.Родитель;
					КонецЕсли;					
					
					Ошибка = НЕ ЗагрузитьЗапросыИзОблака(Длг.идПакета, Длг.идЗапроса, Длг.идКода, Длг.ВключатьПодчиненныеЗапросы, лВеткаПриемника)
					
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Возврат Не Ошибка;
		
	КонецЕсли;
	
КонецФункции // ОткрытьЗапросыДействие()

Функция КонвертироватьВнешнююТаблицу(ВнешняяТаблица)
	
	Результат = новый ТаблицаЗначений;
	
	Для каждого КолонкаТаблицы Из ВнешняяТаблица.Колонки Цикл
		Результат.Колонки.Добавить(КолонкаТаблицы.Имя) ;
	КонецЦикла; 
	
	Для каждого СтрокаТаблицы Из ВнешняяТаблица Цикл
		НоваяСтрока = Результат.Добавить();
		Для каждого КолонкаТаблицы Из ВнешняяТаблица.Колонки Цикл
			НоваяСтрока[КолонкаТаблицы.Имя] = СтрокаТаблицы[КолонкаТаблицы.Имя];
		КонецЦикла; 
	КонецЦикла; 
	
	Возврат Результат
	
КонецФункции

// Загружает результат запроса в таблицу или сводную таблицу
//
// Параметры:
//  Нет.
//
Процедура ЗагрузитьРезультат(ЭлементТаблицаРезультата = Неопределено)
	
	Если мРезЗапроса <> Неопределено Тогда
		
		Если ЭлементТаблицаРезультата = Неопределено Тогда
			ЭлементТаблицаРезультата = Вычислить("ЭлементыФормы." + мИмяРеквизитаТаблицы);
		Конецесли;
		
		Если ЭлементыФормы.ПанельРезультата.ТекущаяСтраница.Имя = "СводнаяТаблица" Тогда
			Если мСводнаяТаблицаЗагружена = Ложь Тогда
				Попытка
					ЭлементыФормы.РезультатТабДокСвод.ВстроенныеТаблицы.СводнаяТаблица.ИсточникДанных = мРезЗапроса;
				Исключение
				КонецПопытки;
				мСводнаяТаблицаЗагружена = Истина;
			КонецЕсли;
		Иначе
			Если мТаблицаЗагружена = Ложь Тогда
				ЭлементТаблицаРезультата.Колонки.Очистить();
				Если СпособВыгрузки = 2 И ТипЗнч(мРезЗапроса) <> Тип("ComОбъект") Тогда // Дерево
					Если ФлажокИтогиВсе Тогда
						РезультатДерево = кзЗаполнитьДеревоСПризнакомВСЕ(ЭтаФорма, мРезЗапроса);
					Иначе
						РезультатДерево = мРезЗапроса.Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
					КонецЕсли;
					ЭлементТаблицаРезультата.Данные = "РезультатДерево";
					ЭлементТаблицаРезультата.СоздатьКолонки();
				Иначе // Список
					Если ТипЗнч(мРезЗапроса) = Тип("ComОбъект") Тогда
						ТекРезультатЗапроса = мРезЗапроса.Выгрузить();
						
						ТекРезультатТаблица = новый ТаблицаЗначений;
						Для каждого КолонкаТаблицы Из ТекРезультатЗапроса.Колонки Цикл
							ТекРезультатТаблица.Колонки.Добавить(КолонкаТаблицы.Имя) ;
						КонецЦикла; 
						Для каждого СтрокаТаблицы Из ТекРезультатЗапроса Цикл
							НоваяСтрока = ТекРезультатТаблица.Добавить();							
							Для каждого КолонкаТаблицы Из ТекРезультатТаблица.Колонки Цикл
								НоваяСтрока[КолонкаТаблицы.Имя] = СтрокаТаблицы[КолонкаТаблицы.Имя];
							КонецЦикла; 
						КонецЦикла; 
						//ТаблицаРезультата = КонвертироватьВнешнююТаблицу(мРезЗапроса.Выгрузить());
						ЭлементТаблицаРезультата.Значение = ТекРезультатТаблица.Скопировать();
						ЭлементТаблицаРезультата.СоздатьКолонки();
					Иначе
						Если ТипЗнч(мРезЗапроса) = Тип("Массив") Тогда //пакет запросов
							лРезультатЗапроса = Новый ТаблицаЗначений;
							лРезультатЗапроса.Колонки.Добавить("РезультатЗапроса");
							Для каждого текРезультат Из мРезЗапроса Цикл
								НоваяСтрока = лРезультатЗапроса.Добавить();
								НоваяСтрока.РезультатЗапроса = текРезультат.Выгрузить();
							КонецЦикла; 
						Иначе
							лРезультатЗапроса = мРезЗапроса.Выгрузить();
						КонецЕсли;
						
						Выполнить(мИмяРеквизитаТаблицы + " = лРезультатЗапроса");
						
						ЭлементТаблицаРезультата.Данные = мИмяРеквизитаТаблицы;
						ЭлементТаблицаРезультата.СоздатьКолонки();
					Конецесли;
				КонецЕсли;
				мТаблицаЗагружена = Истина;
			КонецЕсли;
		КонецЕсли;

	КонецЕсли;
		
КонецПроцедуры // ЗагрузитьРезультат()

// Включает или отключает запуск автосохранения.
//
// Параметры:
//  Нет.
//
Процедура ОбработкаАвтосохранения()

	Если ИспользоватьАвтосохранение Тогда
		ПодключитьОбработчикОжидания("СохранитьЗапросыАвтосохранение", ИнтервалАвтосохранения);
	Иначе
		ОтключитьОбработчикОжидания("СохранитьЗапросыАвтосохранение");
	КонецЕсли;

КонецПроцедуры // ОбработкаАвтосохранения()

///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ КОМАНДНОЙ ПАНЕЛИ

// Обработчик нажатия кнопки командной панели "Новый список запросов"
//
Процедура КоманднаяПанельФормыНовыйФайл(Кнопка)
	Если ПроверитьМодифицированность() Тогда
		СоздатьНовыйФайлЗапросов();
	КонецЕсли;
КонецПроцедуры // НовыйФайл()

// Обработчик нажатия кнопки командной панели "Открыть файл запросов"
//
Процедура КоманднаяПанельФормыОткрытьФайл(Кнопка)
	ОткрытьЗапросыДействие(Истина);
КонецПроцедуры // ОткрытьФайл()

Процедура КоманднаяПанельФормыСохранить(Кнопка)
	СохранитьЗапросыЛокальноДействие(гОперацииСЗапросами().Сохранить);
КонецПроцедуры

Процедура КоманднаяПанельФормыДобавитьЗапросИзОблака(Кнопка)
	ОткрытьЗапросыДействие(Ложь);
КонецПроцедуры

Процедура КоманднаяПанельФормыВыполнитьЗапрос(Кнопка)
	
	Если ЗначениеЗаполнено(мТекущаяСтрока) И ЗначениеЗаполнено(мТекущаяСтрока.Идентификатор) И гСоответствиеЗапросы.Получить(мТекущаяСтрока.Идентификатор) <> Неопределено Тогда 
		ВыполнитьЗапросИзФормы(гСоответствиеЗапросы[мТекущаяСтрока.Идентификатор]);
	Иначе
		ВыполнитьЗапросИзФормы();
	КонецЕсли;
	
	кзУстановитьШиринуКолонок(ЭлементыФормы, мСтруктураСРазмерами);

	ЭлементыФормы.ПанельРезультата.ТекущаяСтраница = ЭлементыФормы.ПанельРезультата.Страницы[0]; //позиционируемся на первую страницу
	
КонецПроцедуры

Процедура КоманднаяПанельФормыВыполнитьЗапросПоСпискуИБ(Кнопка)
	ТекПараметрыИБ = мФормаПараметрыИБ.ТаблицаПараметров;
	Если ТекПараметрыИБ.Количество() = 0 Тогда
		ПоказатьПредупреждение(, "Не указаны параметры ИБ", 10);
		Возврат;
	Конецесли;
	
	Если Вопрос("Выполнить запросы по списку информационных баз?", РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	мФормаПрогрессора.Открыть();
	мФормаПрогрессора.ЭлементыФормы.Индикатор.МаксимальноеЗначение = ТекПараметрыИБ.Количество();
	мФормаПрогрессора.НадписьСостоянияИндикатораОбщая = "Формирование внешних запросов:";
	мФормаПрогрессора.ЗначениеИндикатора = 0;
	
	//Очищаем панель вывода отчета
	УдалитьСтраницыСВнешниимРезультатами();
	
	Для каждого ТекПараметрИБ Из ТекПараметрыИБ Цикл
		
		ОбработкаПрерыванияПользователя();
			
		мФормаПрогрессора.НадписьСостоянияИндикатораТекущая = "Формируется запрос по базе: " + ТекПараметрИБ.ИмяБазы;
		
		Если ТекПараметрИБ.Использовать Тогда
			СтруктураПараметров = Новый Структура("Установить, Версия, КаталогБазы, SQL_Сервер, SQL_БазаДанных, ТипБазы, ИмяПользователя, Пароль, ВыводитьСообщение", 
			Истина, ТекПараметрИБ.Версия,ТекПараметрИБ.КаталогБазы, ТекПараметрИБ.SQL_Сервер, ТекПараметрИБ.SQL_БазаДанных, ТекПараметрИБ.ФайловаяБаза, 
			ТекПараметрИБ.ИмяПользователя, ТекПараметрИБ.Пароль, Истина);
			V8COM = кзСвязьСВнешнейБазой(СтруктураПараметров);	
			Если V8COM <> Неопределено Тогда		
				//Добавляем закладку
				ТекИмяСтраницыРезультата = "СтраницаСРезультатом_" + ТекПараметрИБ.ИмяБазы;
				ТекИмяТаблицыРезультата = "ТаблицаСРезультатом_" + ТекПараметрИБ.ИмяБазы;
				ЭлементыФормы.ПанельРезультата.Страницы.Вставить(1,ТекИмяСтраницыРезультата, ТекПараметрИБ.ИмяБазы);
				ЭлементыФормы.ПанельРезультата.ТекущаяСтраница = ЭлементыФормы.ПанельРезультата.Страницы[ТекИмяСтраницыРезультата];
				//Добавить(<Тип > , <Имя > , <Видимость > , <Поместить на > ) 
				Элемент = ЭлементыФормы.Добавить(Тип("Табличноеполе"), ТекИмяТаблицыРезультата,,ЭлементыФормы.ПанельРезультата); 
				Элемент.КонтекстноеМеню = ЭлементыФормы.СпецМеню;
				Элемент.Лево = 6; Элемент.Верх = 6;
				Элемент.Ширина = ЭлементыФормы.ПанельРезультата.Ширина - 12; Элемент.Высота = ЭлементыФормы.ПанельРезультата.Высота - 12;
				Элемент.ТолькоПросмотр = Ложь;
				Элемент.УстановитьДействие("Выбор", Новый Действие("ТаблицаРезультатаВыбор"));
				//УстановитьПривязку(<Граница > , <Первый элемент > , <Граница первого элемента > , <Второй элемент > , <Граница второго элемента > ) 
				Элемент.УстановитьПривязку(ГраницаЭлементаУправления.Верх,ЭлементыФормы.ПанельРезультата,ГраницаЭлементаУправления.Верх);
				Элемент.УстановитьПривязку(ГраницаЭлементаУправления.НИз,ЭлементыФормы.ПанельРезультата,ГраницаЭлементаУправления.НИз);
				Элемент.УстановитьПривязку(ГраницаЭлементаУправления.Лево,ЭлементыФормы.ПанельРезультата,ГраницаЭлементаУправления.Лево);
				Элемент.УстановитьПривязку(ГраницаЭлементаУправления.Право,ЭлементыФормы.ПанельРезультата,ГраницаЭлементаУправления.Право);
				
				//Выполняем запросы во внешних базах
				Попытка
					ВыполнитьЗапросИзФормы(V8COM.NewObject("Запрос"), ЭлементыФормы[ТекИмяТаблицыРезультата]);
					кзУстановитьШиринуКолонок(ЭлементыФормы, мСтруктураСРазмерами);
				Исключение
					Сообщить("Ошибка при формировании запроса для базы: " + ТекПараметрИБ.ИмяБазы, СтатусСообщения.Важное);
					Сообщить(ОписаниеОшибки());
					Сообщить(" - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -");
				КонецПопытки; 
				
				V8COM = Неопределено;
			Иначе	
				Сообщить("Ошибка при формировании запроса для базы: " + ТекПараметрИБ.ИмяБазы, СтатусСообщения.Важное);
				Сообщить(" - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -");
			КонецЕсли;
			
			мФормаПрогрессора.ЗначениеИндикатора = мФормаПрогрессора.ЗначениеИндикатора + 1;
			
		КонецЕсли;
	КонецЦикла; 
	
	мФормаПрогрессора.Закрыть()
КонецПроцедуры

Процедура КоманднаяПанельФормыВыполнитьПакет(Кнопка)
	Если СпособВыгрузки <> 1 Тогда 
		ПоказатьПредупреждение(, "Функция доступна только в режиме способа выгрузки ""Список"".", 10);
		ТекущийЭлемент = ЭлементыФормы.СпособВыгрузки;
	Иначе
		ВыполнитьЗапросИзФормы(,,Истина);
		кзУстановитьШиринуКолонок(ЭлементыФормы, мСтруктураСРазмерами);
		ЭлементыФормы.ПанельРезультата.ТекущаяСтраница = ЭлементыФормы.ПанельРезультата.Страницы[0]; //позиционируемся на первую страницу
	КонецЕсли;
КонецПроцедуры

// Обработчик нажатия кнопки командной панели "Сохранить"
//
Процедура МенюДействияСохранитьКакЛокально(Кнопка)
	СохранитьЗапросыЛокальноДействие(гОперацииСЗапросами().СохранитьКАК);
КонецПроцедуры // Сохранить()

Процедура МенюДействияЗагрузитьВеткуЛокально(Кнопка)
	ЗагрузитьВеткуЛокальноДействие();
КонецПроцедуры

Процедура МенюДействияДобавитьЗапросИзОблака(Кнопка)
	ОткрытьЗапросыДействие(Ложь);
КонецПроцедуры

Процедура МенюДействиеВыгрузитьВсеЗапросыВОблако(Кнопка)
	СохранитьЗапросыВОблакоДействие(гОперацииСЗапросами().СохранитьКАК);
КонецПроцедуры

Процедура МенюДействияВыгрузитьТекущийЗапросВОблако(Кнопка)
	ВыгрузитьТекущийЗапросДействие(Ложь);
КонецПроцедуры

Процедура МенюДействияВыгрузитьТекущийЗапросЛокально(Кнопка)
	ВыгрузитьТекущийЗапросДействие(Истина);
КонецПроцедуры

Процедура МенюДействиеПараметрыПодключенияКОблаку(Кнопка)
	ПодключенияКОблакуДействие();
КонецПроцедуры

Процедура МенюДействияНастройкиПрокси(Кнопка)
	НастройкиПроксиДействие();
КонецПроцедуры

// Обработчик нажатия кнопки командной панели "Настройка автосохранения"
//
Процедура МенюДействияНастройкаАвтосохранения(Кнопка)
	ФормаНастройкиАвтосохранения = ПолучитьФорму("ФормаНастройкиАвтосохранения");
	ФормаНастройкиАвтосохранения.ОткрытьМодально();
	ОбработкаАвтосохранения();
КонецПроцедуры // НастройкаАвтосохранения()

Процедура МенюДействияСравнитьЗапросы(Кнопка)
	СравнитьЗапросыДействие();
КонецПроцедуры

Процедура МенюСпискаЗапросовЗагрузитьВетку(Кнопка)
	ЗагрузитьВеткуЛокальноДействие();
КонецПроцедуры

Процедура МенюСпискаЗапросовВыгрузитьТекущийЗапросЛокально(Кнопка)
	ВыгрузитьТекущийЗапросДействие(Истина);
КонецПроцедуры

Процедура МенюСпискаЗапросовВыгрузитьТекущийЗапросВОблако(Кнопка)
	ВыгрузитьТекущийЗапросДействие(Ложь);
КонецПроцедуры

Процедура МенюСпискаЗапросовСравнитьЗапросы(Кнопка)
	СравнитьЗапросыДействие();
КонецПроцедуры

Процедура МенюСпискаЗапросовСтрокаНаТомЖеУровне(Кнопка)
	РодительТекущейСтроки = ?(мТекущаяСтрока.Родитель = Неопределено, ДеревоЗапросов, мТекущаяСтрока.Родитель);
	ЭлементыФормы.ДеревоЗапросов.ТекущаяСтрока = РодительТекущейСтроки.Строки.Добавить();
КонецПроцедуры // МенюСпискаЗапросовСтрокаНаТомЖеУровне()


// Обработчик нажатия кнопки командной панели "Перенести в другую группу"
//
Процедура КоманднаяПанельФормыПеренестиСтрокуДерева(Кнопка)

	СохранитьЗапросТекущейСтроки();
	
	ФормаВыбораСтрокиДереваЗапросов = ПолучитьФорму("ФормаВыбораСтрокиДереваЗапросов", ЭтаФорма);
	ФормаВыбораСтрокиДереваЗапросов.ЗакрыватьПриВыборе = Истина;

	ФормаВыбораСтрокиДереваЗапросов.ДеревоЗапросов = ДеревоЗапросов;
	ФормаВыбораСтрокиДереваЗапросов.ТекущаяСтрокаВладельца = ЭлементыФормы.ДеревоЗапросов.ТекущаяСтрока;
	ФормаВыбораСтрокиДереваЗапросов.ЭлементыФормы.ДеревоЗапросов.ТекущаяСтрока = ЭлементыФормы.ДеревоЗапросов.ТекущаяСтрока;

	РежимВыбораВеткиДерева = "ПеренестиВДругуюВетку";
	ФормаВыбораСтрокиДереваЗапросов.ОткрытьМодально();

КонецПроцедуры // ПеренестиСтрокуДерева()

Функция кзСвязьСВнешнейБазой(СтруктураПараметров) Экспорт
	
	Результат = Истина;
	
	Установить       = СтруктураПараметров.Установить;
	Версия		 	 = СтруктураПараметров.Версия;
	КаталогБазы		 = СтруктураПараметров.КаталогБазы;
	SQL_Сервер		 = СтруктураПараметров.SQL_Сервер;
	SQL_БазаДанных	 = СтруктураПараметров.SQL_БазаДанных;
	ТипБазы			 = СтруктураПараметров.ТипБазы;
	ИмяПользователя	 = СтруктураПараметров.ИмяПользователя;
	Пароль			 = СтруктураПараметров.Пароль;
	ВыводитьСообщение= СтруктураПараметров.ВыводитьСообщение;
	
	
	Если Установить Тогда					
		
		Если Лев(Версия, 3) = "8.3" Тогда 
			V8COMConnector = Новый COMОбъект("V83.COMConnector");
		ИначеЕсли Лев(Версия, 3) = "8.2" Тогда 
			V8COMConnector = Новый COMОбъект("V82.COMConnector");
		ИначеЕсли Лев(Версия, 3) = "8.1" Тогда 
			V8COMConnector = Новый COMОбъект("V81.COMConnector");
		Иначе
			V8COMConnector = Новый COMОбъект("V8.COMConnector");
		КонецЕсли;
		
		Если ТипБазы = Истина Тогда
			//файловый вариант
			СтрокаСоединения = "File = """ + СокрЛП(КаталогБазы) + """";	
		Иначе
			//серверный вариант
			СтрокаСоединения = "Srvr = " + СокрЛП(SQL_Сервер) + ";Ref = """ + СокрЛП(SQL_БазаДанных) + """";
		КонецЕсли;
		
		Состояние("Соединение с базой: " + СтрокаСоединения);
		
		//добавим имя пользователя и пароль
		СтрокаСоединения = СтрокаСоединения + "; Usr = """ + ИмяПользователя + """; Pwd = """ + Пароль + """;";
		
		Попытка
			Результат = V8COMConnector.Connect(СтрокаСоединения); 
		Исключение
			Если ВыводитьСообщение Тогда
				Сообщить(ОписаниеОшибки(), СтатусСообщения.Важное);
			КонецЕсли;
			Результат = Неопределено;		
		КонецПопытки;
	Иначе
		Результат = Неопределено;		
	КонецЕсли;
	
	Возврат Результат
	
КонецФункции

// Обработчик нажатия кнопки командной панели "Параметры"
//
Процедура КоманднаяПанельФормыПараметры() Экспорт

	Если мФормаПараметров.Открыта() Тогда
		мФормаПараметров.Активизировать();
	Иначе
		мФормаПараметров.Открыть();
	КонецЕсли;

КонецПроцедуры // Параметры()

// Обработчик нажатия кнопки командной панели "Сохранить в табличный документ"
//
Процедура СохранитьРезультат()
	
	Перем ЗаголовокКолонки;
	
	Если мРезЗапроса <> Неопределено Тогда
		
		Если ТипЗнч(мРезЗапроса) = Тип("COMОбъект") Тогда 
			
			ПоказатьПредупреждение(, "Функция печати результата запроса доступна только для запроса по текущей базе.", 10);	
			
		Иначе
			
			Выборка = мРезЗапроса.Выбрать(ОбходРезультатаЗапроса.Прямой);

			Если Выборка.Количество() = 0 Тогда
				ПоказатьПредупреждение(, "Выборка пуста.", 10);
				Возврат;
			КонецЕсли;
			
			ТабДок = Новый ТабличныйДокумент;
			КоличествоКолонок = мРезЗапроса.Колонки.Количество();

	        ДетальнаяСтрока = ТабДок.ПолучитьОбласть(1, , 1, );
			ОбластьОбщихИтогов = ТабДок.ПолучитьОбласть(1, , 1, );
		    ОбластьОбщихИтогов.Область().Шрифт = Новый Шрифт(ОбластьОбщихИтогов.Область().Шрифт, , , Истина, , ,);
			ОбластьИерархическихЗаписей = ТабДок.ПолучитьОбласть(1, , 1, );
		    ОбластьИерархическихЗаписей.Область().Шрифт = Новый Шрифт(ОбластьИерархическихЗаписей.Область().Шрифт, , , Истина, , ,);
			ОбластьГрупповыхЗаписей = ТабДок.ПолучитьОбласть(1, , 1, );
		    ОбластьГрупповыхЗаписей.Область().Шрифт = Новый Шрифт(ОбластьГрупповыхЗаписей.Область().Шрифт, , , Истина, , ,);
			ОбластьЗаголовка = ТабДок.ПолучитьОбласть(1, , 1, );
			
			Для ТекущееПоле = 0 По КоличествоКолонок - 1 Цикл
				Область = ОбластьЗаголовка.Область(1, ТекущееПоле + 1);
				Область.Текст = мРезЗапроса.Колонки[ТекущееПоле].Имя;
	            Область.ШиринаКолонки = мРезЗапроса.Колонки[ТекущееПоле].Ширина;
			КонецЦикла;
			ТабДок.Вывести(ОбластьЗаголовка);
			ОбластьЗаголовка = ТабДок.Область(1, 1, 1, КоличествоКолонок);
			
			ОбластьЗаголовка.Шрифт = Новый Шрифт(ОбластьЗаголовка.Шрифт, , , Истина, , ,);
			ОбластьЗаголовка.ЦветФона = Новый Цвет(255, 255, 0);
			ОбластьЗаголовка.ГраницаСнИзу = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная, 1);

			мФормаПрогрессора.Открыть();
			мФормаПрогрессора.ЭлементыФормы.Индикатор.МаксимальноеЗначение = Вычислить(мИмяРеквизитаТаблицы + ".Количество()");
			мФормаПрогрессора.НадписьСостоянияИндикатораОбщая = "Формирование печатной формы:";
			Инд = 1;
			мФормаПрогрессора.ЗначениеИндикатора = 1;
			
	        ТабДок.НачатьАвтогруппировкуСтрок();
			Пока Выборка.Следующий() Цикл
				
				Инд = Инд + 1;
					
				ОбработкаПрерыванияПользователя();
				
				мФормаПрогрессора.ЗначениеИндикатора = Инд;
				мФормаПрогрессора.НадписьСостоянияИндикатораТекущая = "Выводится " + Инд + " - я строка";
							
				Если Выборка.ТипЗаписи() = ТипЗаписиЗапроса.ИтогПоГруппировке Тогда 
					ИсходнаяСтрока = ОбластьГрупповыхЗаписей;
				ИначеЕсли Выборка.ТипЗаписи() = ТипЗаписиЗапроса.ИтогПоИерархии Тогда 
					ИсходнаяСтрока = ОбластьИерархическихЗаписей;
				ИначеЕсли Выборка.ТипЗаписи() = ТипЗаписиЗапроса.ОбщийИтог Тогда 
					ИсходнаяСтрока = ОбластьОбщихИтогов;
				Иначе
					ИсходнаяСтрока = ДетальнаяСтрока;
				КонецЕсли;
					
				Для ТекущееПоле = 0 По КоличествоКолонок - 1 Цикл
					Область = ИсходнаяСтрока.Область(1, ТекущееПоле + 1);
					Область.Текст = Выборка[ТекущееПоле];
					Область.Расшифровка = Выборка[ТекущееПоле];
				КонецЦикла;
				ТабДок.Вывести(ИсходнаяСтрока, Выборка.Уровень());
			КонецЦикла;
			ТабДок.ЗакончитьАвтогруппировкуСтрок();

			мФормаПрогрессора.Закрыть();
			
			ТабДок.ОтображатьСетку = True;
			ТабДок.Защита = Ложь;
			ТабДок.ТолькоПросмотр = Истина;
			ТабДок.Показать("Результат запроса");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // СохранитьРезультат()

///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ УПРАВЛЕНИЯ

// Обработчик выбора строки в дереве запросов
//
Процедура ДеревоЗапросовВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	ВыполнитьЗапросИзФормы();
	кзУстановитьШиринуКолонок(ЭлементыФормы, мСтруктураСРазмерами);

КонецПроцедуры // ДеревоЗапросовВыбор()

Процедура ДеревоЗапросовПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	лТекущаяСтрока = Элемент.ТекущаяСтрока;
	Если Копирование Тогда
		СохранитьЗапросТекущейСтроки();
		СкопироватьДеревоЗапросовРекурсивно(мТекущаяСтрока, лТекущаяСтрока, Новый Структура("ОбновлятьИД, ОбновлятьИДСтрокДанныхЗапроса, ЭтоСтарыйРелиз, НеОпределятьПараметрыРекурсии", Истина, Истина, Ложь, Истина));
		мТекущаяСтрока = лТекущаяСтрока;
	КонецЕсли;
	
	Если (НоваяСтрока ИЛИ Копирование) Тогда 
		Если мТекущаяСтрока.СпособВыгрузки = Неопределено Тогда
			мТекущаяСтрока.СпособВыгрузки = 1; СпособВыгрузки = 1;
		КонецЕсли;
		
		Если НоваяСтрока Тогда 
			мТекущаяСтрока.Идентификатор = Новый УникальныйИдентификатор;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ДеревоЗапросовПриНачалеРедактирования()

Процедура ДеревоЗапросовПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	Если ОтменаРедактирования Тогда 
		мОтменаРедактирования = Истина;
	КонецЕсли;
КонецПроцедуры

Функция ПривестиСпецЗначение(ИсходноеЗначение, ИдентификаторЗначенияВСтруктуреВременныхТаблиц)
	// корректируем специальные значения
	Если ТипЗнч(ИсходноеЗначение) = Тип("Граница") Тогда 
		Возврат ИсходноеЗначение.Значение;
	ИначеЕсли ТипЗнч(ИсходноеЗначение) = Тип("МоментВремени") Тогда 
		Возврат ИсходноеЗначение.Дата;
	ИначеЕсли ТипЗнч(ИсходноеЗначение) = Тип("Массив") Тогда 
		лТемпСписокЗначений = Новый СписокЗначений;
		лТемпСписокЗначений.ЗагрузитьЗначения(ИсходноеЗначение);
		Возврат лТемпСписокЗначений;
	ИначеЕсли ТипЗнч(ИсходноеЗначение) = Тип("ТаблицаЗначений") Тогда 
		гСтруктураТЗДляВременныхТаблиц.Вставить(ИдентификаторЗначенияВСтруктуреВременныхТаблиц, ИсходноеЗначение.Скопировать());		
		Возврат "Таблица значений (Строк = " + ИсходноеЗначение.Количество() + "; Колонок = " + ИсходноеЗначение.Колонки.Количество() + ")"
	Иначе
		Возврат ИсходноеЗначение
	КонецЕсли;
КонецФункции // ПривестиСпецЗначение()

Процедура ДобавитьПараметрВСписокНаФорме(ПараметрыСписок, СтрокаИсходнойТаблицы, ЭтоГлобальныйПараметр = Ложь)
	
	НоваяСтрока = ПараметрыСписок.Добавить();
	ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаИсходнойТаблицы, "Тип, Имя, ИдентификаторСтроки");
	НоваяСтрока.ГлобальныйПараметр = ЭтоГлобальныйПараметр;
	
	Если СтрокаИсходнойТаблицы.Тип = гТипыЗначенийПараметров().ТаблицаЗначений Тогда 
		лИсходноеЗначениеПараметра = ЗначениеИзСтрокиВнутр(ЗначениеВСтрокуВнутр(гСтруктураТЗДляВременныхТаблиц.Свойство(гПреобразоватьВПравильноеНазвание(СтрокаИсходнойТаблицы.ИдентификаторСтроки))));
	Иначе		
		лИсходноеЗначениеПараметра = ЗначениеИзСтрокиВнутр(СтрокаИсходнойТаблицы.Значение);
		Если Не ЗначениеЗаполнено(НоваяСтрока.Тип) Тогда 
			НоваяСтрока.Тип = ПолучитьТипПараметраПоЗначению(лИсходноеЗначениеПараметра);
		КонецЕсли;
	КонецЕсли;
	
	НоваяСтрока.Значение = ПривестиСпецЗначение(лИсходноеЗначениеПараметра, гПреобразоватьВПравильноеНазвание(НоваяСтрока.ИдентификаторСтроки));
	
КонецПроцедуры // ДобавитьПараметрВСписокНаФорме()
 
Процедура ДеревоЗапросовПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель)
	Если Копирование Тогда 
		Отказ = Истина;
		СохранитьЗапросТекущейСтроки();
		РодительТекущейСтроки = ?(мТекущаяСтрока.Родитель = Неопределено, ДеревоЗапросов, мТекущаяСтрока.Родитель);
		лНоваяСтрока = РодительТекущейСтроки.Строки.Добавить();
		СкопироватьДеревоЗапросовРекурсивно(мТекущаяСтрока, лНоваяСтрока, Новый Структура("ОбновлятьИД, ОбновлятьИДСтрокДанныхЗапроса, ЭтоСтарыйРелиз, НеОпределятьПараметрыРекурсии", Истина, Истина, Ложь, Истина));
		ЭлементыФормы.ДеревоЗапросов.ТекущаяСтрока = лНоваяСтрока;		
	КонецЕсли;
КонецПроцедуры // ДеревоЗапросовПередНачаломДобавления()

// Обработчик активации строки в дереве запросов
//
Процедура ДеревоЗапросовПриАктивизацииСтроки(Элемент)

	//Если мПрограммноеДобавлениеСтрокиЗапроса = Истина Тогда 
	//	мПрограммноеДобавлениеСтрокиЗапроса = Ложь;
	//	мТекущаяСтрока = Элемент.ТекущаяСтрока;
	//	Возврат;
	//КонецЕсли;
	
	СохранитьЗапросТекущейСтроки();
	
	мТекущаяСтрока = Элемент.ТекущаяСтрока;
	
	// чистим ссылки на удаленные строки
	Пока мМассивУдаленныхСтрок.Количество() Цикл
		лУдаленнаяСтрока = мМассивУдаленныхСтрок[0];
		лМассивУдаляемыхЭлементов = ИсполняемыйКод.НайтиСтроки(Новый Структура("ИдентификаторЗапроса", лУдаленнаяСтрока));
		Для Каждого УдаляемыйЭлемент Из лМассивУдаляемыхЭлементов Цикл
			ИсполняемыйКод.Удалить(УдаляемыйЭлемент);
		КонецЦикла;
		лМассивУдаляемыхЭлементов = ПараметрыЗапросов.НайтиСтроки(Новый Структура("ИдентификаторЗапроса", лУдаленнаяСтрока));
		Для Каждого УдаляемыйЭлемент Из лМассивУдаляемыхЭлементов Цикл
			ПараметрыЗапросов.Удалить(УдаляемыйЭлемент);
		КонецЦикла;
		мМассивУдаленныхСтрок.Удалить(0);
	КонецЦикла;
	
	Если ДеревоЗапросов.Строки.Количество() <> 0 И мТекущаяСтрока <> Неопределено Тогда

		СпособВыгрузки       = мТекущаяСтрока.СпособВыгрузки;
		СтатусДереваЗапросов = мТекущаяСтрока.Имя;
		
		ЗадатьТекстЗапроса(мТекущаяСтрока.ТекстЗапроса);
		
		мФормаПараметров.ПараметрыСписок.Очистить();
		// заполняем параметры для текущей строки запроса
		Если Не ПустаяСтрока(мТекущаяСтрока.Идентификатор) Тогда 
			Для каждого СтрокаИсходнойТаблицы Из ПараметрыЗапросов.НайтиСтроки(Новый Структура("ИдентификаторЗапроса", мТекущаяСтрока.Идентификатор)) Цикл
				ДобавитьПараметрВСписокНаФорме(мФормаПараметров.ПараметрыСписок, СтрокаИсходнойТаблицы);
			КонецЦикла;
		КонецЕсли;
		
		// заполняем глобальные параметры для текущей строки запроса
		Для каждого СтрокаИсходнойТаблицы Из ПараметрыЗапросов.НайтиСтроки(Новый Структура("ИдентификаторЗапроса", "")) Цикл
			ДобавитьПараметрВСписокНаФорме(мФормаПараметров.ПараметрыСписок, СтрокаИсходнойТаблицы, Истина);
		КонецЦикла;

		// заполняем исполняемый код для текущей строки запроса
		мФормаИсполняемыйКод.ИсполняемыйКодСписок.Очистить();
		Для каждого СтрокаИсходнойТаблицы Из ИсполняемыйКод.НайтиСтроки(Новый Структура("ИдентификаторЗапроса", мТекущаяСтрока.Идентификатор)) Цикл
			НоваяСтрока       = мФормаИсполняемыйКод.ИсполняемыйКодСписок.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаИсходнойТаблицы, "Имя, ИдентификаторСтроки, Текст");
		КонецЦикла;
		
	Иначе
		ЗадатьТекстЗапроса("");
		мФормаПараметров.ПараметрыСписок.Очистить();
		мФормаИсполняемыйКод.ИсполняемыйКодСписок.Очистить();
		мФормаПараметрыИБ.ТаблицаПараметров.Очистить();
	КонецЕсли;

КонецПроцедуры // ДеревоЗапросовПриАктивизацииСтроки()

// Обработчик события перед удалением строки в дереве запросов
//
Процедура ДеревоЗапросовПередУдалением(Элемент, Отказ)

	Если ДеревоЗапросов.Строки.Количество() = 1 И ЭлементыФормы.ДеревоЗапросов.ТекущаяСтрока = ДеревоЗапросов.Строки[0] Тогда   
		Отказ = Истина;
		ПоказатьВопрос(Новый ОписаниеОповещения("ПослеЗакрытияВопроса", ЭтаФорма, Новый Структура("Режим", "СоздатьНовыйФайл")), "Список запросов должен содержать хотя бы одну строку. Создать новый файл?", РежимДиалогаВопрос.ДаНет);
		Возврат;
	КонецЕсли;
	гМассивИзмененныхСтрок.Добавить(мТекущаяСтрока.Идентификатор);
	мМассивУдаленныхСтрок.Добавить(мТекущаяСтрока.Идентификатор);
	мТекущаяСтрока = Неопределено;
	
	Модифицированность = Истина;

КонецПроцедуры // ДеревоЗапросовПередУдалением()

// Обработчик Изменения способа выгрузки
//
Процедура СпособВыгрузкиПриИзменении(Элемент)

	Если СпособВыгрузки <> 2 Тогда
		ФлажокИтогиВсе	 = Ложь;
	КонецЕсли;

КонецПроцедуры // СпособВыгрузкиПриИзменении()

Процедура ФлажокИтогиВсеПриИзменении(Элемент)
	
	Если Элемент.Значение И СпособВыгрузки <> 2 Тогда
		СпособВыгрузки = 2		
	КонецЕсли;
КонецПроцедуры

// Обработчик выбора строки в таблице результата
//
Процедура ТаблицаРезультатаВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	
    СодержимоеЯчейки = ВыбраннаяСтрока[Колонка.Имя];

	Если ТипЗнч(СодержимоеЯчейки) = Тип("ТаблицаЗначений") Тогда
		ФормаВложеннойТаблицы = Обработка.ПолучитьФорму("ФормаВложеннойТаблицы", ЭтаФорма);
		ФормаВложеннойТаблицы.ВложеннаяТаблица = СодержимоеЯчейки;
		ФормаВложеннойТаблицы.ЭлементыФормы.ВложеннаяТаблица.СоздатьКолонки();
		ФормаВложеннойТаблицы.Открыть();
	ИначеЕсли Не ЗначениеЗаполнено(СодержимоеЯчейки) Тогда 
		ПоказатьПредупреждение(, "[Пустое значение типа: " + ТипЗнч(СодержимоеЯчейки) + "]", 10);
	Иначе
		ПоказатьЗначение(, СодержимоеЯчейки);
	КонецЕсли;

КонецПроцедуры // ТаблицаРезультатаВыбор()

//+++ Сервис: Выгрузка в документ
Процедура СсылкаНаДокументПриИзменении(Элемент)
	
	ЭлементыФормы.ИмяТабличнойЧастиЗагружаемогоДокумента.СписокВыбора.Очистить();
	Если ЭлементыФормы.СсылкаНаДокумент.Значение <> Неопределено Тогда
		Если СсылкаНаДокумент.Метаданные().ТабличныеЧасти.Количество() Тогда
			Для Каждого ТЧ Из СсылкаНаДокумент.Метаданные().ТабличныеЧасти Цикл
				ЭлементыФормы.ИмяТабличнойЧастиЗагружаемогоДокумента.СписокВыбора.Добавить(ТЧ.Имя);
			КонецЦикла;
			ИмяТабличнойЧастиЗагружаемогоДокумента = ЭлементыФормы.ИмяТабличнойЧастиЗагружаемогоДокумента.СписокВыбора[0];
			ИмяТабличнойЧастиЗагружаемогоДокументаПриИзменении(Неопределено);
		Иначе
			ПоказатьПредупреждение(, "В документе вида """ + СсылкаНаДокумент.Метаданные().Синоним + """ нет табличных частей для выгрузки.");
			СсылкаНаДокумент = Неопределено;
		КонецЕсли;
	КонецЕсли;		
	
	ИнициализацияКнопкиВыгрузки();
	
КонецПроцедуры

Процедура СсылкаНаДокументОчистка(Элемент, СтандартнаяОбработка)
	
		ЭлементыФормы.СтрокТЧ.Значение = "";
		ИмяТабличнойЧастиЗагружаемогоДокумента = Неопределено;
		
КонецПроцедуры

Процедура ИмяТабличнойЧастиЗагружаемогоДокументаПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(ИмяТабличнойЧастиЗагружаемогоДокумента) Тогда
	     ЭлементыФормы.СтрокТЧ.Значение = "Строк ТЧ = " + СсылкаНаДокумент[ИмяТабличнойЧастиЗагружаемогоДокумента].Количество();
	КонецЕсли;
	
	ИнициализацияКнопкиВыгрузки();

КонецПроцедуры

Процедура ВыгрузитьВДокументНажатие(Элемент)
	
	Если ЗначениеЗаполнено(СсылкаНаДокумент) Тогда
		
		лОбъект = СсылкаНаДокумент.ПолучитьОбъект();
		лТаблицаРезультата = Вычислить(мИмяРеквизитаТаблицы);
		
		Если ОчищатьТЧПриВыгрузкеВДокумент Тогда
			лОбъект[ИмяТабличнойЧастиЗагружаемогоДокумента].Загрузить(лТаблицаРезультата);
		Иначе
			Для Каждого Стр Из лТаблицаРезультата Цикл
				СтрТЧ = лОбъект[ИмяТабличнойЧастиЗагружаемогоДокумента].Добавить();
				ЗаполнитьЗначенияСвойств(СтрТЧ, Стр);		
			КонецЦикла;
		КонецЕсли;
		
		лОбъект.ПолучитьФорму().Открыть();
	Иначе
		ПоказатьПредупреждение(, "Выберите документ для заполнения.", 10);
		ТекущийЭлемент = ЭлементыФормы.СсылкаНаДокумент;
	КонецЕсли;
	
КонецПроцедуры

//--- Сервис: Выгрузка в документ

///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Обработчик события перед открытием формы
//
Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)

	//Метаданные.РежимИспользованияСинхронныхВызововРасширенийИВнешнихКомпонент - в старых релизах
	//Если Метаданные.СвойстваОбъектов.РежимИспользованияСинхронныхВызововРасширенийПлатформыИВнешнихКомпонент = Метаданные.СвойстваОбъектов.РежимИспользованияСинхронныхВызововРасширенийПлатформыИВнешнихКомпонент.Использовать Тогда 
	//	Сообщить("В свойствах конфигурации установлен режим использования синхронных вызовов расширений платформы и внешних компонент. 
	//	|(Корень дерева метаданных > Cвойства > Совместимость > Режим использования синхронных вызовов расширений платформы и внешних компонент)
	//	|Работа данной обработки при таком свойстве в режиме обычного приложения невозможна.
	//	|Вы можете изменить данное свойство на ""Использовать с предупреждением"" или использовать обработку в управляемом режиме.", СтатусСообщения.Важное);
	//	Отказ = Истина;
	//	Возврат;
	//КонецЕсли;
	
	//Если Метаданные.РежимИспользованияМодальности = Метаданные.СвойстваОбъектов.РежимИспользованияМодальности.Использовать Тогда 
	//	Сообщить("В свойствах конфигурации установлен запрет использования модальных окон. 
	//	|(Корень дерева метаданных > Cвойства > Совместимость > Режим использования модальности)
	//	|Некоторые функции обработки при таком свойстве в режиме обычного приложения будут недоступны.
	//	|Вы можете изменить данное свойство на ""Использовать с предупреждением"" или использовать обработку в управляемом режиме.", СтатусСообщения.Важное);
	//КонецЕсли;
	
	//Инициализация обработки
	
	мЗаголовокФормы = гНазваниеОбработки();

	мТекущаяСтрока                   = Неопределено;
	//#рефакторинг 
	//мИдетДобавление                  = Ложь; 
	мАктивизированаДобавляемаяЗапись = Ложь;

	мФормаПараметров = Обработка.ПолучитьФорму("ФормаПараметрыЗапроса", ЭтаФорма);
	мФормаПараметров.РазрешитьСостояниеПрикрепленное = Истина;
	мФормаПараметров.РазрешитьСостояниеПрячущееся    = Ложь;
	мФормаПараметров.РазрешитьСостояниеСвободное     = Истина;

	мФормаИсполняемыйКод = Обработка.ПолучитьФорму("ФормаИсполняемыйКод"    , ЭтаФорма);
	мФормаПараметрыИБ    = Обработка.ПолучитьФорму("ФормаПараметрыИБ"       , ЭтаФорма);
	мФормаПрогрессора    = Обработка.ПолучитьФорму("ФормаИндикатораЛокально",ЭтаФорма);

	мТаблицаЗагружена        = Ложь;
	мСводнаяТаблицаЗагружена = Ложь;

	АгрегатнаяФункция = "Нет";

	гСоответствиеВременныеТаблицы = Новый Соответствие;

	гСеансовыеДанные = Новый Структура();	
	гСеансовыеДанные.Вставить("ИдентификаторСеанса", Новый УникальныйИдентификатор);
	гСеансовыеДанные.Вставить("КЭШ"                , Новый Структура);
	гСеансовыеДанные.Вставить("ПутьККартинкам"     , гИзвлечьКартинкиОбработкиНаСервере());
	                                                                                                                    
	// ФОРМИРОВАНИЕ МЕНЮ
	МассивМенюДляДобавления = Новый Массив;                                                                                                                                        
	МассивМенюДляДобавления.Добавить(Новый Структура("Имя, Контейнер, Картинка, Индекс", "КодЗапрос"    , ЭлементыФормы.КоманднаяПанельФормы     , ЭлементыФормы.КоманднаяПанельФормы.Кнопки.Индекс(ЭлементыФормы.КоманднаяПанельФормы.Кнопки.Найти("ПолучитьИзЗапроса")) + 1));
	МассивМенюДляДобавления.Добавить(Новый Структура("Имя, Контейнер, Картинка, Индекс", "Дополнительно", ЭлементыФормы.КоманднаяПанельФормы     ));
	МассивМенюДляДобавления.Добавить(Новый Структура("Имя, Контейнер, Картинка, Индекс", "Информация"   , ЭлементыФормы.КоманднаяПанельФормы     ));
	МассивМенюДляДобавления.Добавить(Новый Структура("Имя, Контейнер, Картинка, Индекс", "Сервис"       , ЭлементыФормы.КоманднаяПанельРезультата));
	МассивМенюДляДобавления.Добавить(Новый Структура("Имя, Контейнер, Картинка, Индекс", "Запросы"      , ЭлементыФормы.КоманднаяПанельРезультата));
	
	СформироватьМеню(МассивМенюДляДобавления);
	
КонецПроцедуры // ПередОткрытием

// Обработчик события при открытии формы
//
Процедура ПриОткрытии()
	
	мФормаПрогрессора.НадписьСостоянияИндикатораОбщая = "Заполнение начальных параметров ...";
	мФормаПрогрессора.Открыть();
	мФормаПрогрессора.ЭлементыФормы.Индикатор.МаксимальноеЗначение = 5;
	мФормаПрогрессора.ЗначениеИндикатора = 1;
	
	гПодменюВременныеТаблицыКнопки = ЭлементыФормы.КоманднаяПанельФормы.Кнопки.ПодменюВременныеТаблицы.Кнопки;
	
	кзНастроитьИнтерфейс(ЭтаФорма);
	кзУстановитьДополнительныеПараметры();	
	
	мФормаПрогрессора.ЗначениеИндикатора = 2;
	
	ИнициализацияДанных();
	
	мФормаПрогрессора.ЗначениеИндикатора = 3;
	
	Если Не мФормаПрогрессора.Открыта() Тогда 
		мФормаПрогрессора.Открыть();
		мФормаПрогрессора.ЗначениеИндикатора = 3;
	КонецЕсли;
	
	ЗагрузитьЗапросы(Истина, мИмяФайла); 
	
	мФормаПрогрессора.ЗначениеИндикатора = 4;
	
	ИспользоватьАвтосохранение      = ВосстановитьЗначение("КонсольЗапросов_ИспользоватьАвтосохранение");
	ИнтервалАвтосохранения          = ВосстановитьЗначение("КонсольЗапросов_ИнтервалАвтосохранения");
	ФлажокИтогиВсе                  = ВосстановитьЗначение("КонсольЗапросов_ФлажокИтогиВсе");
	ФлажокСохранитьПередВыполнением = ВосстановитьЗначение("КонсольЗапросов_ФлажокСохранитьПередВыполнением");

	ОбработкаАвтосохранения();
	
	мФормаПрогрессора.ЗначениеИндикатора = 5;
	
	// Открытие обработки из режима отладки "1С Предприятия"
	Если ЗапросДляОтладки <> Неопределено И ТипЗнч(ЗапросДляОтладки) = Тип("Запрос") Тогда 
		
		мТекущаяСтрока                = ДеревоЗапросов.Строки.Добавить();
		мТекущаяСтрока.Имя            = "" + ТекущаяДата();
		мТекущаяСтрока.ТекстЗапроса   = ЗапросДляОтладки.Текст;
		мТекущаяСтрока.Идентификатор  = Строка(Новый УникальныйИдентификатор);
		мТекущаяСтрока.СпособВыгрузки = 1; // СпособВыгрузки = 1;

		гСоответствиеЗапросы.Вставить(мТекущаяСтрока.Идентификатор, ЗапросДляОтладки);
		
		Для каждого ТекПараметр Из ЗапросДляОтладки.Параметры Цикл
			
			//#добавлениепараметразапроса 4
			СтрокаПараметров = ПараметрыЗапросов.Добавить();
			
			СтрокаПараметров.ИдентификаторЗапроса = мТекущаяСтрока.Идентификатор;
			СтрокаПараметров.ИдентификаторСтроки  = Строка(Новый УникальныйИдентификатор);
			СтрокаПараметров.Имя                  = ТекПараметр.Ключ;
			СтрокаПараметров.Значение             = ЗначениеВСтрокуВнутр(ТекПараметр.Значение);
			
		КонецЦикла; 		
		
		ЭлементыФормы.ДеревоЗапросов.ТекущаяСтрока = мТекущаяСтрока;
	КонецЕсли;

	КнопкаПодменю = ЭлементыФормы.КоманднаяПанельФормы.Кнопки.ПодменюВременныеТаблицы.Кнопки.УдалитьВременнуюТаблицу;
	КнопкаПодменю.Кнопки.Добавить("УдалитьВсе", ТипКнопкиКоманднойПанели.Действие, "Удалить все таблицы", Новый Действие("ПодменюВременныеТаблицыПриВыбореУдалитьВременнуюТаблицу"));
	КнопкаПодменю.Кнопки.Добавить();
	
	Если мФормаПрогрессора.Открыта() Тогда 
		мФормаПрогрессора.Закрыть();
	КонецЕсли;
	
	Модифицированность = Ложь;
	
КонецПроцедуры // ПриОткрытии()

// Обработчик события преред закрытием формы
//
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	Если ПроверитьМодифицированность() Тогда
		СохранитьЗначение("КонсольЗапросов_ИспользоватьАвтосохранение"     , ИспользоватьАвтосохранение);
		СохранитьЗначение("КонсольЗапросов_ИнтервалАвтосохранения"         , ИнтервалАвтосохранения);
		СохранитьЗначение("КонсольЗапросов_ФлажокИтогиВсе"                 , ФлажокИтогиВсе);
		СохранитьЗначение("КонсольЗапросов_ФлажокСохранитьПередВыполнением", ФлажокСохранитьПередВыполнением);
		СохранитьЗначение("КонсольЗапросов_ИмяФайла"                       , мИмяФайла);
	Иначе
        СтандартнаяОбработка = Ложь;
		Отказ                = Истина;
	КонецЕсли;
КонецПроцедуры // ПередЗакрытием()

&НаКлиенте
Процедура ПриЗакрытии()
	мФормаИсполняемыйКод = "";
	мФормаПараметров = "";
	мФормаПараметрыИБ = "";
	
	Если ЗначениеЗаполнено(мПутьККартинкам) Тогда 
		Попытка
			УдалитьФайлы(мПутьККартинкам);
		Исключение
		КонецПопытки; 
	КонецЕсли;
КонецПроцедуры

// Обработчик события выбора в подчиненной форме
//
Процедура ОбработкаВыбора(ЗначениеВыбора, Источник)
	
	Если РежимВыбораВеткиДерева = "ПеренестиВДругуюВетку" Тогда
		НоваяСтрока = ЗначениеВыбора.Строки.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, мТекущаяСтрока);
		СкопироватьДеревоЗапросовРекурсивно(мТекущаяСтрока, НоваяСтрока, Новый Структура("ОбновлятьИД, ЭтоСтарыйРелиз", Ложь, Ложь));

		РодительТекущейСтроки = ?(мТекущаяСтрока.Родитель = Неопределено, ДеревоЗапросов, мТекущаяСтрока.Родитель);		
		РодительТекущейСтроки.Строки.Удалить(РодительТекущейСтроки.Строки.Индекс(мТекущаяСтрока));
		мТекущаяСтрока = Неопределено;
		
		ЭлементыФормы.ДеревоЗапросов.ТекущаяСтрока = НоваяСтрока;

		Модифицированность = Истина;
	ИначеЕсли РежимВыбораВеткиДерева = "СравнитьЗапросы" Тогда
		
		ЗаписьФайла = Новый ЗаписьТекста;
		
		ПервыйФайл = ПолучитьИмяВременногоФайла("txt");
		ВторойФайл = ПолучитьИмяВременногоФайла("txt");
		
		ЗаписьФайла.Открыть(ПервыйФайл);
		ЗаписьФайла.ЗаписатьСтроку(мТекущаяСтрока.ТекстЗапроса);
		ЗаписьФайла.Закрыть();
		
		ЗаписьФайла.Открыть(ВторойФайл);
		ЗаписьФайла.ЗаписатьСтроку(ЗначениеВыбора.ТекстЗапроса);
		ЗаписьФайла.Закрыть();
		
		
		Сравнение = Новый СравнениеФайлов;
		Сравнение.СпособСравнения = СпособСравненияФайлов.ТекстовыйДокумент;
		Сравнение.ИгнорироватьПустоеПространство = Истина;
		
		Сравнение.ПервыйФайл = ПервыйФайл;
		Сравнение.ВторойФайл = ВторойФайл;
		
		Если Сравнение.Сравнить() Тогда
			ПоказатьПредупреждение(, "Различий нет", 10);
		Иначе
			Сравнение.ПоказатьРазличияМодально();
		КонецЕсли;
		
		УдалитьФайлы(ПервыйФайл);
		УдалитьФайлы(ВторойФайл);
		
	КонецЕсли;

КонецПроцедуры // ОбработкаВыбора()

// Обработчик события при смене страницы панели
//
Процедура ПанельРезультатаПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	Если Элемент.ТекущаяСтраница.Имя = "ВыгрузкаВДокумент" Тогда
		ИнициализацияКнопкиВыгрузки();
	Иначе	
		ЗагрузитьРезультат();
	КонецЕсли;
	
КонецПроцедуры // ПанельРезультатаПриСменеСтраницы()

/////////////////////////////////////////
// НЕТИПОВЫЕ_ДОПОЛНЕНИЯ

Процедура кзСохранитьШаблоны(ГлавнаяФорма)
	СтрокаСШаблонами=ДополнительныеПараметры.Найти("Шаблоны","Параметр");
	Если СтрокаСШаблонами=Неопределено тогда
		//создаем эту строку, если не нашли
		СтрокаСШаблонами=ДополнительныеПараметры.Добавить();
		СтрокаСШаблонами.Параметр="Шаблоны";
	Конецесли;
	СтрокаСШаблонами.Значение = ГлавнаяФорма.Шаблоны.Скопировать();
	ГлавнаяФорма.ЭлементыФормы.ТабличноеПолеШаблонов.СоздатьКолонки();	
Конецпроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

Процедура МенюАгрегатныеФункции(Кнопка)
	Если ЭлементыФормы[мИмяРеквизитаТаблицы].Данные = мИмяРеквизитаТаблицы Тогда
		АгрегатнаяФункция = Кнопка.Текст;
		ЗаполнитьЗначениеАгрегатнойФункции();
	Иначе
		ПоказатьПредупреждение(, "Агрегатные функции работают только для способа выгрузки ""Список"".", 10);
	КонецЕсли;
КонецПроцедуры

Процедура ЗаполнитьЗначениеАгрегатнойФункции();
	СтатусРезультатаТаблицыАгрФункции = "";
	
	лТаблицаРезультата = ЭлементыФормы[мИмяРеквизитаТаблицы];
	
	ТекКолонка = лТаблицаРезультата.ТекущаяКолонка;
	Если лТаблицаРезультата.Данные = мИмяРеквизитаТаблицы И АгрегатнаяФункция<> "Нет" И ТекКолонка<> Неопределено Тогда
		//Рассчитываем агрегатные функции	
		Если лТаблицаРезультата.ВыделенныеСтроки.Количество() > 0 Тогда
			Результат = Неопределено;
			КоличествоЧисел = 0;
			КоличествоНеЧисел = 0;
			Для каждого ТекЯчейка Из лТаблицаРезультата.ВыделенныеСтроки Цикл
				ТекЗначение = ТекЯчейка[ТекКолонка.Имя];
				Если ТипЗнч(ТекЗначение) = Тип("Число") Тогда
					КоличествоЧисел = КоличествоЧисел + 1;
					Если АгрегатнаяФункция = "Среднее" Тогда					
						Результат = ?(Результат = Неопределено, 0, Результат) + ТекЗначение;
					ИначеЕсли АгрегатнаяФункция = "Минимум" Тогда
						Если Результат = Неопределено ИЛИ Результат > ТекЗначение Тогда
							Результат = ТекЗначение;
						КонецЕсли;
					ИначеЕсли АгрегатнаяФункция = "Максимум" Тогда
						Если Результат = Неопределено ИЛИ Результат<ТекЗначение Тогда
							Результат = ТекЗначение;
						КонецЕсли;
					ИначеЕсли АгрегатнаяФункция = "Сумма" Тогда
						Результат = ?(Результат = Неопределено, 0, Результат) + ТекЗначение;
					КонецЕсли;
				Иначе
					КоличествоНеЧисел = КоличествоНеЧисел + 1;
				КонецЕсли;
			КонецЦикла; 
			Если АгрегатнаяФункция = "Среднее" И КоличествоЧисел <> 0 Тогда					
				Результат = Результат / КоличествоЧисел;
			ИначеЕсли АгрегатнаяФункция = "Количество значений" Тогда
				Результат = КоличествоЧисел + КоличествоНеЧисел;
			ИначеЕсли АгрегатнаяФункция = "Количество чисел" Тогда
				Результат = КоличествоЧисел;
			КонецЕсли;
			Если Результат <> Неопределено Тогда
				СтатусРезультатаТаблицыАгрФункции = АгрегатнаяФункция + " = " + Окр(Результат,4);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры // ЗаполнитьЗначениеАгрегатнойФункции()

Процедура кзНастроитьИнтерфейс(ГлавнаяФорма)
	кзВосстановитьШиринуКолонок(ГлавнаяФорма.ЭлементыФормы, мСтруктураСРазмерами);
КонецПроцедуры

Процедура кзУстановитьДополнительныеПараметры()
	ДополнительныеПараметры.Колонки.Добавить("Параметр");
	ДополнительныеПараметры.Колонки.Добавить("Значение");
	НоваяСтрока = ДополнительныеПараметры.Добавить();
	НоваяСтрока.Параметр = гПараметрыСпецСтрокиДереваЗапросов().Параметр;
	НоваяСтрока.Значение = гПараметрыСпецСтрокиДереваЗапросов().Значение;	
КонецПроцедуры

Процедура кзОчиститьЗначениеПоУмолчанию(СтруктураСРазмерами) Экспорт
	СохранитьЗначение("КонсольЗапросов_ОтЛавелина_СтруктураСШиринойКолонок", новый Структура);
КонецПроцедуры

Функция кзПолучитьСохраненноеЗначение(Режим) Экспорт
	Результат = Неопределено;
	Если Режим = "СписокТекстовКодов" Тогда
		Результат = ВосстановитьЗначение("КонсольЗапросов_ОтЛавелина_СписокТекстовКодов");
	Конецесли;
	Возврат Результат
КонецФункции	

Процедура кзЗаписатьСохраненноеЗначение(Режим, Значение, знач ДопЗначение = Неопределено) Экспорт
	Если Режим = "СписокТекстовКодов" Тогда		
		Если ДопЗначение = Неопределено Тогда
			ДопЗначение = новый СписокЗначений;
		КонецЕсли;
		Если ДопЗначение.Количество() > 10 Тогда
			ДопЗначение.Удалить(0);
		КонецЕсли;
		Если не гЗначениеНеЗаполнено(СокрЛп(Значение)) Тогда
			ДопЗначение.Добавить(Значение);
		КонецЕсли;
		СохранитьЗначение("КонсольЗапросов_ОтЛавелина_СписокТекстовКодов", ДопЗначение);
	Конецесли;
КонецПроцедуры

//ДЕЙСТВИЯ НАД РЕЗУЛЬТАТОМ ЗАПРОСА

Процедура кзДокПроведение(Кнопка, ЭлементыФормы, Форма) Экспорт
	
	лТаблицаРезультата = Вычислить(мИмяРеквизитаТаблицы);
	
	Если лТаблицаРезультата.Количество() = 0 Тогда
		ПоказатьПредупреждение(, "Таблица результатов запроса пуста.", 10);
		Возврат
	КонецЕсли;
	
	Ответ = Вопрос("Выполнить в транзакции?", РежимДиалогаВопрос.ДаНетОтмена); 
	Если Ответ = КодВозвратаДиалога.Отмена Тогда
		возврат;
	ИначеЕсли Ответ = КодВозвратаДиалога.Нет Тогда
		Если Вопрос("В этом режиме все Изменения будут применяться сразу. " + Символы.ПС + 
			"При прерывании обработки сделаные Изменения не отменятся. " + Символы.ПС + 
			"Вы уверены?", РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Нет Тогда
			Возврат;
		Конецесли;
	Конецесли;
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		НачатьТранзакцию();
	Конецесли;
	
	
	Если Кнопка.Имя = "Провести" Тогда
		
		//Проводим текущий документ
		ТекДанные = ЭлементыФормы[мИмяРеквизитаТаблицы].ТекущиеДанные;
		Если ТекДанные = Неопределено Тогда
			ПоказатьПредупреждение(, "Выберите документ для проведения", 10);
			Возврат;
		КонецЕсли;

		Рез = кзВыбратьКолонку(ЭлементыФормы);
		Если Рез = Неопределено Тогда
			Возврат
		КонецЕсли;
		
		Сообщить("Проведение документа (колонка """ + Рез + """) начато в " + ТекущаяДата());
		
		Если НЕ Документы.ТипВсеСсылки().СодержитТип(ТипЗнч(ТекДанные[Рез])) Тогда
			Ошибки = Ошибки + 1;
			Сообщить("Ошибка в строке " + ТекДанные[Рез] + "]: " + " значение не является документом");			
		Иначе
			Попытка
				ТекДок = ТекДанные[Рез].ПолучитьОбъект();
				ТекДок.Записать(РежимЗаписиДокумента.Проведение);
				Сообщить(Строка(ТекДок) + " проведен");
			Исключение
				СтандартнаяОшибка = ОписаниеОшибки();
				Сообщить("Ошибка в строке " + ТекДанные[Рез] + "]" + Прав(СтандартнаяОшибка, СтрДлина(СтандартнаяОшибка) - Найти(СтандартнаяОшибка, "}:")));			
			КонецПопытки;
		КонецЕсли;
		
		Сообщить("Проведение документа (колонка """ + Рез + """) завершено в " + ТекущаяДата());
		
	Иначе
		
		//Проводим все документы		
		Рез = кзВыбратьКолонку(ЭлементыФормы);
		Если Рез = Неопределено Тогда
			Возврат
		КонецЕсли;
		Сообщить("Проведение документов (колонка """ + Рез + """) начато в " + ТекущаяДата());
		
		Ошибки = 0;
		
		Форма.мФормаПрогрессора.НадписьСостоянияИндикатораОбщая = "Заполнение начальных параметров ...";
		Форма.мФормаПрогрессора.Открыть();
		Форма.мФормаПрогрессора.ЭлементыФормы.Индикатор.МаксимальноеЗначение = лТаблицаРезультата.Количество();
		Форма.мФормаПрогрессора.НадписьСостоянияИндикатораОбщая = "Проводится документ:";
		Инд = 1;
		Форма.мФормаПрогрессора.ЗначениеИндикатора = 1;

		Для каждого СтрокаРезультатовЗапроса Из лТаблицаРезультата Цикл		
			
			Инд = Инд + 1;
			
			ОбработкаПрерыванияПользователя();
			
			Если НЕ Документы.ТипВсеСсылки().СодержитТип(ТипЗнч(СтрокаРезультатовЗапроса[Рез])) Тогда
				Ошибки = Ошибки + 1;
				Сообщить("Ошибка в строке " + СтрокаРезультатовЗапроса[Рез] + "]: " + " значение не является документом");			
			Иначе
				Попытка
					ТекДок = СтрокаРезультатовЗапроса[Рез].ПолучитьОбъект();
					
					Форма.мФормаПрогрессора.ЗначениеИндикатора = Инд;
					Форма.мФормаПрогрессора.НадписьСостоянияИндикатораТекущая = ТекДок;
					
					ТекДок.Записать(РежимЗаписиДокумента.Проведение);
					Сообщить(Строка(ТекДок) + " проведен");
				Исключение
					СтандартнаяОшибка = ОписаниеОшибки();
					Сообщить("Ошибка в строке " + СтрокаРезультатовЗапроса[Рез] + "]" + Прав(СтандартнаяОшибка, СтрДлина(СтандартнаяОшибка) - Найти(СтандартнаяОшибка, "}:")));			
					Ошибки = Ошибки + 1;
				КонецПопытки;
			КонецЕсли;
			
			Если Ошибки > 100 Тогда
				Сообщить("Много ошибок. Возможно неправильно указана колонка. Обработка прервана");
				Форма.мФормаПрогрессора.Закрыть();
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		
		Сообщить("Проведение документов (колонка """ + Рез + """) завершено в " + ТекущаяДата());
		
		Форма.мФормаПрогрессора.Закрыть();
		
	КонецЕсли;
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ЗафиксироватьТранзакцию();
	Конецесли;
	
КонецПроцедуры // кзДокПроведение()

Процедура кзСнятиеПометкиУдаления(Кнопка, ЭлементыФормы, Форма) Экспорт
	
	Ответ = Вопрос("Выполнить в транзакции?", РежимДиалогаВопрос.ДаНетОтмена); 
	Если Ответ = КодВозвратаДиалога.Отмена Тогда
		возврат;
	ИначеЕсли Ответ = КодВозвратаДиалога.Нет Тогда
		Если Вопрос("В этом режиме все Изменения будут применяться сразу. " + Символы.ПС + 
			"При прерывании обработки сделаные Изменения не отменятся. " + Символы.ПС + 
			"Вы уверены?", РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Нет Тогда
			Возврат;
		Конецесли;
	Конецесли;
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		НачатьТранзакцию();
	Конецесли;
	
	лТаблицаРезультата = Вычислить(мИмяРеквизитаТаблицы);
	Если Кнопка.Имя = "ОтменаПометкиНаУдаления" Тогда
		
		//Обрабатываем текущий документ
		ТекДанные = ЭлементыФормы[мИмяРеквизитаТаблицы].ТекущиеДанные;
		Если ТекДанные = Неопределено Тогда
			ПоказатьПредупреждение(, "Выберите объект для пометки удаления", 10);
			Возврат;
		КонецЕсли;
		
		Если лТаблицаРезультата.Количество() = 0 Тогда
			ПоказатьПредупреждение(, "Таблица результатов запроса пуста.", 10);
			Возврат
		КонецЕсли;
		
		Рез = кзВыбратьКолонку(ЭлементыФормы);
		Если Рез = Неопределено Тогда
			Возврат
		КонецЕсли;
		
		Сообщить("Снятие пометки удаления (колонка """ + Рез + """) начато в " + ТекущаяДата());
		
		Попытка
			ТекДок = ТекДанные[Рез].ПолучитьОбъект();
			ТекДок.УстановитьПометкуУдаления(False);
			Сообщить(Строка(ТекДок) + " пометка удаления снята");
		Исключение
			СтандартнаяОшибка = ОписаниеОшибки();
			Сообщить("Ошибка в строке " + ТекДанные[Рез] + "]" + Прав(СтандартнаяОшибка, СтрДлина(СтандартнаяОшибка) - Найти(СтандартнаяОшибка, "}:")));			
			Ошибки = Ошибки + 1;
		КонецПопытки;
	Иначе
		Если лТаблицаРезультата.Количество() = 0 Тогда
			ПоказатьПредупреждение(, "Таблица результатов запроса пуста.", 10);
			Возврат
		КонецЕсли;
		
		Рез = кзВыбратьКолонку(ЭлементыФормы);
		Если Рез = Неопределено Тогда
			Возврат
		КонецЕсли;
		
		Сообщить("Снятие пометки удаления (колонка """ + Рез + """) начато в " + ТекущаяДата());
		
		Ошибки = 0;
		
		Форма.мФормаПрогрессора.Открыть();
		Форма.мФормаПрогрессора.ЭлементыФормы.Индикатор.МаксимальноеЗначение = лТаблицаРезультата.Количество();
		Форма.мФормаПрогрессора.НадписьСостоянияИндикатораОбщая = "Снятие пометки удаления:";
		Инд = 1;
		Форма.мФормаПрогрессора.ЗначениеИндикатора = 1;

		Для каждого СтрокаРезультатовЗапроса Из лТаблицаРезультата Цикл
			
			Инд = Инд + 1;
			
			ОбработкаПрерыванияПользователя();
			Попытка
				ТекДок = СтрокаРезультатовЗапроса[Рез].ПолучитьОбъект();
					
				Форма.мФормаПрогрессора.ЗначениеИндикатора = Инд;
				Форма.мФормаПрогрессора.НадписьСостоянияИндикатораТекущая = ТекДок;
					
				ТекДок.УстановитьПометкуУдаления(False);
				Сообщить(Строка(ТекДок) + " пометка удаления снята");
			Исключение
				СтандартнаяОшибка = ОписаниеОшибки();
				Сообщить("Ошибка в строке " + СтрокаРезультатовЗапроса[Рез] + "]" + Прав(СтандартнаяОшибка, СтрДлина(СтандартнаяОшибка) - Найти(СтандартнаяОшибка, "}:")));			
				Ошибки = Ошибки + 1;
				Если Ошибки > 100 Тогда
					Сообщить("Много ошибок. Возможно неправильно указана колонка. Обработка прервана");
					Форма.мФормаПрогрессора.Закрыть();
					Прервать;
				КонецЕсли;
			КонецПопытки;
		КонецЦикла;
	КонецЕсли;
	
	Сообщить("Снятие пометки удаления (колонка """ + Рез + """) завершено в " + ТекущаяДата());
		
	Форма.мФормаПрогрессора.Закрыть();
		
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ЗафиксироватьТранзакцию();
	Конецесли;
КонецПроцедуры // кзСнятиеПометкиУдаления()

Процедура кзСнятиеПроведения(Кнопка, ЭлементыФормы, Форма) Экспорт
	Ответ = Вопрос("Выполнить в транзакции?", РежимДиалогаВопрос.ДаНетОтмена); 
	Если Ответ = КодВозвратаДиалога.Отмена Тогда
		возврат;
	ИначеЕсли Ответ = КодВозвратаДиалога.Нет Тогда
		Если Вопрос("В этом режиме все изменения будут применяться сразу. " + Символы.ПС + 
			"При прерывании обработки сделаные изменения не отменятся. " + Символы.ПС + 
			"Продолжить?", РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Нет Тогда
			Возврат;
		Конецесли;
	Конецесли;
	Если Ответ = КодВозвратаДиалога.Да Тогда
		НачатьТранзакцию();
	Конецесли;
	
	лТаблицаРезультата = Вычислить(мИмяРеквизитаТаблицы);
	
	Если лТаблицаРезультата.Количество() = 0 Тогда
		ПоказатьПредупреждение(, "Таблица результатов запроса пуста.", 10);
		Возврат
	КонецЕсли;
	
	Если Кнопка.Имя = "ОтменаПроведения" Тогда
		ТекДанные = ЭлементыФормы[мИмяРеквизитаТаблицы].ТекущиеДанные;
		Если ТекДанные = Неопределено Тогда
			ПоказатьПредупреждение(, "Выберите документ для отмены проведения", 10);
			Возврат;
		КонецЕсли;
		
		Рез = кзВыбратьКолонку(ЭлементыФормы);
		Если Рез = Неопределено Тогда
			Возврат
		КонецЕсли;
		
		Сообщить("Снятие проведения документов (колонка """ + Рез + """) начато в " + ТекущаяДата());
		
		Ошибки = 0;
		
		Если НЕ Документы.ТипВсеСсылки().СодержитТип(ТипЗнч(ТекДанные[Рез])) Тогда
			Ошибки = Ошибки + 1;
			Сообщить("Ошибка в строке " + ТекДанные[Рез] + "]: " + " значение не является документом");			
		Иначе
			Попытка
				ТекДок = ТекДанные[Рез].ПолучитьОбъект();
				ТекДок.Записать(РежимЗаписиДокумента.ОтменаПроведения);
				Сообщить(Строка(ТекДок) + " не проведен");
			Исключение
				СтандартнаяОшибка = ОписаниеОшибки();
				Сообщить("Ошибка в строке " + ТекДанные[Рез] + "]" + Прав(СтандартнаяОшибка, СтрДлина(СтандартнаяОшибка) - Найти(СтандартнаяОшибка, "}:")));			
			КонецПопытки;
		КонецЕсли;
		
		Сообщить("Снятие проведения документов (колонка """ + Рез + """) завершено в " + ТекущаяДата());
	Иначе
		
		Рез = кзВыбратьКолонку(ЭлементыФормы);
		Если Рез = Неопределено Тогда
			Возврат
		КонецЕсли;
		
		Сообщить("Снятие проведения документов (колонка """ + Рез + """) начато в " + ТекущаяДата());
		
		Ошибки = 0;
		
		Форма.мФормаПрогрессора.Открыть();
		Форма.мФормаПрогрессора.ЭлементыФормы.Индикатор.МаксимальноеЗначение = лТаблицаРезультата.Количество();
		Форма.мФормаПрогрессора.НадписьСостоянияИндикатораОбщая = "Снятие с проведения документа:";
		Инд = 1;
		Форма.мФормаПрогрессора.ЗначениеИндикатора = 1;

		Для Каждого СтрокаРезультатовЗапроса Из  лТаблицаРезультата Цикл
			
			Инд = Инд + 1;
			
			ОбработкаПрерыванияПользователя();
			
			Если НЕ Документы.ТипВсеСсылки().СодержитТип(ТипЗнч(СтрокаРезультатовЗапроса[Рез])) Тогда
				Ошибки = Ошибки + 1;
				Сообщить("Ошибка в строке " + СтрокаРезультатовЗапроса[Рез] + "]: " + " значение не является документом");			
			Иначе
				Попытка
					ТекДок = СтрокаРезультатовЗапроса[Рез].ПолучитьОбъект();
					
					Форма.мФормаПрогрессора.ЗначениеИндикатора = Инд;
					Форма.мФормаПрогрессора.НадписьСостоянияИндикатораТекущая = ТекДок;
					
					ТекДок.Записать(РежимЗаписиДокумента.ОтменаПроведения);
					Сообщить(Строка(ТекДок) + " не проведен");
				Исключение
					СтандартнаяОшибка = ОписаниеОшибки();
					Сообщить("Ошибка в строке " + СтрокаРезультатовЗапроса[Рез] + "]" + Прав(СтандартнаяОшибка, СтрДлина(СтандартнаяОшибка) - Найти(СтандартнаяОшибка, "}:")));			
					
					Форма.мФормаПрогрессора.Закрыть();
					
					Ошибки = Ошибки + 1;					
				КонецПопытки;
			КонецЕсли;
			
			Если Ошибки > 100 Тогда
				Сообщить("Много ошибок. Возможно неправильно указана колонка. Обработка прервана");
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		
		Сообщить("Снятие проведения документов (колонка """ + Рез + """) завершено в " + ТекущаяДата());
		
		Форма.мФормаПрогрессора.Закрыть();
		
	КонецЕсли;
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ЗафиксироватьТранзакцию();
	Конецесли;
	
КонецПроцедуры // кзСнятиеПроведения()

Процедура кзПометкаУдаления(Кнопка, ЭлементыФормы, Форма) Экспорт
	
	Ответ = Вопрос("Выполнить в транзакции?", РежимДиалогаВопрос.ДаНетОтмена); 
	Если Ответ = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	ИначеЕсли Ответ = КодВозвратаДиалога.Нет Тогда
		Если Вопрос("В этом режиме все Изменения будут применяться сразу. " + Символы.ПС + 
			"При прерывании обработки сделаные Изменения не отменятся. " + Символы.ПС + 
			"Вы уверены?", РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Нет Тогда
			Возврат;
		Конецесли;
	Конецесли;
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		НачатьТранзакцию();
	Конецесли;
	
	лТаблицаРезультата = Вычислить(мИмяРеквизитаТаблицы);
	
	Если лТаблицаРезультата.Количество() = 0 Тогда
		ПоказатьПредупреждение(, "Таблица результатов запроса пуста.", 10);
		Возврат
	КонецЕсли;
	
	Если Кнопка.Имя = "ПометитьНаУдаление" Тогда
		
		//Обрабатываем текущий документ
		ТекДанные = ЭлементыФормы[лТаблицаРезультата].ТекущиеДанные;
		Если ТекДанные = Неопределено Тогда
			ПоказатьПредупреждение(, "Выберите объект для пометки удаления", 10);
			Возврат;
		КонецЕсли;
		
		Рез = кзВыбратьКолонку(ЭлементыФормы);
		Если Рез = Неопределено Тогда
			Возврат
		КонецЕсли;
		
		Сообщить("Пометка на удаление (колонка """ + Рез + """) начато в " + ТекущаяДата());
		
		Попытка
			ТекОбъект = ТекДанные[Рез].ПолучитьОбъект();
			Если не (Справочники.ТипВсеСсылки().СодержитТип(ТипЗнч(ТекОбъект.Ссылка)) 
				и ТекОбъект.Предопределенный) Тогда
				ТекОбъект.УстановитьПометкуУдаления(True);
				Сообщить(Строка(ТекОбъект) + " помечен на удаление");
			КонецЕсли;
		Исключение                           
			СтандартнаяОшибка = ОписаниеОшибки();
			Сообщить("Ошибка в строке " + ТекДанные[Рез] + "]" + 
			Прав(СтандартнаяОшибка, СтрДлина(СтандартнаяОшибка) - Найти(СтандартнаяОшибка, "}:")));			
		КонецПопытки;
		
	Иначе
		
		Рез = кзВыбратьКолонку(ЭлементыФормы);
		Если Рез = Неопределено Тогда
			Возврат
		КонецЕсли;
		
		Сообщить("Пометка на удаление (колонка """ + Рез + """) начато в " + ТекущаяДата());
		
		Ошибки = 0;
		
		Форма.мФормаПрогрессора.Открыть();
		Форма.мФормаПрогрессора.ЭлементыФормы.Индикатор.МаксимальноеЗначение = лТаблицаРезультата.Количество();
		Форма.мФормаПрогрессора.НадписьСостоянияИндикатораОбщая = "Помечается на удаление объект:";
		Инд = 1;
		Форма.мФормаПрогрессора.ЗначениеИндикатора = 1;

		Для каждого СтрокаРезультатовЗапроса Из лТаблицаРезультата Цикл
			
			Инд = Инд + 1;
			
			ОбработкаПрерыванияПользователя();
			Попытка
				ТекОбъект = СтрокаРезультатовЗапроса[Рез].ПолучитьОбъект();
				Если Справочники.ТипВсеСсылки().СодержитТип(ТипЗнч(ТекОбъект.Ссылка)) 
					и ТекОбъект.Предопределенный Тогда
					Продолжить;
				КонецЕсли;
					
				Форма.мФормаПрогрессора.ЗначениеИндикатора = Инд;
				Форма.мФормаПрогрессора.НадписьСостоянияИндикатораТекущая = ТекОбъект;
					
 				ТекОбъект.УстановитьПометкуУдаления(True);
				Сообщить(Строка(ТекОбъект) + " помечен на удаление");
			Исключение
				СтандартнаяОшибка = ОписаниеОшибки();
				Сообщить("Ошибка в строке " + СтрокаРезультатовЗапроса[Рез] + "]" + 
				Прав(СтандартнаяОшибка, СтрДлина(СтандартнаяОшибка) - Найти(СтандартнаяОшибка, "}:")));			
				Ошибки = Ошибки + 1;
				Если Ошибки > 100 Тогда
					Сообщить("Много ошибок. Возможно неправильно указана колонка. Обработка прервана");
					Прервать;
				КонецЕсли;
			КонецПопытки;
		КонецЦикла;
	КонецЕсли;
	
	Сообщить("Пометка на удаление (колонка """ + Рез + """) завершено в " + ТекущаяДата());
	
		
	Форма.мФормаПрогрессора.Закрыть();
		
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ЗафиксироватьТранзакцию();
	Конецесли;
	
КонецПроцедуры // кзПометкаУдаления()

Процедура кзВыгрузитьВПараметр(Кнопка, Форма) Экспорт
	
	лТаблицаРезультата = Вычислить(мИмяРеквизитаТаблицы);
	
	//Проводим все документы
	Если лТаблицаРезультата.Количество() = 0 Тогда
		ПоказатьПредупреждение(, "Таблица результатов запроса пуста.", 10);
		Возврат
	КонецЕсли;

	Рез = кзВыбратьКолонку(Форма.ЭлементыФормы);
	Если Рез = Неопределено Тогда
		Возврат
	КонецЕсли;

	ТаблицаСПараметрами = Форма.мФормаПараметров.ПараметрыСписок;
	НазваниеКолонки = Рез;
	НоваяСтрока = ТаблицаСПараметрами.Найти(НазваниеКолонки, "Имя");
	Если НоваяСтрока <> Неопределено Тогда
		ПоказатьПредупреждение(, "Такой параметр уже есть", 10);
		Возврат
	Иначе
		НоваяСтрока = ТаблицаСПараметрами.Добавить();
		НоваяСтрока.Имя = НазваниеКолонки;
	Конецесли;
	
	СписокЗначений = Новый СписокЗначений;
	
	Для каждого СтрокаРезультатовЗапроса Из лТаблицаРезультата Цикл		
		ОбработкаПрерыванияПользователя();
		СписокЗначений.Добавить(СтрокаРезультатовЗапроса[Рез]);
	КонецЦикла;
	
	НоваяСтрока.Значение = СписокЗначений;
	НоваяСтрока.Тип      = ПолучитьТипПараметраПоЗначению(НоваяСтрока.Значение);
	
	ПоказатьПредупреждение(, "Загрузка параметров завершена.", 10);

Конецпроцедуры // кзВыгрузитьВПараметр()

//ДЕЙСТВИЯ НАД ТАБЛИЦЕЙ С РЕЗУЛЬТАТОМ ЗАПРОСА

Процедура кзВосстановитьШиринуКолонок(ЭлементыФормы, СтруктураСРазмерами) Экспорт;
	СтруктураСРазмерами = ВосстановитьЗначение("КонсольЗапросов_ОтЛавелина_СтруктураСШиринойКолонок");
	Если гЗначениеНеЗаполнено(СтруктураСРазмерами) или ТипЗнч(СтруктураСРазмерами)<> Тип("Структура") Тогда
		СтруктураСРазмерами = Новый Структура
	КонецЕсли;
КонецПроцедуры
            
Процедура кзЗапомнитьШиринуКолонок(ЭлементыФормы, СтруктураСРазмерами) Экспорт
	Для каждого ТекКолонка Из ЭлементыФормы[мИмяРеквизитаТаблицы].Колонки Цикл
		СтруктураСРазмерами.Вставить(ТекКолонка.Имя,ТекКолонка.Ширина);
	КонецЦикла;
КонецПроцедуры

Функция кзСформироватьТаблицуДляПросмотра(Структура) Экспорт	
	ТаблицаДляПросмотра = новый ТаблицаЗначений;
	ТаблицаДляПросмотра.Колонки.Добавить("Ключ");
	ТаблицаДляПросмотра.Колонки.Добавить("Значение");
	Если ТипЗнч(Структура) = Тип("Структура") Тогда
		Для каждого ТекЗначение Из Структура Цикл
			НоваяСтрока = ТаблицаДляПросмотра.Добавить();
			НоваяСтрока.Ключ = ТекЗначение.Ключ;
			НоваяСтрока.Значение = ТекЗначение.Значение;
		КонецЦикла; 
	КонецЕсли;
	Возврат ТаблицаДляПросмотра
КонецФункции

//СОЗДАНИЕ СПЕЦ ЗАПРОСОВ

Процедура кзВсеСправочники(ЭлементФормы) Экспорт
	Если ЭлементыФормы.ТекстЗапроса.ПолучитьТекст()<> "" Тогда
		Если Вопрос("Текущий запрос очистится. Продолжить?", РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Нет Тогда
			Возврат;			
		КонецЕсли;
	КонецЕсли;
	
	СписокМетаданных = Новый СписокЗначений;
	
	ПризнакПометкиУдаления = (ЭлементФормы.Имя = "КнопкаМенюВсеСправочники");
	
	Для каждого ТекМетаданное Из Метаданные.Справочники Цикл
		СписокМетаданных.Добавить(ТекМетаданное,ТекМетаданное.Имя,ПризнакПометкиУдаления);
	КонецЦикла;
	
	Если НЕ ПризнакПометкиУдаления Тогда 
		Если не СписокМетаданных.ОтметитьЭлементы("Выберите справочники для формирования отчета") Тогда
			Возврат;		
		КонецЕсли;
	Конецесли;
	
	ТекстЗапроса = "";
	сч = 0;
	
	ФормаВыбораПолейЗапроса = ПолучитьФорму("ФормаВыбораПолейЗапроса");
	
	Поля = ВосстановитьЗначение("КонсольЗапросов_ОтЛавелина_ПоляВсеСправочники");
	Поля = ?(ЗначениеЗаполнено(Поля), Поля, "Ссылка");		
	
	ФормаВыбораПолейЗапроса.ПоляЗапроса    = Поля;
	ФормаВыбораПолейЗапроса.УсловиеЗапроса = ВосстановитьЗначение("КонсольЗапросов_ОтЛавелина_УсловиеВсеСправочники");
	ФормаВыбораПолейЗапроса.ИсточникДанных = "Справочники." + ПолучитьОтмеченныеЭлементыСписка(СписокМетаданных)[0].Значение.Имя;
	
	РезультатВыбораПолей = ФормаВыбораПолейЗапроса.ОткрытьМодально();
	Если РезультатВыбораПолей = Неопределено Тогда 
		Возврат;		
	КонецЕсли;
	
	Поля = РезультатВыбораПолей.Поля;
	Условие = РезультатВыбораПолей.Условие;
	
	СохранитьЗначение("КонсольЗапросов_ОтЛавелина_ПоляВсеСправочники",Поля);
	СохранитьЗначение("КонсольЗапросов_ОтЛавелина_УсловиеВсеСправочники",Условие);
	
	Для каждого ТекМетаданное Из СписокМетаданных Цикл
		
		Если НЕ ТекМетаданное.Пометка Тогда
			Продолжить
		КонецЕсли;
		
		//Заменяем шаблонные поля на их значения
		ТекПоля = СтрЗаменить(ВРег(Поля), "[ИМЯ]", """" + ТекМетаданное.Значение.Имя + """");
		ТекПоля = СтрЗаменить(ТекПоля, "[СИНОНИМ]", """" + СтрЗаменить(ТекМетаданное.Значение.Синоним, """", " ") + """");
		
		ТекУсловие = СтрЗаменить(ВРег(Условие), "[ИМЯ]", """" + ТекМетаданное.Значение.Имя + """");
		ТекУсловие = СтрЗаменить(ТекУсловие, "[СИНОНИМ]", """" + СтрЗаменить(ТекМетаданное.Значение.Синоним, """", " ") + """");
		
		ПодЗапрос = "ВЫБРАТЬ " + ТекПоля + " ИЗ Справочник." + ТекМетаданное.Значение.Имя + ?(ТекУсловие = "", "", " ГДЕ " + ТекУсловие);
		
		Запрос = Новый Запрос(Подзапрос);
		Попытка
			ПараметрыЗапросовТекущаяСтрока = Запрос.НайтиПараметры();
		Исключение
			Сообщить("Справочник " + ТекМетаданное.Значение.Имя  + " не попал в запрос (" + ОписаниеОшибки() + ")");
			Продолжить;
		КонецПопытки;
		
		Если сч > 0 Тогда
			ТекстЗапроса = ТекстЗапроса + "
			|ОБЪЕДИНИТЬ ВСЕ 
			|";
		КонецЕсли;
		
		ТекстЗапроса = ТекстЗапроса + ПодЗапрос;
		сч = сч + 1;
	КонецЦикла; 
	
	ЭлементыФормы.ТекстЗапроса.УстановитьТекст(ТекстЗапроса);
	
КонецПроцедуры

Процедура кзВсеДокументы(ЭлементФормы) Экспорт
	
	Если ЭлементыФормы.ТекстЗапроса.ПолучитьТекст()<> "" Тогда
		Если Вопрос("Текущий запрос очистится. Продолжить?", РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Нет Тогда
			Возврат;			
		КонецЕсли;
	КонецЕсли;
	
	СписокМетаданных = Новый СписокЗначений;
	
	ПризнакПометкиУдаления = (ЭлементФормы.Имя = "КнопкаМенюВсеДокументы");
	
	Для каждого ТекМетаданное Из Метаданные.Документы Цикл
		СписокМетаданных.Добавить(ТекМетаданное,ТекМетаданное.Имя,ПризнакПометкиУдаления);
	КонецЦикла;
	
	Если Не ПризнакПометкиУдаления Тогда 
		Если Не СписокМетаданных.ОтметитьЭлементы("Выберите документы для формирования отчета") Тогда
			Возврат;		
		КонецЕсли;
	Конецесли;
	
	ТекстЗапроса = "";
	сч = 0;
	
	ФормаВыбораПолейЗапроса = ПолучитьФорму("ФормаВыбораПолейЗапроса");
	
	Поля = ВосстановитьЗначение("КонсольЗапросов_ОтЛавелина_ПоляВсеДокументы");
	Поля = ?(ЗначениеЗаполнено(Поля), Поля, "Ссылка");		
	
	ФормаВыбораПолейЗапроса.ПоляЗапроса    = Поля;
	ФормаВыбораПолейЗапроса.УсловиеЗапроса = ВосстановитьЗначение("КонсольЗапросов_ОтЛавелина_УсловиеВсеДокументы");
	ФормаВыбораПолейЗапроса.ИсточникДанных = "Документы." + ПолучитьОтмеченныеЭлементыСписка(СписокМетаданных)[0].Значение.Имя;
	
	РезультатВыбораПолей = ФормаВыбораПолейЗапроса.ОткрытьМодально();
	Если РезультатВыбораПолей = Неопределено Тогда 
		Возврат;		
	КонецЕсли;
	
	Поля = РезультатВыбораПолей.Поля;
	Условие = РезультатВыбораПолей.Условие;
	
	СохранитьЗначение("КонсольЗапросов_ОтЛавелина_ПоляВсеДокументы"   , Поля);
	СохранитьЗначение("КонсольЗапросов_ОтЛавелина_УсловиеВсеДокументы", Условие);
	
	Для каждого ТекМетаданное Из СписокМетаданных Цикл
		
		Если НЕ ТекМетаданное.Пометка Тогда
			Продолжить
		КонецЕсли;
		
		ТекПоля = СтрЗаменить(Поля, "[ИМЯ]", """" + ТекМетаданное.Значение.Имя + """");
		ТекПоля = СтрЗаменить(ТекПоля, "[СИНОНИМ]", """" + СтрЗаменить(ТекМетаданное.Значение.Синоним, """", " ") + """");
		
		ТекУсловие = СтрЗаменить(Условие, "[ИМЯ]", """" + ТекМетаданное.Значение.Имя + """");
		ТекУсловие = СтрЗаменить(ТекУсловие, "[СИНОНИМ]", """" + СтрЗаменить(ТекМетаданное.Значение.Синоним, """", " ") + """");
		
		Подзапрос = "ВЫБРАТЬ " + ТекПоля + " ИЗ Документ." + ТекМетаданное.Значение.Имя + ?(ТекУсловие = "", "", " ГДЕ " + ТекУсловие);
		
		Запрос = Новый Запрос(Подзапрос);
		Попытка
			ПараметрыЗапросовТекущаяСтрока = Запрос.НайтиПараметры();
		Исключение
			Сообщить("Документ " + ТекМетаданное.Значение.Имя  + " не попал в запрос (" + ОписаниеОшибки() + ")");
			Продолжить;
		КонецПопытки;
		
		Если сч > 0 Тогда
			ТекстЗапроса = ТекстЗапроса + "
			|ОБЪЕДИНИТЬ ВСЕ
			|";
		КонецЕсли;
		
		ТекстЗапроса = ТекстЗапроса + Подзапрос;
		сч = сч + 1;
	КонецЦикла; 
	ЭлементыФормы.ТекстЗапроса.УстановитьТекст(ТекстЗапроса);
КонецПроцедуры

Процедура кзВыбратьМетаданноеДляЗапроса(ИмяКнопки) Экспорт
	
	Если ЭлементыФормы.ТекстЗапроса.ПолучитьТекст() <> "" Тогда
		Если Вопрос("Текущий запрос очистится. Продолжить?", РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Нет Тогда
			Возврат;			
		КонецЕсли;
	КонецЕсли;
	
	СписокКоллекцийМетаданных = Новый СписокЗначений;
	СписокКоллекцийМетаданных.Добавить("Справочник"            , "Справочники"           , Истина,БиблиотекаКартинок.Справочник);
	СписокКоллекцийМетаданных.Добавить("Документ"              ,"Документы"              , Истина,БиблиотекаКартинок.Документ);
	СписокКоллекцийМетаданных.Добавить("Перечисление"          ,"Перечисления"           , Истина,БиблиотекаКартинок.Перечисление);
	СписокКоллекцийМетаданных.Добавить("ПланВидовХарактеристик","ПланыВидовХарактеристик", Истина,БиблиотекаКартинок.ПланВидовХарактеристик);
	СписокКоллекцийМетаданных.Добавить("ПланСчетов"            ,"ПланыСчетов"            , Истина,БиблиотекаКартинок.ПланСчетов);
	СписокКоллекцийМетаданных.Добавить("ПланВидовРасчета"      ,"ПланыВидовРасчета"      , Истина,БиблиотекаКартинок.ПланВидовРасчета);
	СписокКоллекцийМетаданных.Добавить("РегистрСведений"       ,"РегистрыСведений"       , Истина,БиблиотекаКартинок.РегистрСведений);
	СписокКоллекцийМетаданных.Добавить("РегистрНакопления"     ,"РегистрыНакопления"     , Истина,БиблиотекаКартинок.РегистрНакопления);
	СписокКоллекцийМетаданных.Добавить("РегистрБухгалтерии"    ,"РегистрыБухгалтерии"    , Истина,БиблиотекаКартинок.РегистрБухгалтерии);
	СписокКоллекцийМетаданных.Добавить("РегистрРасчета"        ,"РегистрыРасчета"        , Истина,БиблиотекаКартинок.РегистрРасчета);
	СписокКоллекцийМетаданных.Добавить("БизнесПроцесс"         ,"БизнесПроцессы"         , Истина,БиблиотекаКартинок.БизнесПроцесс);
	СписокКоллекцийМетаданных.Добавить("Задача"                ,"Задачи"                 , Истина,БиблиотекаКартинок.Задача);
	
	Сч = 0;
	Пока Сч < СписокКоллекцийМетаданных.Количество() Цикл 
		Если Метаданные[СписокКоллекцийМетаданных[Сч].Представление].Количество() = 0 Тогда 
			СписокКоллекцийМетаданных.Удалить(Сч);
			Сч = Сч - 1;
			КонецЕсли;
		
		Сч = Сч + 1;
	КонецЦикла;
	
	ВыбраннаяКоллекция = СписокКоллекцийМетаданных.ВыбратьЭлемент("Выберите коллекцию");
	
	Если ВыбраннаяКоллекция = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Ответ = Вопрос("Выбрать все элементы из коллекции [" + ВыбраннаяКоллекция.Представление + "]""", РежимДиалогаВопрос.ДаНетОтмена,,,"Вопрос");
	
	Если Ответ = КодВозвратаДиалога.Отмена Тогда 
		Возврат;
	КонецЕсли;
	
	СписокМетаданных = Новый СписокЗначений;	
	Для каждого ТекМетаданное Из Метаданные[ВыбраннаяКоллекция.Представление] Цикл
		СписокМетаданных.Добавить(ТекМетаданное, ТекМетаданное.Имя, Ответ = КодВозвратаДиалога.Да);
	КонецЦикла;

	Если Ответ = КодВозвратаДиалога.Нет Тогда 
		Если Не СписокМетаданных.ОтметитьЭлементы("Выберите элементы коллекции [" + ВыбраннаяКоллекция.Представление + "] для формирования отчета") Тогда
			Возврат;		
		КонецЕсли;
	Конецесли;
	
	ТекстЗапроса = "";
	сч = 0;
	
	ФормаВыбораПолейЗапроса = ПолучитьФорму("ФормаВыбораПолейЗапроса");
	
	ПрефиксСохраняемыхЗначений = "ВыбратьМетаданноеДляЗапроса_" + ВыбраннаяКоллекция.Представление;
	
	Поля = ВосстановитьЗначение("КонсольЗапросов_ОтЛавелина_Поля" + ПрефиксСохраняемыхЗначений);
	
	ФормаВыбораПолейЗапроса.ПоляЗапроса    = Поля;
	ФормаВыбораПолейЗапроса.УсловиеЗапроса = ВосстановитьЗначение("КонсольЗапросов_ОтЛавелина_Условие" + ПрефиксСохраняемыхЗначений);
	ФормаВыбораПолейЗапроса.ИсточникДанных = ВыбраннаяКоллекция.Представление + "." + ПолучитьОтмеченныеЭлементыСписка(СписокМетаданных)[0].Значение.Имя;
	
	РезультатВыбораПолей = ФормаВыбораПолейЗапроса.ОткрытьМодально();
	Если РезультатВыбораПолей = Неопределено Тогда 
		Возврат;		
	КонецЕсли;
	
	Поля = РезультатВыбораПолей.Поля;
	Условие = РезультатВыбораПолей.Условие;
	
	СохранитьЗначение("КонсольЗапросов_ОтЛавелина_Поля" + ПрефиксСохраняемыхЗначений   , Поля);
	СохранитьЗначение("КонсольЗапросов_ОтЛавелина_Условие" + ПрефиксСохраняемыхЗначений, Условие);
	
	Для каждого ТекМетаданное Из СписокМетаданных Цикл
		
		Если НЕ ТекМетаданное.Пометка Тогда
			Продолжить
		КонецЕсли;
		
		ТекПоля = СтрЗаменить(Поля, "[ИМЯ]", """" + ТекМетаданное.Значение.Имя + """");
		ТекПоля = СтрЗаменить(ТекПоля, "[СИНОНИМ]", """" + СтрЗаменить(ТекМетаданное.Значение.Синоним, """", " ") + """");
		
		ТекУсловие = СтрЗаменить(Условие, "[ИМЯ]", """" + ТекМетаданное.Значение.Имя + """");
		ТекУсловие = СтрЗаменить(ТекУсловие, "[СИНОНИМ]", """" + СтрЗаменить(ТекМетаданное.Значение.Синоним, """", " ") + """");
		
		Подзапрос = "ВЫБРАТЬ " + ТекПоля + " ИЗ " + ВыбраннаяКоллекция.Значение + "." + ТекМетаданное.Значение.Имя + ?(ТекУсловие = "", "", " ГДЕ " + ТекУсловие);
		
		Запрос = Новый Запрос(Подзапрос);
		Попытка
			ПараметрыЗапросовТекущаяСтрока = Запрос.НайтиПараметры();
		Исключение
			Сообщить("Объект метаданных " + ВыбраннаяКоллекция.Представление + "." + ТекМетаданное.Значение.Имя  + " не попал в запрос (" + ОписаниеОшибки() + ")", СтатусСообщения.Важное);
			Продолжить;
		КонецПопытки;
		
		Если сч > 0 Тогда
			ТекстЗапроса = ТекстЗапроса + "
			|ОБЪЕДИНИТЬ ВСЕ
			|";
		КонецЕсли;
		
		ТекстЗапроса = ТекстЗапроса + Подзапрос;
		сч = сч + 1;
	КонецЦикла; 
	ЭлементыФормы.ТекстЗапроса.УстановитьТекст(ТекстЗапроса);
КонецПроцедуры // кзВыбратьМетаданноеДляЗапроса


/////////////////////////////////////////
// ПРОЧИЕ ПРОЦЕДУРЫ И ФКНЦИИ

Функция ПолучитьОтмеченныеЭлементыСписка(СписокЗначений, КоличествоВозвращаемыхЭлементов = 0)
	
	Результат = Новый СписокЗначений;
	
	Сч = 0;
	Для каждого ЭлементСписка Из СписокЗначений Цикл
		
		Сч = Сч + 1;
		
		Если КоличествоВозвращаемыхЭлементов > 0 И КоличествоВозвращаемыхЭлементов < Сч Тогда 
			Прервать;
		КонецЕсли;
		
		Если ЭлементСписка.Пометка Тогда 
			Результат.Добавить(ЭлементСписка.Значение, ЭлементСписка.Представление);
		КонецЕсли;
		
	КонецЦикла; 

	Возврат Результат

КонецФункции // ПолучитьОтмеченныеЭлементыСписка()
 
Функция кзСвоеДеревоЗапросов(Дерево) Экспорт
	СвоеДерево       = Дерево.Скопировать();
	СтрокаДерева     = СвоеДерево.Строки.Добавить();
	СтрокаСШаблонами = ДополнительныеПараметры.Найти("Шаблоны", "Параметр");
	
	Если СтрокаСШаблонами <> Неопределено Тогда
		СлужебнаяСтрокаДереваЗапросов = ДополнительныеПараметры.Найти("СлужебнаяСтрокаДереваЗапросов", "Параметр");
		СтрокаДерева.ТекстЗапроса     = СлужебнаяСтрокаДереваЗапросов.Значение;
		СтрокаДерева.ПараметрыЗапросов = СтрокаСШаблонами.Значение;
	Конецесли;
	
	Возврат СвоеДерево;
КонецФункции

Функция кзПреобразоватьСекунды(Секунд) Экспорт
	Если Секунд > 0 Тогда
		Час = Цел(Секунд / 60 / 60);
		Мин = Цел((Секунд % 3600) / 60);
		Сек = Цел(Секунд % 60);
		Результат = ?(час > 0, строка(час) + " час ", "") + ?(мин > 0, строка(мин) + " мин ", "") + ?(сек > 0, строка(сек) + " сек", "");
	Иначе
		Результат = "меньше секунды"
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция кзВыбратьКолонку(ЭлементыФормы) Экспорт
	
	лКолонкиТаблицыРезультата = ЭлементыФормы[мИмяРеквизитаТаблицы].Колонки;
	
	Если лКолонкиТаблицыРезультата.Количество() = 0 Тогда
		Рез = Неопределено
	ИначеЕсли лКолонкиТаблицыРезультата.Количество() = 1 Тогда
		Рез = лКолонкиТаблицыРезультата[0].Имя
	Иначе
		СписокКолонок = Новый СписокЗначений;
		Для каждого КолонкаРезультатаЗапроса Из лКолонкиТаблицыРезультата Цикл
			СписокКолонок.Добавить(КолонкаРезультатаЗапроса.Имя);
		КонецЦикла;
		Рез = СписокКолонок.ВыбратьЭлемент("Выберите колонку для указания источника данных");
		Рез = ?(Рез = Неопределено,Неопределено,Рез.Значение);
	КонецЕсли;
	
	Возврат Рез;
	
КонецФункции // кзВыбратьКолонку()     

Процедура кзСкопироватьВБуфер(Форма, Текст) Экспорт
	НазваниеЭлемента = "ПолеHTMLДокумента321432458767";
	Форма.ЭлементыФормы.Добавить(Тип("ПолеHTMLДокумента"), НазваниеЭлемента, Ложь); 
	Окно = Форма.ЭлементыФормы[НазваниеЭлемента].Документ.parentWindow; 
	Окно.ClipboardData.SetData("Text", Текст); 
	Индекс = Форма.ЭлементыФормы.Индекс(Форма.ЭлементыФормы.Найти(НазваниеЭлемента)); 
	Форма.ЭлементыФормы.Удалить(Индекс); 
Конецпроцедуры

&НаКлиенте
Процедура ОбработкаДействияКодЗапросПолучитьЗапрос(ПростаяОбработкаТекста)
	
	лЗапрос = гПолучитьЗапросИзТекста(гПолучитьСодержимоеБуфера(), ПростаяОбработкаТекста);
	
	лПолеТекстаЗапроса = ЭлементыФормы.ТекстЗапроса;
	Если лПолеТекстаЗапроса.ВыделенныйТекст = "" Тогда
		лПолеТекстаЗапроса.ДобавитьСтроку(лЗапрос.Текст); 
	Иначе
		лПолеТекстаЗапроса.ВыделенныйТекст = лЗапрос.Текст;
	КонецЕсли;
	
	Если лЗапрос.Параметры.Количество() > 0 Тогда
		гОбработкаДействийЗаполнитьПараметрыИзЗапроса(мФормаПараметров);
		Для каждого лПараметр Из лЗапрос.Параметры Цикл
			Попытка
				лСтрокаСПараметром = мФормаПараметров.ПараметрыСписок.Найти(лПараметр.Имя, "Имя");
				Если лСтрокаСПараметром = Неопределено Тогда
					Сообщить("Параметр " + лПараметр.Имя + "(" + лПараметр.Значение + ")" + " не используется в запросе. Не добавлен.");
				Иначе
					лСтрокаСПараметром.Значение = лПараметр.Значение;
				КонецЕсли;
			Исключение
				Сообщить("Неудачная попытка установки параметра " + лПараметр.Имя + "(" + лПараметр.Значение + ")");
			КонецПопытки; 
		КонецЦикла; 
	КонецЕсли;
	
КонецПроцедуры // ОбработкаДействияКодЗапросПолучитьЗапрос()

Процедура ЗагрузитьВеткуИзФайла(Знач Ветка, Колонки) Экспорт
	
	Если Ветка = Неопределено Тогда
		ПоказатьПредупреждение(, "Не выбрана ветка для сохранения.", 10);
		Возврат;
	КонецЕсли;
	
	Длг = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);

	мИмяФайлаВетки = ВосстановитьЗначение("КонсольЗапросов_ОтЛавелина_ФайлДляВосстановленияВеток");
	Если гЗначениеНеЗаполнено(мИмяФайлаВетки) Тогда
		мИмяФайлаВетки = "";
	КонецЕсли;

	лФайлСВеткой = Новый Файл(мИмяФайлаВетки);
	
	Длг.ПолноеИмяФайла = мИмяФайлаВетки;
	Длг.Каталог        = лФайлСВеткой.Путь;
	Длг.Заголовок      = "Укажите файл для загрузки ветки";
	Длг.Фильтр         = "Файлы веток (*.sl)|*.sl";
	Длг.Расширение     = "sl";
		
	Если Длг.Выбрать() Тогда
		СохранитьЗначение("КонсольЗапросов_ОтЛавелина_ФайлДляВосстановленияВеток", Длг.ПолноеИмяФайла);
	Иначе
		Возврат;
	КонецЕсли;

	ЗагрузитьЗапросы(Истина, Длг.ПолноеИмяФайла, Ветка);	
	
КонецПроцедуры // ЗагрузитьВеткуИзФайла()

Функция кзВыполнитьКод() Экспорт
	КодДляВыполнения = мФормаИсполняемыйКод.ОткрытьМодально();
	Если КодДляВыполнения <> Неопределено Тогда
		Попытка
			Выполнить(КодДляВыполнения);
		Исключение
			лТекстОшибки = ОписаниеОшибки();
			гВывестиОшибкуВыполнениякода(лТекстОшибки, КодДляВыполнения);
		КонецПопытки;
	КонецЕсли;
КонецФункции

Функция кзЗаполнитьДеревоСПризнакомВСЕ(Форма, мРезЗапроса) Экспорт
	
	Построитель = новый ПостроительЗапроса;
	Построитель.Текст = Форма.ПолучитьТекстЗапроса(Истина);
	Построитель.ЗаполнитьНастройки();
	
	СписокИзмерений = новый СписокЗначений;
	Для каждого ТекИзмерение Из Построитель.Измерения Цикл
		СписокИзмерений.Добавить(ТекИзмерение.Имя);
	КонецЦИкла;
	
	Если СписокИзмерений.Количество() = 1 Тогда
		Измерения = СписокИзмерений[0].Значение;
	Иначе
		ВыбЭлемент = СписокИзмерений.ВыбратьЭлемент("Веберите группировку для обхода.");
		Если ВыбЭлемент = неопределено Тогда
			Возврат Неопределено;
		Иначе
			Измерения = ВыбЭлемент.Значение;
		КонецЕсли;
	КонецЕсли;
	
	РезультатЗапроса = мРезЗапроса.Выбрать(ОбходРезультатаЗапроса.Прямой,Измерения, "Все");
					
	РезультатДерево = новый ДеревоЗначений;
	Для каждого Колонка Из мРезЗапроса.Колонки Цикл
		РезультатДерево.Колонки.Добавить(Колонка.Имя);
	КонецЦикла;
					
	ТекУровень = 0;
	ТекРодитель = РезультатДерево.Строки;
	Пока РезультатЗапроса.Следующий() Цикл
		Если РезультатЗапроса.Уровень() < ТекУровень Тогда
			ТекУровень = ТекУровень - 1;
			ТекРодитель = ТекРодитель.Родитель.Родитель;
			Если ТекРодитель = Неопределено Тогда
				ТекРодитель = РезультатДерево.Строки;
			Иначе
				ТекРодитель = ТекРодитель.Строки;
			КонецЕсли;
		ИначеЕсли РезультатЗапроса.Уровень() > ТекУровень Тогда
			ТекУровень = ТекУровень + 1;
			ТекРодитель = ТекРодитель[ТекРодитель.Количество() - 1].Строки;
		КонецЕсли;
		НоваяСтрока = ТекРодитель.Добавить();

		Для каждого Колонка Из РезультатДерево.Колонки Цикл
			НоваяСтрока[Колонка.Имя] = РезультатЗапроса[Колонка.Имя];
		КонецЦикла;
	КонецЦикла;
	
	Возврат РезультатДерево 
	
КонецФункции

Функция ЗагрузитьЗапросыИзОблака(идПакетаВОблаке, идЗапроса, идСтрокиКода, включатьПодчиненные, Родитель)
	лСоединение = гВосстановитьИдентификаторСессииConsqueryCloud();
	лРезультат = гПолучитьЗапросыИзОблака(лСоединение, идПакетаВОблаке, идЗапроса, идСтрокиКода, включатьПодчиненные);
	Если лРезультат.Статус = "OK" Тогда 
		ДобавитьВетку(лРезультат.Данные, Родитель);
	Иначе
		Сообщить("Ошибка добавления запроса: " + лРезультат.ТекстОшибки);
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;	
КонецФункции

Функция ЗагрузитьЗапросы(локально, Знач идентификаторФайла, веткаПриемника = Неопределено, типЗагружаемогоОбъекта = Неопределено)
	
	лОшибка = Ложь;
	
	Если ЗначениеЗаполнено(идентификаторФайла) Тогда 
		
		лТекстОшибки = "";
		
		лРезультатЗагрузки = ПрочитатьФайлСЗапросомJSON(Локально, идентификаторФайла, веткаПриемника, лТекстОшибки, Не локально);

		Если Локально Тогда 
			Если лРезультатЗагрузки = 0 Тогда 
				лТекстОшибки = "Ошибка загрузки.  Текст ошибки: """ + лТекстОшибки + """
					|Попытка загрузки старого формата...";
				Сообщить(лТекстОшибки, СтатусСообщения.Важное);
				
				лОшибка = Не ЗагрузитьЗапросыИзФайлаСтараяВерсия(идентификаторФайла, лТекстОшибки);
				Если Не лОшибка Тогда 
					Сообщить("Файл загружен.", СтатусСообщения.Информация);
				Иначе
					лТекстОшибки = "Ошибка загрузки старого формата.  Текст ошибки: """ + лТекстОшибки + """";
					Сообщить(лТекстОшибки, СтатусСообщения.Важное);
				КонецЕсли;
			ИначеЕсли лРезультатЗагрузки = -1 Тогда 
				Сообщить(лТекстОшибки, СтатусСообщения.Важное);
				лОшибка = Истина;
			КонецЕсли;
		ИначеЕсли лРезультатЗагрузки = -1 Тогда 
			Сообщить(лТекстОшибки, СтатусСообщения.Важное);
			лОшибка = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если ДеревоЗапросов.Строки.Количество() = 0 Тогда 
		Если ЗначениеЗаполнено(идентификаторФайла) Тогда 
			Сообщить("Невозможно загрузить список запросов из указанного файла или файл пуст.
						   |Выберите другой файл.", СтатусСообщения.Важное);
		КонецЕсли;
		СоздатьНовыйФайлЗапросов();
	КонецЕсли;			
	
	Если веткаПриемника = Неопределено Тогда 
		ЭлементыФормы.ДеревоЗапросов.ТекущаяСтрока = ДеревоЗапросов.Строки[0];
		гМассивИзмененныхСтрок.Очистить();
		Модифицированность = Ложь;	
	Иначе
		Модифицированность = Истина;	
	КонецЕсли;
	
	Если лОшибка Тогда 
		Сообщить("Файл не загружен. Обратитесь с проблемой к разработчику, возможно еще не все потеряно(меню: ""Информация > Отправить отзыв"").", СтатусСообщения.Информация);
	Иначе
		ОбновитьЗаголовок();			
	КонецЕсли;
	
	Возврат Не лОшибка
	
КонецФункции // ЗагрузитьЗапросы()

Функция ПолучитьТипПараметраПоЗначению(Значение)
	
	Если ТипЗнч(Значение) = Тип("СписокЗначений") ИЛИ ТипЗнч(Значение) = Тип("Массив") Тогда 
		Возврат 2
	ИначеЕсли ТипЗнч(Значение) = Тип("ТаблицаЗначений") Тогда 
		Возврат 3
	Иначе
		Возврат 1
	КонецЕсли;

КонецФункции // ПолучитьТипПараметраПоЗначению()
 
// Процедура загружает строку в таблицу значений
//
// Параметры
//  <Текст>  - <Строка> - строка, которую необходимо добавить в таблицу значений
//  <ТаблицаЗначений>  - <ТаблицаЗначений> - Таблица значений, в которую добавляем строку
//  <Разделитель>  - <Строка> - разделитель колонок в строке
//
&НаКлиенте
Процедура ТекстВТаблицу(ИсходныйТекст, ТаблицаЗначений, Разделитель = Неопределено)
	
	Текст =  Новый ТекстовыйДокумент;	
	Текст.УстановитьТекст(ИсходныйТекст);
	
	лКоличествоСтрок = Текст.КоличествоСтрок();
	
	Если лКоличествоСтрок = 0 Тогда 
		Возврат;
	КонецЕсли;
	
	Если Разделитель = Неопределено Тогда
		Разделитель = Символы.Таб;		
	КонецЕсли;
	
	лКоличествоКолонокЭлементаТЗ = ТаблицаЗначений.Колонки.Количество();
	лМассивКолонокСтрокиТаблицы = гРазложитьСтрокуВМассивПодстрок(Текст.ПолучитьСтроку(1), Разделитель);

	Если лМассивКолонокСтрокиТаблицы.Количество() > лКоличествоКолонокЭлементаТЗ Тогда 
		Результат = Вопрос("Добавить колонки в таблице?", РежимДиалогаВопрос.ДаНетОтмена);
		Если Результат = КодВозвратаДиалога.Отмена Тогда 
			Возврат
		ИначеЕсли Результат = КодВозвратаДиалога.Да Тогда 
			Для Сч = лКоличествоКолонокЭлементаТЗ По лМассивКолонокСтрокиТаблицы.Количество() - 1 Цикл
				НоваяКолонка     = ТаблицаЗначений.Колонки.Добавить();
				НоваяКолонка.Имя = "НоваяКолонка__" + Формат(Сч, "ЧН=; ЧГ=0");
			КонецЦикла; 
		КонецЕсли;
	КонецЕсли;

	лКоличествоКолонокЭлементаТЗ = ТаблицаЗначений.Колонки.Количество();
	
	Для сч = 1 По Текст.КоличествоСтрок() Цикл
		лМассивКолонокСтрокиТаблицы = гРазложитьСтрокуВМассивПодстрок(Текст.ПолучитьСтроку(сч), Разделитель);
		лКоличествоЗаполняемыхКолонок = Мин(лКоличествоКолонокЭлементаТЗ, лМассивКолонокСтрокиТаблицы.Количество());
		НоваяСтрока = ТаблицаЗначений.Добавить();
		Для Сч1 = 0 По лКоличествоЗаполняемыхКолонок - 1 Цикл
			НоваяСтрока[Сч1] = лМассивКолонокСтрокиТаблицы[Сч1];
		КонецЦикла; 
	КонецЦикла; 
	
КонецПроцедуры // ТекстВТаблицу()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ УПРАВЛЕНИЯ ВНЕШНИМ ВИДОМ ФОРМЫ

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ
Процедура ОбновлениеОтображения()
	Если мФормаПрогрессора<> Неопределено и мФормаПрогрессора.Открыта() Тогда
		мФормаПрогрессора.Закрыть();
	КонецЕсли;
	
	лТаблицаРезультата = Вычислить(мИмяРеквизитаТаблицы);
	
	Если ЭлементыФормы.ПанельРезультата.ТекущаяСтраница.Имя = "Результат" Тогда
		СтатусРезультатаТаблицы = "";
		Если ЭлементыФормы[мИмяРеквизитаТаблицы].Данные = мИмяРеквизитаТаблицы и лТаблицаРезультата.Количество() > 0 Тогда
			СтатусРезультатаТаблицы = "Строк в запросе: " + лТаблицаРезультата.Количество();
		КонецЕсли;
		Если мДатаОкончанияЗапроса <> Неопределено и мДатаНачалаВыполнения <> Неопределено Тогда
			СтатусРезультатаТаблицы = СтатусРезультатаТаблицы + ?(мДатаНачалаВыполнения = Неопределено, 
				"", ?(СтатусРезультатаТаблицы = "", "", "; ") + 
				"Выполнение запроса: " + кзПреобразоватьСекунды(мДатаОкончанияЗапроса - мДатаНачалаВыполнения));
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

//+++ Сервис: Выгрузка в документ
Процедура ИнициализацияКнопкиВыгрузки()
	
	лТаблицаРезультата = Вычислить(мИмяРеквизитаТаблицы);
	
	Если НЕ ЗначениеЗаполнено(СсылкаНаДокумент) Тогда
		Если ЭлементыФормы.СсылкаНаДокумент.Значение = Неопределено Тогда
			ЭлементыФормы.ВыгрузитьВДокумент.Заголовок = "Выгрузить в новый документ <???>";
		Иначе
			ЭлементыФормы.ВыгрузитьВДокумент.Заголовок = "Выгрузить в новый документ <" + ЭлементыФормы.СсылкаНаДокумент.Значение.Метаданные().Имя + "." + ?(ЗначениеЗаполнено(ИмяТабличнойЧастиЗагружаемогоДокумента), ИмяТабличнойЧастиЗагружаемогоДокумента, "???") + ">";
		КонецЕсли;
		ЭлементыФормы.ВыгрузитьВДокумент.Доступность = (лТаблицаРезультата.Количество() > 0);
	Иначе
		Если ЗначениеЗаполнено(ИмяТабличнойЧастиЗагружаемогоДокумента)  Тогда
			ЭлементыФормы.ВыгрузитьВДокумент.Доступность = (лТаблицаРезультата.Количество() > 0);
			ЭлементыФормы.ВыгрузитьВДокумент.Заголовок = "Выгрузить в <" + СсылкаНаДокумент.Метаданные().Имя + " № " + СокрЛП(СсылкаНаДокумент.Номер) + "." + ИмяТабличнойЧастиЗагружаемогоДокумента + ">";
		Иначе
			ЭлементыФормы.ВыгрузитьВДокумент.Доступность = Ложь;
			ЭлементыФормы.ВыгрузитьВДокумент.Заголовок = "Выгрузить в <???>";
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры
//--- Сервис: Выгрузка в документ

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

Процедура КоманднаяПанельФормыЗаполнитьПараметрыИзЗапроса(Кнопка)
	гОбработкаДействийЗаполнитьПараметрыИзЗапроса(мФормаПараметров);
КонецПроцедуры

Процедура КоманднаяПанельФормыПараметрыИБ(Кнопка)
	мФормаПараметрыИБ.Открыть();
КонецПроцедуры

Процедура МенюТаблицаРезультатаПолучитьТипЗначения(Кнопка)
	СтандартнаяОбработка = Ложь;
	лТаблицаРезультатаЭлемент = ЭлементыФормы[мИмяРеквизитаТаблицы];
	Если лТаблицаРезультатаЭлемент.ТекущиеДанные <> Неопределено Тогда 
		СодержимоеЯчейки = лТаблицаРезультатаЭлемент.ТекущиеДанные[лТаблицаРезультатаЭлемент.ТекущаяКолонка.Имя];
		ПоказатьВводСтроки(Новый ОписаниеОповещения("ПослеВводаСтроки", ЭтаФорма, Новый Структура("Режим", "ПоказатьТипЗначения")), гСтроковоеПредставлениеТипа(ТипЗнч(СодержимоеЯчейки)), "Тип значения", ,Истина);
	КонецЕсли;
КонецПроцедуры

// Меню для работы с временными таблицами

Функция кзДобавитьВременнуюТаблицу(ИмяВременнойТаблицы, МенеджерВременныхТаблиц)
	
	Результат = Новый ТаблицаЗначений;
	
	Если МенеджерВременныхТаблиц <> Неопределено Тогда
		Попытка
			ВремЗапрос = Новый Запрос;
			ВремЗапрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
			ВремЗапрос.Текст = "Выбрать * из " + ИмяВременнойТаблицы;
			Результат = ВремЗапрос.Выполнить().Выгрузить();
		Исключение
			Результат = Неопределено;
			//Сообщить("Не удалось показать временную таблицу " + ИмяВременнойТаблицы + " : " + ОписаниеОшибки(), СтатусСообщения.ОченьВажное);
		КонецПопытки;
	КонецЕсли;
	
	Возврат Результат
	
КонецФункции

// Обработчики нажатий на менюшки показа и уничтожения временных таблиц
//
Процедура ПодменюВременныеТаблицыПриВыбореПоказатьВременнуюТаблицу(Элемент)
	
	ФормаВложеннойТаблицы = Обработка.ПолучитьФорму("ФормаВложеннойТаблицы", ЭтаФорма, Новый УникальныйИдентификатор);
	ФормаВложеннойТаблицы.ВложеннаяТаблица = гСоответствиеВременныеТаблицы[Элемент.Имя];
	ФормаВложеннойТаблицы.ЭлементыФормы.ВложеннаяТаблица.СоздатьКолонки();
	ФормаВложеннойТаблицы.Заголовок = "Временная таблица " + Элемент.Имя;
	ФормаВложеннойТаблицы.Открыть();
	
КонецПроцедуры // ПодменюВременныеТаблицыПриВыбореПоказатьВременнуюТаблицу()

Процедура КоманднаяПанельФормыПоказатьВыделеннуюВременнуюТаблицу(Кнопка)
	
    ТекстЗап = СокрЛП(ЭлементыФормы.ТекстЗапроса.ПолучитьВыделенныйТекст());
	Если СтрДлина(ТекстЗап) = 0 Тогда
		ПоказатьПредупреждение(, "Выделите временную таблицу для просмотра", 10);
		Возврат;
	КонецЕсли;
	
	лНазваниеВременнойТаблицы = ТекстЗап;
	
	Если гСоответствиеВременныеТаблицы[лНазваниеВременнойТаблицы] = Неопределено Тогда 
		ПоказатьПредупреждение(, "Не найдена временная таблица """ + лНазваниеВременнойТаблицы + """", 10);
		Возврат;
	КонецЕсли;
	
	ФормаВложеннойТаблицы = Обработка.ПолучитьФорму("ФормаВложеннойТаблицы", ЭтаФорма, Новый УникальныйИдентификатор);
	ФормаВложеннойТаблицы.ВложеннаяТаблица = гСоответствиеВременныеТаблицы[лНазваниеВременнойТаблицы];
	ФормаВложеннойТаблицы.ЭлементыФормы.ВложеннаяТаблица.СоздатьКолонки();
	ФормаВложеннойТаблицы.Заголовок = "Временная таблица " + лНазваниеВременнойТаблицы;
	ФормаВложеннойТаблицы.Открыть();
	
КонецПроцедуры

Процедура КоманднаяПанельДополнительнаяПанельРезультатаПерейтиКОписаниюВременнойТаблицы(Кнопка)
	
	лПолеТекстаЗапроса = ЭлементыФормы.ТекстЗапроса;
	
    ТекстЗап = СокрЛП(лПолеТекстаЗапроса.ПолучитьВыделенныйТекст());
	Если СтрДлина(ТекстЗап) = 0 Тогда
		ПоказатьПредупреждение(, "Выделите временную таблицу для просмотра", 100);
		Возврат;
	КонецЕсли;
	
	лНазваниеВременнойТаблицы = ТекстЗап;
	
	лВесьТекстЗапроса = ЭлементыФормы.ТекстЗапроса.ПолучитьТекст();
	
	лПозицияВременнойТаблицыВТекстеЗапроса = Найти(ВРег(лВесьТекстЗапроса), "ПОМЕСТИТЬ " + ВРег(лНазваниеВременнойТаблицы));
	
	Если лПозицияВременнойТаблицыВТекстеЗапроса = 0 Тогда 
		ПоказатьПредупреждение(, "В тексте запроса не найдена строка ""Поместить " + лНазваниеВременнойТаблицы + """", 100);
		Возврат;
	КонецЕсли;
	
	лПолеТекстаЗапроса.УстановитьГраницыВыделения(лПозицияВременнойТаблицыВТекстеЗапроса, лПозицияВременнойТаблицыВТекстеЗапроса);
	
КонецПроцедуры

Процедура ПодменюВременныеТаблицыПриВыбореУдалитьВременнуюТаблицу(Элемент)
	
	Если Элемент.Имя = "УдалитьВсе" Тогда
		ВременныеТаблицы.Очистить();		
	Иначе
		ВременныеТаблицы.Удалить(ВременныеТаблицы.Найти(Элемент.Имя));			
	КонецЕсли;
	
	ПерерисоватьСпискиВременныхТаблиц();		

КонецПроцедуры // ПодменюВременныеТаблицыПриВыбореУдалитьВременнуюТаблицу()

Процедура ПодменюВременныеТаблицыИспользоватьМенеджерВременныхТаблиц(Кнопка)
	Кнопка.Пометка = НЕ Кнопка.Пометка;
	Если Не Кнопка.Пометка = Истина Тогда
		ВременныеТаблицы.Очистить();
		ПерерисоватьСпискиВременныхТаблиц();
	КонецЕсли;
КонецПроцедуры

//ДИНАМИЧЕСКИ СОЗДАВАЕМЫЕ КНОПКИ

Процедура КнопкаДействияКнопкаМенюПроведениеДокументов(Кнопка)
	кзДокПроведение(Кнопка, ЭлементыФормы, ЭтаФорма)
КонецПроцедуры

Процедура КнопкаДействияКнопкаМенюОтменаПроведенияДокументов(Кнопка)
	кзСнятиеПроведения(Кнопка, ЭлементыФормы, ЭтаФорма)
КонецПроцедуры // КнопкаДействияКнопкаМенюОтменаПроведенияДокументов

Процедура КнопкаДействияКнопкаМенюПометкаУдаленияОбъектов(Кнопка)
	кзПометкаУдаления(Кнопка, ЭлементыФормы, ЭтаФорма)
КонецПроцедуры

Процедура КнопкаДействияКнопкаМенюСнятиеПометкиУдаленияОбъектов(Кнопка)
	кзСнятиеПометкиУдаления(Кнопка, ЭлементыФормы, ЭтаФорма)
КонецПроцедуры

Процедура КнопкаДействияКнопкаМенюВсеДокументы(Кнопка)
	кзВсеДокументы(Кнопка)
КонецПроцедуры

Процедура КнопкаДействияКнопкаМенюВыбранныеДокументы(Кнопка)	
	кзВсеДокументы(Кнопка)
КонецПроцедуры

Процедура СервисВыгрузитьВПараметр(Кнопка)
	кзВыгрузитьВПараметр(Кнопка, ЭтаФорма)
КонецПроцедуры // СервисВыгрузитьВПараметр()

Процедура КнопкаДействияКнопкаМенюВыполнитьКод(Кнопка)
	кзВыполнитьКод();
КонецПроцедуры

Процедура КнопкаДействияКнопкаМенюПечатьРезультатаЗапроса(Кнопка)
	СохранитьРезультат()
КонецПроцедуры

Процедура КнопкаДействияКнопкаМенюПечатьДанныхТаблицы(Кнопка)
	
	ТекСтраница = ЭлементыФормы.ПанельРезультата.ТекущаяСтраница;
	Если ТекСтраница.Имя = "Результат" Тогда 
		ВывестиНаПечать(ЭлементыФормы[мИмяРеквизитаТаблицы].Значение);
	Иначе
		
		Если ТекСтраница <> Неопределено Тогда
			Если Найти(ТекСтраница.Имя, "СтраницаСРезультатом_") > 0 Тогда
				ВывестиНаПечать(ЭлементыФормы[СтрЗаменить(ТекСтраница.Имя, "СтраницаСРезультатом_", "ТаблицаСРезультатом_")].Значение);
			Конецесли;
		Конецесли;		
	КонецЕсли;

КонецПроцедуры

Процедура КнопкаДействияКнопкаМенюВсеСправочники(Кнопка)
	кзВсеСправочники(Кнопка)
КонецПроцедуры

Процедура КнопкаДействияКнопкаМенюВыбранныеСправочники(Кнопка)
	кзВсеСправочники(Кнопка)
КонецПроцедуры

Процедура КнопкаДействияКнопкаМенюВыбратьМетаданноеДляЗапроса(Кнопка)
	
	кзВыбратьМетаданноеДляЗапроса(Кнопка.Имя)
	
КонецПроцедуры // КнопкаДействияКнопкаМенюВыбратьМетаданноеДляЗапроса

Процедура КнопкаДействияКнопкаМенюЗапомнитьШиринуКолонок(Кнопка)
	кзЗапомнитьШиринуКолонок(ЭлементыФормы, мСтруктураСРазмерами);
КонецПроцедуры

Процедура КнопкаДействияКнопкаМенюПросмотретьТекущиеЗначения(Кнопка)
	ТаблицаДляПросмотра = кзСформироватьТаблицуДляПросмотра(мСтруктураСРазмерами);
	ТаблицаДляПросмотра.ВыбратьСтроку();
КонецПроцедуры

Процедура КнопкаДействияКнопкаМенюСохранитьТекущиеРазмерыПоУмолчанию(Кнопка)
	ТекСтруктура = ВосстановитьЗначение("КонсольЗапросов_ОтЛавелина_СтруктураСШиринойКолонок");
	Если гЗначениеНеЗаполнено(ТекСтруктура) Тогда
		ТекСтруктура = новый Структура
	КонецЕсли;
	Для каждого ЗначениеСтруктуры Из мСтруктураСРазмерами Цикл
		ТекСтруктура.Вставить(ЗначениеСтруктуры.Ключ,ЗначениеСтруктуры.Значение);
	КонецЦикла;
	СохранитьЗначение("КонсольЗапросов_ОтЛавелина_СтруктураСШиринойКолонок",ТекСтруктура);
КонецПроцедуры

Процедура КнопкаДействияКнопкаМенюВосстановитьЗначенияПоУмолчанию(Кнопка)
	кзВосстановитьШиринуКолонок(ЭлементыФормы, мСтруктураСРазмерами);
КонецПроцедуры

Процедура КнопкаДействияКнопкаМенюОчиститьЗначенияПоУмолчанию(Кнопка)
	кзОчиститьЗначениеПоУмолчанию(мСтруктураСРазмерами)
КонецПроцедуры

Процедура КнопкаДействияКнопкаМенюПросмотретьЗначенияПоУмолчанию(Кнопка)
	ТаблицаДляПросмотра = кзСформироватьТаблицуДляПросмотра(ВосстановитьЗначение("КонсольЗапросов_ОтЛавелина_СтруктураСШиринойКолонок"));
	ТаблицаДляПросмотра.ВыбратьСтроку();
КонецПроцедуры

Процедура КнопкаДействияКнопкаМенюУстановитьВсемОднуШирину(Кнопка)
	ТекШиринаКолонки = 10;
	Если ВвестиЧисло(ТекШиринаКолонки, "Введите ширину колонки", 3, 0) Тогда
		ТекКолонки = ЭлементыФормы[мИмяРеквизитаТаблицы].Колонки;
		Для каждого Колонка Из ТекКолонки Цикл
			Колонка.Ширина = ТекШиринаКолонки
		КонецЦикла; 
	КонецЕсли;
КонецПроцедуры

Процедура СервисЗагрузитьИзБуфераВТаблицуСРезультатом(Кнопка)
	
	ТекстВТаблицу(гПолучитьСодержимоеБуфера(), Вычислить(мИмяРеквизитаТаблицы));
	
	ЭлементыФормы[мИмяРеквизитаТаблицы].СоздатьКолонки();
	
	ПоказатьПредупреждение(, "Загрузка данных из буфера.", 10);
	
КонецПроцедуры // СервисЗагрузитьИзБуфераВТаблицуСРезультатом()

Процедура КнопкаДействияКнопкаМенюВыгрузкаВDBF(Кнопка)
	кзКоманднаяПанельФормыВыгрузитьВДБФ(Кнопка)
КонецПроцедуры

Процедура КнопкаДействияКнопкаМенюДиаграмма(Кнопка)
	кзКоманднаяПанельФормыДиаграмма(Кнопка)
КонецПроцедуры

Процедура КнопкаДействияКнопкаМенюПоискСсылок(Кнопка)
	
	КолонкаССылками         = Неопределено;
	лТаблицаНайденныхСсылок = ПолучитьТаблицуНайденныхСсылок(КолонкаССылками);

	Если ЗначениеЗаполнено(лТаблицаНайденныхСсылок) Тогда 
		ВывестиНаПечать(лТаблицаНайденныхссылок);
		Сообщить("Поиск ссылок на объекты (колонка """ + КолонкаССылками + """) завершено в " + ТекущаяДата());
	КонецЕсли;
	
КонецПроцедуры

Процедура КнопкаДействияКнопкаМенюПоискСсылокВСтруктуру(Кнопка)
	
	КолонкаССылками         = Неопределено;
	лТаблицаНайденныхСсылок = ПолучитьТаблицуНайденныхСсылок(КолонкаССылками);

	Если ЗначениеЗаполнено(лТаблицаНайденныхСсылок) Тогда 
		
		Сч = 1;
		Пока Истина Цикл
			ТекПараметр = "Параметр" + Формат(Сч, "ЧГ=0");
			Если Не гСтруктураТЗДляВременныхТаблиц.Свойство(ТекПараметр) Тогда 
				Прервать;
			КонецЕсли;
			Сч = Сч + 1;
		КонецЦикла;
		
		гСтруктураТЗДляВременныхТаблиц.Вставить(ТекПараметр, лТаблицаНайденныхСсылок);
		
		Сообщить("В глобальную структуру выгружена таблица значений с найденными ссылками. 
		|Доступ к таблице в структуре: гСтруктураТЗДляВременныхТаблиц." + ТекПараметр + "
		|Поля таблицы                : Данные, Метаданные, ПутьКРасположениюСсылкиВОбъекте, Ссылка");
		
	КонецЕсли;
	
КонецПроцедуры // КнопкаДействияКнопкаМенюПоискСсылокВСтруктуру

Процедура СервисСформироватьКодДляОтладки(Кнопка)
	кзКоманднаяПанельФормыСформироватьКодДляОтладки(Кнопка)
КонецПроцедуры // СервисСформироватьКодДляОтладки()

Процедура КнопкаДействияКнопкаМенюСервисIntellisense(Кнопка)
	
	ОткрытьМенюIntelliSense(ЭлементыФормы.ТекстЗапроса);
	
КонецПроцедуры // КнопкаДействияКнопкаМенюСервисIntellisense()

Процедура ДополнительноПоиск(Кнопка)
	
	Если мФормаПоиска <> Неопределено И мФормаПоиска.Открыта() Тогда
		мФормаПоиска.АктивИзировать();
		мФормаПоиска.ТекущийЭлемент = мФормаПоиска.Элементыформы.СтрокаПоиска;
	Иначе
		Если мФормаПоиска = Неопределено Тогда
			мФормаПоиска = ПолучитьФорму("ФормаПоиск");
		КонецЕсли;
		мФормаПоиска.ВладелецФормы = ЭтаФорма;
		мФормаПоиска.Открыть();
	КонецЕсли;
		
КонецПроцедуры

Процедура ВыборМенюИнформация(Кнопка)
	гНажатиеНаКнопкуВыборМенюИнформация(Кнопка.Имя);
КонецПроцедуры 

//&НаКлиенте
//Функция НастройкиFTP()
//	
//	лФормаНастройкиFTP = ПолучитьФорму("ВнешняяОбработка.КонсольЗапросов.Форма.ФормаНастройкиFTP");
//	ЗаполнитьЗначенияСвойств(лФормаНастройкиFTP, ПараметрыПодключенияFTP);
//	РезультатЗакрытия = лФормаНастройкиFTP.ОткрытьМодально();
//		
//	Если ТипЗнч(РезультатЗакрытия) = Тип("Структура") Тогда 
//		ПараметрыПодключенияFTP = РезультатЗакрытия;
//	КонецЕсли;
//		
//КонецФункции // НастройкиFTP()

//&НаКлиенте
//Функция СохранитьНаFTP()
//	ПоказатьПредупреждение(, "Функционал в разработке...");
//	//Если ПроверитьМодифицированность(мОперацииСЗапросами.СохранитьНаFTP) Тогда 
//	//	Попытка
//	//		FTPСоединение = Новый FTPСоединение(ПараметрыПодключенияFTP.Адрес, 
//	//			?(ЗначениеЗаполнено(ПараметрыПодключенияFTP.Порт), ПараметрыПодключенияFTP.Порт, "21"), ПараметрыПодключенияFTP.Пользователь, ПараметрыПодключенияFTP.Пароль);
//	//		FTPСоединение.Записать(ИмяФайлаСЗапросом, "consquery.data");
//	//		FTPСоединение = Неопределено;
//	//		Состояние("Запрос сохранен на ftp.", 100);
//	//	Исключение
//	//		ПоказатьПредупреждение(, "Ошибка. Подключение не выполнено.", 10, "Сохранение запроса на ftp.");
//	//		Сообщить(ОписаниеОшибки());
//	//	КонецПопытки; 
//	//КонецЕсли;
//КонецФункции // СохранитьНаFTP()

//&НаКлиенте
//Функция ЗагрузитьСFTP()
//	
//	Если ПроверитьМодифицированность() Тогда 
//		Попытка
//			FTPСоединение = Новый FTPСоединение(ПараметрыПодключенияFTP.Адрес, 
//				?(ЗначениеЗаполнено(ПараметрыПодключенияFTP.Порт), ПараметрыПодключенияFTP.Порт, "21"), ПараметрыПодключенияFTP.Пользователь, ПараметрыПодключенияFTP.Пароль);
//				
//			лМассивФайлов = FTPСоединение.НайтиФайлы(, "*.epf", Истина);
//			
//			лСписокФайлов = новый СписокЗначений;
//			лСписокФайлов.ЗагрузитьЗначения(лМассивФайлов);
//			лФайлДляЗагрузки = лСписокФайлов.ПоказатьВыборЭлемента(Новый ОписаниеОповещения("ПоказатьВыборЭлементаОбработкаВыбора", Вычислить(мЭтотОбъектФорма)), "Выберите файл для загрузки...");
//		Исключение
//			ПоказатьПредупреждение(, "Ошибка. Подключение не выполнено.", 10, "Загрузка запроса с ftp.");			
//			Сообщить(ОписаниеОшибки());
//		КонецПопытки; 
//	КонецЕсли;
//	
//КонецФункции // ЗагрузитьСFTP()

Процедура КодЗапросСформироватьКод(Кнопка)
	
	Если Найти(Кнопка.Имя, гРежимыФормированияКодаИзТекстаЗапроса().Выборка) > 0 Тогда
		лРежим = гРежимыФормированияКодаИзТекстаЗапроса().Выборка;
	ИначеЕсли Найти(Кнопка.Имя, гРежимыФормированияКодаИзТекстаЗапроса().ТЗ) > 0 Тогда
		лРежим = гРежимыФормированияКодаИзТекстаЗапроса().ТЗ;
	ИначеЕсли Найти(Кнопка.Имя, гРежимыФормированияКодаИзТекстаЗапроса().ОбработкаРезультата) > 0 Тогда
		лРежим = гРежимыФормированияКодаИзТекстаЗапроса().ОбработкаРезультата;
	Иначе
		Возврат;
	КонецЕсли;
	
	лПараметрыСтруктура = Новый Соответствие;
	Для каждого лПараметр Из мФормаПараметров.ПараметрыСписок Цикл
		лПараметрыСтруктура.Вставить(лПараметр.Имя, Новый Структура("Значение", лПараметр.Значение));
	КонецЦикла; 
	
	Результат = гСформироватьКодЗапросаДля1С(ПолучитьТекстЗапроса(Истина), лПараметрыСтруктура, лРежим);
	
	Ответ = Вопрос("Скопировать в буфер?", РежимДиалогаВопрос.ДаНетОтмена);
	Если Ответ = КодВозвратаДиалога.Да Тогда
		кзСкопироватьВБуфер(ПолучитьФорму("Точка"),Результат);
	ИначеЕсли Ответ = КодВозвратаДиалога.Нет Тогда
		ТД = Новый ТекстовыйДокумент();
		ТД.УстановитьТекст(Результат);
		ТД.Показать(,);
	КонецЕсли;
КонецПроцедуры // КодЗапросСформироватьКод()

Процедура КодЗапросПолучитьЗапрос(Кнопка)
	ОбработкаДействияКодЗапросПолучитьЗапрос(Найти(Кнопка.Имя, гРежимыПолученияЗапросаИзКода().СОбработкой) = 0);
КонецПроцедуры // КодЗапросПолучитьЗапрос()

Процедура ДополнительноОткрытьФормуСпискаМетаданногоизЗапроса(Кнопка)
	
	//Временный алгоритм. Необходимо сделать универсальный для других объектов метаданных!!!!

	СписокОбъектовМетаданных = гСписокОбъектовМетаданныхИзТекста(Элементыформы.ТекстЗапроса.ПолучитьТекст());
	
	Если СписокОбъектовМетаданных.Количество() > 0 Тогда 
		ВыбЗначениеМетаданного = СписокОбъектовМетаданных.ВыбратьЭлемент("Выберите объект для открытия формы списка.");
		Если ВыбЗначениеМетаданного <> Неопределено Тогда 
			ВыбЗначениеМетаданного.Значение.ПолучитьФормуСписка().Открыть();
		КонецЕсли;
	Иначе
		ПоказатьПредупреждение(,"Не найдены метаданные в запросе.");
	КонецЕсли;

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ТАБЛИЧНОГО ПОЛЯ

Процедура ТабличноеПолеШаблоновПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если не Копирование и НоваяСтрока Тогда
		Элемент.ТекущиеДанные.Использовать = True;
	Конецесли;
КонецПроцедуры

Процедура ТаблицаРезультатаПриАктивИзацииЯчейки(Элемент)
	ЗаполнитьЗначениеАгрегатнойФункции();
КонецПроцедуры


Процедура ОсновнаяПанельПриСменеСтраницы(Элемент, ТекущаяСтраница)
	Если Модифицированность и ТекущаяСтраница = 0 Тогда
		кзСохранитьШаблоны(ЭтаФорма);
	КонецЕсли;
КонецПроцедуры

Процедура кзСозданиеДБФ(ФайлДБФ, ИмяФайлаВыгрузки, ТекКолонки)
	
	Для каждого Колонка Из ТекКолонки Цикл
		ИмяПоля = Колонка.Имя;
		ТипПоля = Колонка.ЭлементУправления.ТипЗначения;
		Длина = 0;
		ДлинаДЧ = 0;
		Если ТипПоля.СодержитТип(Тип("Строка")) Тогда
			ТипДБФ = "S";
			Длина = ТипПоля.КвалификаторыСтроки.Длина;
		ИначеЕсли ТипПоля.СодержитТип(Тип("Число")) Тогда
			ТипДБФ = "N";
			Длина = ТипПоля.КвалификаторыЧисла.Разрядность;
			ДлинаДЧ = ТипПоля.КвалификаторыЧисла.РазрядностьДробнойЧасти;
			Если Длина = 0 Тогда
				СтрокаЗапрсаСВыразить = ЭтаФорма.ПолучитьТекстЗапроса(Истина);
				ПоложениеПоляВЗапросе = Найти(СтрокаЗапрсаСВыразить, ИмяПоля);
				СтрокаЗапрсаСВыразить = Сред(СтрокаЗапрсаСВыразить,1, ПоложениеПоляВЗапросе - 1);
				КоличествоВыразить = СтрЧислоВхождений(СтрокаЗапрсаСВыразить, "ВЫРАЗИТЬ(");
				Для зю = 1 по КоличествоВыразить Цикл
					ПоложениеВыразитьВЗапросе = Найти(СтрокаЗапрсаСВыразить, "ВЫРАЗИТЬ(");
					СтрокаЗапрсаСВыразить = Прав(СтрокаЗапрсаСВыразить,СтрДлина(СтрокаЗапрсаСВыразить) - ПоложениеВыразитьВЗапросе - 8);
				КонецЦикла;
				ПоложениеЧИСЛОВЗапросе = Найти(СтрокаЗапрсаСВыразить, "ЧИСЛО(");
				СтрокаЗапрсаСВыразить = Прав(СтрокаЗапрсаСВыразить,СтрДлина(СтрокаЗапрсаСВыразить) - ПоложениеЧИСЛОВЗапросе - 5);
				ПоложениеЗапятушки = Найти(СтрокаЗапрсаСВыразить, ", ");
				Длина = Число(Лев(СтрокаЗапрсаСВыразить,ПоложениеЗапятушки - 1));
				СтрокаЗапрсаСВыразить = Прав(СтрокаЗапрсаСВыразить,СтрДлина(СтрокаЗапрсаСВыразить) - ПоложениеЗапятушки);
				ПоложениеЗапятушки = Найти(СтрокаЗапрсаСВыразить, ")");
				ДлинаДЧ = Число(Лев(СтрокаЗапрсаСВыразить,ПоложениеЗапятушки - 1));
			КонецЕсли;
		ИначеЕсли ТипПоля.СодержитТип(Тип("Булево")) Тогда
			ТипДБФ = "L";
		ИначеЕсли ТипПоля.СодержитТип(Тип("Дата")) Тогда
			ТипДБФ = "D";
		Иначе
			ТипДБФ = "S";
			Длина = 200;
		КонецЕсли;
		ФайлДБФ.поля.Добавить(ИмяПоля, ТипДБФ, Длина, ДлинаДЧ);
	КонецЦикла;
	
	ФайлДБФ.Кодировка = КодировкаXBase.OEM;
	ФайлДБФ.СоздатьФайл(ИмяФайлаВыгрузки);
	

КонецПроцедуры

Процедура кзКоманднаяПанельФормыВыгрузитьВДБФ(Кнопка)
	
	Если СпособВыгрузки <> 1 Тогда
		ПоказатьПредупреждение(, "Выгрузка работает только для способа выгрузки ""Список"" ", 10);
		Возврат;
	КонецЕсли;
	
	ТекКолонки = ЭлементыФормы[мИмяРеквизитаТаблицы].Колонки;
	Если ТекКолонки.Количество() = 0 Тогда
		Сообщить("Нет результата запроса");
		Возврат;
	КонецЕсли;
	
	Режим = РежимДиалогаВыбораФайла.Открытие;
	ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(Режим);
	ДиалогОткрытияФайла.ПолноеИмяФайла = "";
	Фильтр = "Файлы DBF(*.dbf)|*.dbf";
	ДиалогОткрытияФайла.Фильтр = Фильтр;
	ДиалогОткрытияФайла.МножественныйВыбор = Ложь;
	ДиалогОткрытияФайла.Заголовок = "Выберите файл для выгрузки";
	Если ДиалогОткрытияФайла.Выбрать() Тогда
		ИмяФайлаВыгрузки = ДиалогОткрытияФайла.ПолноеИмяФайла;
	Иначе
		Сообщить("Файл не выбран");
		Возврат;
	КонецЕсли;
	Файло = Новый Файл(ИмяФайлаВыгрузки);
	
	ФайлДБФ = Новый xBase;
	ФайлДБФ.АвтоСохранение = Истина;
	Если Файло.Существует() Тогда
		ОтветНаВопрос = Вопрос("Файл с таким именем существует. Дописать данные в файл?", РежимДиалогаВопрос.ДаНетОтмена);
		
		Если ОтветНаВопрос = КодВозвратаДиалога.Да Тогда
			//Проверим структуру
			ФайлДБФ.ОткрытьФайл(ИмяФайлаВыгрузки);
			Для каждого Колонка Из ТекКолонки Цикл
				Если ФайлДБФ.поля.Найти(Колонка.Имя) = Неопределено Тогда
					Сообщить("Не совпадает структура файла ДБФ с результатом запроса");
					Возврат;
				КонецЕсли;
			КонецЦикла;
			
		ИначеЕсли ОтветНаВопрос = КодВозвратаДиалога.Нет Тогда
			кзСозданиеДБФ(ФайлДБФ, ИмяФайлаВыгрузки, ТекКолонки);
		Иначе
			Возврат;
		КонецЕсли;
	Иначе
		кзСозданиеДБФ(ФайлДБФ, ИмяФайлаВыгрузки, ТекКолонки)
	КонецЕсли;
	
	лТаблицаРезультата = Вычислить(мИмяРеквизитаТаблицы);
	
	мФормаПрогрессора.Открыть();
	мФормаПрогрессора.ЭлементыФормы.Индикатор.МаксимальноеЗначение = лТаблицаРезультата.Количество();
	мФормаПрогрессора.НадписьСостоянияИндикатораОбщая = "Формирование печатной формы:";
	Инд = 1;
	мФормаПрогрессора.ЗначениеИндикатора = 1;
	
	Для каждого СтрокаТЧ Из лТаблицаРезультата Цикл
		
		Инд = Инд + 1;
		
		ОбработкаПрерыванияПользователя();
		
		мФормаПрогрессора.ЗначениеИндикатора = Инд;
		мФормаПрогрессора.НадписьСостоянияИндикатораТекущая = "Выгружается " + Инд + " - я строка";
		
		ФайлДБФ.Добавить();
		Для каждого Колонка Из ТекКолонки Цикл
			ФайлДБФ[Колонка.Имя] = СтрокаТЧ[Колонка.Имя];
		КонецЦикла;
	КонецЦикла;
	ФайлДБФ.ЗакрытьФайл();
	
	Сообщить("Даные выгружены в файл:" + ИмяФайлаВыгрузки);
	
	мФормаПрогрессора.Закрыть();
	
КонецПроцедуры

Процедура кзКоманднаяПанельФормыДиаграмма(Кнопка)
	
	ФормаСДиаграммой = ПолучитьФорму("Точка", ЭтаФорма);
	Если НЕ ФормаСДиаграммой.Открыта() Тогда
		ФормаСДиаграммой.ЭлементыФормы.Добавить(Тип("Диаграмма"), "Диаграмма");
		ФормаСДиаграммой.ЭлементыФормы.Диаграмма.Высота = ФормаСДиаграммой.Высота;
		ФормаСДиаграммой.ЭлементыФормы.Диаграмма.Ширина = ФормаСДиаграммой.Ширина;
	КонецЕсли;
	
	Диаграмма = ФормаСДиаграммой.ЭлементыФормы.Диаграмма;
	Диаграмма.ТипДиаграммы = ТипДиаграммы.График;
	Диаграмма.УстановитьПривязку(ГраницаЭлементаУправления.Лево, ФормаСДиаграммой.Панель, ГраницаЭлементаУправления.Лево);
	Диаграмма.УстановитьПривязку(ГраницаЭлементаУправления.Право, ФормаСДиаграммой.Панель, ГраницаЭлементаУправления.Право);
	Диаграмма.УстановитьПривязку(ГраницаЭлементаУправления.НИз, ФормаСДиаграммой.Панель, ГраницаЭлементаУправления.НИз);
	Диаграмма.УстановитьПривязку(ГраницаЭлементаУправления.Верх, ФормаСДиаграммой.Панель, ГраницаЭлементаУправления.Верх);
	
	Если СпособВыгрузки <> 1 Тогда
		ПоказатьПредупреждение(, "Выгрузка работает только для способа выгрузки ""Список"" ", 10);
		Возврат;
	КонецЕсли;
	
	лТаблицаРезультата = Вычислить(мИмяРеквизитаТаблицы);
	
	ТекКолонки = ЭлементыФормы[мИмяРеквизитаТаблицы].Колонки;
	Если ТекКолонки.Количество() = 0 ИЛИ лТаблицаРезультата.Количество() = 0 Тогда
		Сообщить("Нет результата запроса");
		Возврат;
	КонецЕсли;
	
	//Формируем список серий
	СписокСерий = новый СписокЗначений;
	Для каждого Колонка Из ТекКолонки Цикл
		Попытка
		    a = Число(лТаблицаРезультата[0][Колонка.Имя]);
			СписокСерий.Добавить(Колонка.Имя,Колонка.Имя);
		Исключение
		КонецПопытки; 
	КонецЦикла; 
	
	Если НЕ СписокСерий.ОтметитьЭлементы() Тогда
		Возврат;
	КонецЕсли;
	
	мФормаПрогрессора.Открыть();
	мФормаПрогрессора.ЭлементыФормы.Индикатор.МаксимальноеЗначение = лТаблицаРезультата.Количество();
	мФормаПрогрессора.НадписьСостоянияИндикатораОбщая = "Формирование данных для диаграммы:";
	Инд = 0;
	
	Диаграмма.Очистить();
	
	
	Для каждого ТекЭлемент Из СписокСерий Цикл
		Диаграмма.УстановитьСерию(ТекЭлемент.Значение);
	КонецЦикла; 
	
	Для каждого СтрокаТЧ Из лТаблицаРезультата Цикл
		
		Инд = Инд + 1;
		
		ОбработкаПрерыванияПользователя();
		
		мФормаПрогрессора.ЗначениеИндикатора = Инд;
		мФормаПрогрессора.НадписьСостоянияИндикатораТекущая = "Выгружается " + Инд + " - я строка";
		
		
		Для каждого Серия Из Диаграмма.Серии Цикл
			Диаграмма.УстановитьТочку();
			//УстановитьЗначение
		КонецЦикла; 
	
	КонецЦикла;
	
	НоваяСерия = Диаграмма.Серии.Добавить();
	
	ФормаСДиаграммой.Открыть();
	
	мФормаПрогрессора.Закрыть();
	
КонецПроцедуры

Процедура кзКоманднаяПанельФормыСформироватьКодДляОтладки(Кнопка)
	//Определяем путь к файлу

	ТекИмяФайла = ВосстановитьЗначение("КонсольЗапросов_ОтЛавелина_ПутьКСформироватьКодДляОтладки");
	Если гЗначениеНеЗаполнено(ТекИмяФайла) Тогда
		ТекИмяФайла = "";		
	КонецЕсли;
	
	Длг = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Длг.Заголовок = "Выберите файл обработки Консоль запросов (consquery81.epf)";
	Если Длг.Выбрать() Тогда
		ТекИмяФайла = Длг.ПолноеИмяФайла;
		СохранитьЗначение("КонсольЗапросов_ОтЛавелина_ПутьКСформироватьКодДляОтладки",ТекИмяФайла);
	Иначе
		Возврат;
	КонецЕсли;
	
	ТекстСообщения = "Для отладки запроса необходимо:
		|1. Установить точку останова перед выполнение запроса (Например, перед строкой ""Запрос.Выполнить()"". 
		|2. Когда точка останова отработает, нажмите Shift + F9 (""Вычислить выражение"")).
		|3. В поле ввода ""Выражение"" ввести следующий текст: 
		|ВнешниеОбработки.Создать(""" + ТекИмяФайла + """).гОткрытьФормуДляОтладки(ЗАПРОС)
		|(Вместо слова ЗАПРОС подставляем имя переменной, которая содержит значение запроса, который предстоит отладить) и нажать кнопку ""Рассчитать"".
		|4. Продолжить отладку(F5).
		|5. В результате в режиме предприятия откроется консоль запросов с заполненными параметрами и текстом запроса.";
		
	Если Вопрос("Скопировать в буфер [Да] / Вывести код с инструкцией [Нет]?", РежимДиалогаВопрос.ДаНет,,, "Код для отладки запроса") = КодВозвратаДиалога.Да Тогда
		кзСкопироватьВБуфер(ПолучитьФорму("Точка"), "ВнешниеОбработки.Создать(""" + ТекИмяФайла + """).гОткрытьФормуДляОтладки(ЗАПРОС)");
	Иначе
		ТД = Новый ТекстовыйДокумент(); 		
		ТД.УстановитьТекст(ТекстСообщения);
		ТД.Показать(,); 
	КонецЕсли;
КонецПроцедуры

Процедура УдалитьСтраницыСВнешниимРезультатами()
	ТаблицыСРезультатами = Новый СписокЗначений;
	Для каждого ТекЭлемент Из ЭлементыФормы Цикл
		Если Найти(ТекЭлемент.Имя, "ТаблицаСРезультатом_") > 0 Тогда
			ТаблицыСРезультатами.Добавить(ТекЭлемент);
		КонецЕсли;			
	КонецЦикла; 
	Для каждого ТекЭлемент Из ТаблицыСРезультатами Цикл
		ЭлементыФормы.Удалить(ТекЭлемент.Значение);
	КонецЦикла; 
	
	СтраницыСРезультатами = Новый СписокЗначений;
	Для каждого ТекСтраница Из ЭлементыФормы.ПанельРезультата.Страницы Цикл
		Если Найти(ТекСтраница.Имя, "СтраницаСРезультатом_") > 0 Тогда
			СтраницыСРезультатами.Добавить(ТекСтраница);
		КонецЕсли;			
	КонецЦикла; 
	Для каждого ТекСтраница Из СтраницыСРезультатами Цикл
		ЭлементыФормы.ПанельРезультата.Страницы.Удалить(ТекСтраница.Значение);
	КонецЦикла; 
КонецПроцедуры
	
Процедура СпецМенюЗакрытьТекущуюстраницу(Кнопка)
	ТекСтраница = ЭлементыФормы.ПанельРезультата.ТекущаяСтраница;
	Если ТекСтраница <> Неопределено Тогда
		Если Найти(ТекСтраница.Имя, "СтраницаСРезультатом_") > 0 Тогда
			ЭлементыФормы.Удалить(ЭлементыФормы[СтрЗаменить(ТекСтраница.Имя, "СтраницаСРезультатом_", "ТаблицаСРезультатом_")]);
			ЭлементыФормы.ПанельРезультата.Страницы.Удалить(ТекСтраница);
			Если ЭлементыФормы.ПанельРезультата.Страницы.Количество()<3 Тогда
				ЭлементыФормы.ПанельРезультата.ТекущаяСтраница = ЭлементыФормы.ПанельРезультата.Страницы[0];
			Конецесли;
		Иначе
			ПоказатьПредупреждение(, "Данная страница не подлежит удалению", 10);
		Конецесли;
	Конецесли;
КонецПроцедуры

Процедура СпецМенюЗакрытьвсеВнешниеРезультаты(Кнопка)
	Если Вопрос("Закрыть все страницы с внешниим результатами?", РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Да Тогда
		УдалитьСтраницыСВнешниимРезультатами();
		ЭлементыФормы.ПанельРезультата.ТекущаяСтраница = ЭлементыФормы.ПанельРезультата.Страницы[0];
	КонецЕсли;
КонецПроцедуры

Процедура СформироватьМеню(МассивМенюДляДобавления) Экспорт
	Для каждого ТекущееМеню Из МассивМенюДляДобавления Цикл
		лДеревоМеню = гПолучитьМеню(ТекущееМеню.Имя);
		лКореньМеню = гЗаполнитьМенюПоДереву(ТекущееМеню.Контейнер, ТекущееМеню.Индекс, лДеревоМеню);
	КонецЦикла; 
КонецПроцедуры // СформироватьМеню()


//////////////////////////////////////////////
// ЗавершенияАсинхронныхВызовов

Процедура ПоказатьВыборЭлементаОбработкаВыбора(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныйЭлемент = Неопределено Тогда 
			лИмяВременногоФайла = ПолучитьИмяВременногоФайла();
			ДополнительныеПараметры.FTPСоединение.Получить(ВыбранныйЭлемент, лИмяВременногоФайла);
	//		ЗагрузитьЗапросы(лИмяВременногоФайла, мОперацииСЗапросами.ЗагрузитьСFTP);
	//		ИмяФайлаСЗапросом = Неопределено;
	//		ОбновитьЗаголовок();	
	//		УдалитьФайлы(лИмяВременногоФайла);
	//		FTPСоединение = Неопределено;
	//		Состояние("Запрос загружен с ftp.", 100);
	КонецЕсли;
	
КонецПроцедуры // ПоказатьВыборЭлементаОбработкаВыбора()

Процедура ПослеЗакрытияВопроса(Результат, Параметры) Экспорт
	Если Параметры.Режим = "СоздатьНовыйФайл" Тогда 
		Если Результат = КодВозвратаДиалога.Да Тогда
			Если ПроверитьМодифицированность() Тогда
				СоздатьНовыйФайлЗапросов();
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

//Процедура ПослеЗакрытияВопросаВыгружатьПодчиненныеЗапросы(Результат, Параметры) Экспорт
//	
//	Если Результат = КодВозвратаДиалога.Отмена Тогда 
//		Возврат;
//	КонецЕсли;
//	
//	Если ПроверитьМодифицированность(Истина) Тогда
//		ПолучитьСсылкуИзОблака(Результат = КодВозвратаДиалога.Да);
//	КонецЕсли;
//	
//КонецПроцедуры // ПослеЗакрытияВопросаВыгружатьПодчиненныеЗапросы()

Процедура ПослеВводаСтроки(Строка, Параметры) Экспорт
	Если Строка = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	лРежим = Неопределено;
	Если Параметры.Свойство("ВставитьСсылку", лРежим) Тогда 
		Возврат;
	КонецЕсли;
		
	Если лРежим = "ПоказатьТипЗначения" Тогда 
	КонецЕсли;
КонецПроцедуры // ПослеВводаСтроки()

Процедура ОбработкаОповещения(ИмяСобытия, Параметры, Источник)
	Если ИмяСобытия = "НеобходимоПереподключиться" Тогда 
		ПодключенияКОблакуДействие();
	ИначеЕсли ИмяСобытия = "ДобавитьЗапросИзПредпросмотра" Тогда 
		ЗагрузитьЗапросыИзОблака(Параметры.идПакета, Параметры.идЗапроса, Параметры.идКода, Ложь, ЭлементыФормы.ДеревоЗапросов.ТекущаяСтрока.Родитель)
	ИначеЕсли ИмяСобытия = "ОбновитьНастройкиПрокси" Тогда 
		мНастройкиПрокси = Параметры;
		СохранитьЗначение("Consquery_cloud_НастройкиПрокси", мНастройкиПрокси);
	ИначеЕсли ИмяСобытия = "ОбновитьИмяПользователяВОблаке" Тогда 
		Если Параметры.ИмяПользователя <> мИмяПользователяВОблаке Тогда 
			Если Не ЗначениеЗаполнено(Параметры.ИмяПользователя) Тогда  мИмяФайла = ""; КонецЕсли;
			мИмяПользователяВОблаке = Параметры.ИмяПользователя;
			ОбновитьЗаголовок();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры // ОбработкаОповещения()

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

мИмяРеквизитаТаблицы = гПолучитьСвойстваРеквизитаТаблицаРезультата().ИмяРеквизита;

мМассивУдаленныхСтрок = Новый Массив;